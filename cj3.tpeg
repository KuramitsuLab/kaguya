/* CJ Controlled Japanese 文法 */

/*
トップの規則
入力をClauseの繰り返しとみる
*/

Sentence = {
    (TEN / Chunk IGNORED? / Error)* EOS
    #S
}

Chunk =
    / Conjunction TEN // 接続詞 TEN付きの優先度をあげる
    / Adverb       // 副詞
    / Measure      // 助数詞
    / Adnominal    // 連体詞は先, 「多くの人」が形容詞にマッチする
    / NounChunk0   // 優先名詞チャンク　「組み合わせ」を先にする
    / AdjectiveChunk  // 形容詞節
    / VerbChunk    // 動詞節
    / NounChunk    // 名詞節, 形容動詞節
    / Conjunction  // 接続詞 優先度を下げる

// 接辞

// 主格　Aは,   結果のもっとも
Subject = { 
    ('こそ' / !'もっとも' !'もの' [はがも]) 
    There? #Subject 
}

// 所有格 の
OfCase = 
    / { 'の' XNoun #That } // Aのは Aの(もの)に
    / { 'の' !'み' Like? #Of }   // 

Like = { 'よう' XNoun? #Like }

Suffix = 
    / Subject
    / OfCase

XNoun = // もの
    / IsMood
    / { ('から' / 'より') Suffix? #From }
    / { 'まで' Suffix? #Until }
    / { 'へ' Suffix? #To }         // Aへの, Aへは
    / { 'に' (Against / About / Let / By / Despite / Suffix)? #Adverb }     // Aにの~ , Aには~
    / { 'と' 'か'? (Let / Suffix)? #Object }     // Aとの~ , Aとは~
    / { 'で' (There/Suffix)? #Object }     // Aでの~,
    / { ('かぎり' / '限り') XNoun? #AsLong }  // その場かぎり
    / { 'か' ('どうか'/'否か') XNoun? #Whether }  // 可能かどうか
    / { 'か' XNoun? #OrElse }  // 可能か不可能か
    / { ('のみ'/'だけ'/'ばかり'/'さえ') XNoun? #Only }    // 彼のみを
    / { 'らし' XNot #Sound }   // 単語らしい
    / { 'ごと' XNoun? #Each }    // 単語ごと+で
    / { 'など' XNoun? #Etc }    // 単語など+で
    / { 'なし' XNoun? #No }    // 単語なし+の
    / { 'そう' XNoun? #Seem }    // 正確そう
    / { 'さ' !'れ' XNoun? #Noun }  // 正確さ
    / { 'なら' 'ば'? #If }  // 正確ならば
    / { 'な' #Adjective }  // 正確な
    / { 'を' #Object }
    / { 'や' #AndOr }
    / { 'または' #Or }
    / Suffix

XBase = // する+ した+
    / MayBe / Should
    / { 'らし' XNot #Sound }   // するらしい
    / { ('のみ'/ 'だけ') XNoun? #OnlyThat }
    / { (('ため' / '為') XNoun? ) / ('ので'/'から') #Because }
    / { ('かぎり' / '限り') XNoun? #AsLong }
    / { 'の'? 'か' ('どうか'/'否か')? XNoun? #Whether }
    / { (('とき' / 'と' TEN / '時' / '場合' / '際' / 'ところ') XNoun?) / 'のなら' 'ば'? #If }
    / { ('とおり' / 'よう') XNoun? #As }
    / { 'と' 'か'? XNoun? #That }
    / { 'まで' XNoun? #Until }
    / { 'まま' XNoun? #AsIs }
    / { 'には' #That }
    / { 'の' XNoun? #That }

XPast = // した
    / { 'り' #AndThen }
    / { 'な'? 'ら' 'ば'? #If } 
    / XBase

Let = // と +
    { 'する' XBase? / 'し' ('て' Suffix? / 'た' XPast? )? #Let }

Against = // に + 
    { '対する' / '対し' ('て' Suffix? / 'た' XPast? )? #Against }

About = // に + 
    { 'ついて' Suffix? / '関し' ('て' Suffix? / 'た' XPast? )? #About }

By = // に + 
    { 'よる' / 'よっ' ('て' Suffix? / 'た' )? / 'より' #About }

Despite = // に + 
    { ('かか'/'関') 'わらず' / '関係なく' #Despite }

MayBe =
    { 'かも' ('知'/'し') 'れ' ('ない' / 'ません' / Mood) #May }

Should = 
    { ('べき' / 'べし' / 'はず' ) XNoun? #Should }

Be = // すべき
    / { 'で' ('すら' / 'さえ') #Though }     // 首相ですら 
    / { 'でな' XNot #Not} 
    / { ('でしょう' / 'だろう') XBase? #WouldBe }
    / { ('でした' / 'だった') XPast? #Past }
    / { ('です' / 'だ' !'け') XBase? #Base }

IsMood = &[でだ] Be / MayBe

There = {
    'あ' AMood 
    #There
}

IGNORED = 
    / 'です' 
    / 'ください'

// CJルール 接続詞
// 優先度
Conjunction = { // e.g., こうして
    @choice('cjdic/CONJ.txt')
    #Conjunction 
}

// CJルール 副詞
// ADVERB.txt 副詞辞書
// ADVERB_NOUM.txt 
// 副詞名詞辞書 漢字、もしくはカタカナで終わる副詞
// 例. 過去に戻る 過去、言った

Adverb = {   // e.g., 順次
    (@choice('cjdic/ADVERB.txt') // 次に
    / @choice('cjdic/ADVERB_NOUN.txt')) XNoun?  // 順次、
    #Adverb 
}

// 助数詞
// Code に数量を表す語が続く

Measure = {
    UnitPrefix?
    (Code / NumericNoun ) 
    !NONUNIT Unit
    UnitPostfix?
    XNoun?
    #Measure
}

UnitPrefix = 
    / { '約' / 'およそ' #Aprox }
    / { 'もう' / 'さらに' / '更に' #More }
    / { '高々'/ 'たかだか' / '多くとも' / '最大' #AtMost }
    / { '少なくとも' / '最小' #AtLeast }

NumericNoun = {
    '複数' / [一二三毎数先今次]  // 複数回 毎回 今回
    #Noun
}

// 単位
// カタカナ (例. ドル、メートル)
// もしくは、cjdic/UNIT 単位辞書に登録する

Unit = {
    @choice('cjdic/UNIT.txt') / KATA+
    #Unit
}

NONUNIT = @choice('cjdic/NONUNIT.txt')

UnitPostfix = 
    / { '程' / 'ほど' #Aprox }
    / { 'ごと' / '毎' #Each }
    / { '以上' #Gte }
    / { '以下' #Lte }
    / { '未満' #Lt }

// 連体詞
// 名詞があとに続くが、単独では名詞でも形容動詞でも解釈できない
// 「こ」の 連体詞　「一部」の　名詞＋「の」
// 辞書 ADNOUN.txt
// 「~たる」は除外　微々たる, 切々たる 
// 「~しき」は除外　奇しき, 悪しき 

Adnominal = { 
    @choice('cjdic/ADNOUN.txt') 
    XNoun?   // '同じ' のように名詞化するものもある
    #Adjective 
}

/*
名詞 NounChunk 名詞らしいもので始まる
優先度 0
*/

NounChunk0 = {
    VerbalNoun (XNoun / TEN / EOF)
    / DictNoun XNoun?
    // 「組み込み」ますのように動詞でないことを保証する
    #NChunk
}

// 動詞から派生した複合名詞 
VerbalNoun = {
    XVERB XVERB 
    #Noun
}

// XVERB 
// 呼び + 出し のようにふたつ組み合わせて名詞になる動詞
// 組み+ 込み 戻り+値 上+ 付け   
// 「~ ます」に続く

XVERB = @choice('cjdic/XVERB.txt') 

NounChunk = { 
    Noun XNoun? 
    #NChunk 
} 

example Sentence ハワイについて
example Sentence ハワイに着いて

/*
名詞 Noun
*/

// CJルール 名詞
// 平仮名が含まれない字句は外来語（名詞）とする
//   ForeignNoun  例. クラス・メソッド
// 平仮名が含まれる名詞は
//  あらかじめ辞書に登録する  DictNone 
//  「奥の細道」のようにカッコで書く  NewNoun

Noun = 
    / Code
    / NewNoun
    / DictNoun
    / PatNoun

NounVerb =  // サ変動詞 〜する
    (NewNoun / DictNoun / PatNoun ) &([さしすざじず]/'でき')

NewNoun =
    / '\「' { (!'\」' .)* #Noun } '\」'
    / '\『' { (!'\』' .)* #Noun } '\』'
    / '\【' { (!'\】' .)* #Noun } '\】'
    / '\〔' { (!'\〕' .)* #Noun } '\〕'

// CJルール 名詞
// 辞書に含まれていない名詞
// 漢字、もしくはカタカタで始まる
// ひらがなは XNOUN が接尾に含まれる

PatNoun = {
    XVERB ( XVERB / NONHIRA+ / &[はもにをがと] / EOF )
    / NONHIRA (XVERB / XNOUN / NONHIRA)* 
    #Noun
}

// XVERB 以外の名詞の接尾詞
XNOUN = !'らしい' @choice('cjdic/XNOUN.txt')

DictNoun = {
    @choice('cjdic/NOUN.txt') / @choice('cjdic/NOUNADJ.txt') XNOUN?
    #Noun
}

/*
動詞の未然形活用
未然形はほぼ確実に助動詞が繋がるため先読みで誤マッチを抑制している
これで「止まる」の先頭文字列「止ま」が「止む」の未然形「止ま」にマッチしない
*/

VerbChunk = {
    NounVerb XNounVerb // 愛する　と　愛す（サ行５段）
    / There / Do / Come
    / Verb5KA XVerb5KA
    / Verb5SA XVerb5SA
    / Verb5TA XVerb5TA
    / Verb5NA XVerb5NA
    / Verb5MA XVerb5MA
    / Verb5RA XVerb5RA
    / Verb5WA XVerb5WA
    / Verb5GA XVerb5GA
    / Verb5BA XVerb5BA
    / Verb1 XVerb1
    #VChunk  
}

Verb5KA = { @choice('dic/Verb/VERB5KA.txt') &[か-こい] #VerbKA }
Verb5SA = { @choice('dic/Verb/VERB5SA.txt') &[さ-そ] #VerbSA }
Verb5TA = { @choice('dic/Verb/VERB5TA.txt') &[た-とっ] #VerbTA }
Verb5NA = { @choice('dic/Verb/VERB5NA.txt') &[な-のん] #VerbNA }
Verb5MA = { @choice('dic/Verb/VERB5MA.txt') &[ま-もん] #VerbMA }
Verb5RA = { @choice('dic/Verb/VERB5RA.txt') &[ら-ろっ] #VerbRA }
Verb5WA = { @choice('dic/Verb/VERB5WA.txt') &[わいうえおっ] #VerbWA }
Verb5GA = { @choice('dic/Verb/VERB5GA.txt') &[が-ごい] #VerbGA }
Verb5BA = { @choice('dic/Verb/VERB5BA.txt') &[ば-ぼん] #VerbBA }
Verb1 = { !'それ' @choice('dic/Verb/VERB1.txt') #Verb1 }

XNot = //な
    / { 'ければ' #If }
    / { 'くな' XNot #Not }
    / { 'くても' #EvenIf }
    / { 'く' 'て'? #And }
    / { 'い' XBase? #Base }
    / { 'かった' XPast? #Past }

PoliteMood =
    / { ('ます' / 'ましょう') XBase? #Base }
    / { 'ません' 'でした'? XBase? #Not }
    / { 'ましたら' #If }
    / { 'ました' XPast? #Past }
    / { 'ましても' #EvenIf }
    / { 'まして' #And }

Mood = // でき+　　られ+
    / &'ま' PoliteMood
    / { 'な' XNot #Not }
    / { 'る' XBase? #Base }
    / { 'た' XPast? #Past }
    / { 'れば' #If }
    / { 'てから' #After }
    / { 'ても' #EvenIf }
    / { ('て' XMood?)?  #And }

MOOD1 = [まなるたれて]

AMood = // あ+る
    / 'り' &'ま' PoliteMood
    / { 'る' XBase? #Base }
    / { 'った' XPast? #Past }
    / { 'れば' #If }
    / { 'ってから' #After }
    / { 'っても' #EvenIf }
    / { 'って' #And }

// 補助動詞
XMood = // て
    / { 'い' Mood #Now }    // 笑っている
    / { 'み' Mood #TryTo }  // 笑ってみる
    / There                 // 笑ってある 
    / Come                  // 笑ってくる


ASuffix = // 書か+ない   探さ+ない
    / { 'な' XNot #Not }
    / { 'ず' #Not }
    / { 'さ'? 'せ' 'られ'? Mood #Make }
    / { 'れ' &MOOD1 Mood #Been }   // これる vs. これは

IXSuffix = // 書い+て   探し+て　
    / { 'た' XPast? #Past }
    / { 'てから' #After }
    / { 'ても' #EvenIf }
    / { 'て' XMood? #And }

IXDSuffix = // 遊んだ   遊んで
    / { 'だ' XPast? #Past }
    / { 'でから' #After }
    / { 'でも' #EvenIf }
    / { 'で' XMood? #And }

ISuffix = // 書き+つつ  探し+つつ　
    / &'ま' PoliteMood
    / { 'た' XNot #WantTo }   // 書きたい
    / { ('やす' / 'よ' ) XNot #EasyTo }   // 書きやすい
    / { ('にく' / 'がた' / 'づら') XNot #HardTo } //書きがたい
    / { 'がち' XNoun? #Likely } //休みがちだ
    / { ('つつ' / 'ながら') #While }
    / { XNoun / TEN / EOF #Noun }   // 書出し
    / { !MOOD1 #And }

USuffix = // 書く　探す
    / { XBase? #Base }

ESuffix = // 書け+ば  探せ+ば
    / { 'ば' #If }

OSuffix = // 書こ+う　探そ+う
    / { 'う' XBase? #Will }

XNounVerb = // 例. 行動する 論じる
    / [すず] ('る' USuffix / Should / 'れ' ESuffix)
    / [しじ] (ASuffix / 'よ' OSuffix / IXSuffix / ISuffix )
    / { 'され' Mood #Been }
    / { 'でき' Mood #Can }

Do = // 例. 参考に+する
    / { 'す' ('る' USuffix / Should / 'れ' ESuffix) #Do }
    / { 'し' (ASuffix / 'よ' OSuffix/ IXSuffix / ISuffix ) #Do }
    / { 'され' Mood #Been }
    / { 'でき' Mood #Can }

// カ行変格活用動詞
Come = 
    / { 'こ' ASuffix #Come }
    / { 'き' IXSuffix #Come }
    / { 'くる' XBase? #Come }
    / { 'くれ' { 'ば' #If} #Come }


XVerb5KA = // 書
    / 'か' ASuffix
    / 'き' ISuffix
    / 'い' IXSuffix
    / 'く' USuffix
    / 'け' ESuffix
    / 'こ' OSuffix

XVerb5SA = // 探 出
    / 'さ' ASuffix  // ない
    / 'し' IXSuffix // て
    / 'し' ISuffix  // ながら
    / 'す' USuffix  // 
    / 'せ' ESuffix  // ば
    / 'こ' OSuffix  // う


XVerb5TA = // 勝
    / 'た' ASuffix  // ない
    / 'ち' ISuffix  // ながら
    / 'っ' IXSuffix // て
    / 'つ' USuffix  // 
    / 'て' ESuffix  // ば
    / 'と' OSuffix  // う

XVerb5NA = // 死
    / 'な' ASuffix  // ない
    / 'に' ISuffix  // ながら
    / 'ん' IXDSuffix // て
    / 'ぬ' USuffix  // 
    / 'ね' ESuffix  // ば
    / 'の' OSuffix  // う

XVerb5MA = // 読
    / 'ま' ASuffix  // ない
    / 'み' ISuffix  // ながら
    / 'ん' IXDSuffix // て
    / 'む' USuffix  // 
    / 'め' ESuffix  // ば
    / 'も' OSuffix  // う

XVerb5RA = // 切
    / 'ら' ASuffix  // ない
    / 'り' ISuffix  // ながら
    / 'っ' IXSuffix // て
    / 'る' USuffix  // 
    / 'れ' ESuffix  // ば
    / 'ろ' OSuffix  // う

XVerb5WA = // 笑
    / 'わ' ASuffix  // ない
    / 'い' ISuffix  // ながら
    / 'っ' IXSuffix // て
    / 'う' USuffix  // 
    / 'え' ESuffix  // ば
    / 'お' OSuffix  // う

XVerb5GA = // 防
    / 'が' ASuffix  // ない
    / 'ぎ' ISuffix  // ながら
    / 'い' IXDSuffix // て
    / 'ぐ' USuffix  // 
    / 'げ' ESuffix  // ば
    / 'ご' OSuffix  // う

XVerb5BA = // 遊
    / 'ば' ASuffix  // ない
    / 'び' ISuffix  // ながら
    / 'ん' IXDSuffix // て
    / 'ぶ' USuffix  // 
    / 'べ' ESuffix  // ば
    / 'ぼ' OSuffix  // う

// 「それる」は除外

XVerb1 = // 着る 述べ
    / 'る' USuffix  // 
    / 'れ' ESuffix  // ば
    / { 'られ' Mood #Been }   // 述べられています
    / 'よ' OSuffix  // う
    / ASuffix  // ない
    / IXSuffix // て
    / &HIRA ISuffix  // 視野が視るにマッチするのを防ぐ

/* 形容詞 */

AdjectiveChunk = {
    Adjective ('そう' XNounAdj / XAdjective )
    #AChunk
}

// 形容詞: 美し

Adjective = {
    ADJ
    #Adjective
}

ADJ = @choice('dic/ADJ.txt') &ADJ_CHECK
ADJ_CHECK = 'そう' / [さかくけい]

XAdjective = 
    / XNot 
    / { 'さ' !'れ' XNoun? #Noun }
    / { 'そう' XNounAdj #Noun }

XNounAdj = //素敵
    / XNoun // だ、です、かも知れない
    / { 'ならば' #If }

/* コードは名詞扱い */

Code = {
    [A-Za-z0-9.(){}, +\-*/%\\]+
    #Code
}

// CJ.TPEG マッチしない名詞はエラーとしてスキップする

Error = {
    HIRA+
    #err
}

/* 文字コード */

Ten = { TEN #Ten }

SPC = [ ]
EOF = !.
EOS = [。．.\n]? EOF
TEN = [、，,\n]
KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]
HIRA = [ぁ-ん]
NONHIRA = (!HIRA !TEN .)
ALPHA = [a-zA-Zａ-ｚＡ-Ｚ]
NUM = [\.,0-9０-９一二三四五六七八九十百千万億兆京垓秭穰溝澗正載極]
SYMBOL = [^＾~〜\-ー_＿!！?？#＃$＄&＆=＝@＠*＊+＋/・]


// 多少異なっている
// 以下のような記述形式をとります
// 記述では
// 山田孝雄は、係助詞が陳述に影響を及ぼすとしている
// 一分野か　一分、野球する
// 自動車産業、宇宙産業などで参考にしている