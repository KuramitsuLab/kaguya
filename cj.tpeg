/* CJ Controlled Japanese 文法 */

/*
トップの規則
入力をClauseの繰り返しとみる
*/

Sentence = {
    // Clause* [。．.\n]? {.* #Remain}  // デバッグモード
    (Clause / TEN)* EOS
    #S
}

Clause =
    / ExtraClause   // 接続詞, 副詞, 連体詞
    / AdjvClause    // 形容詞節, 形容動詞節
    / VerbClause    // 動詞節
    / NounClause    // 名詞節

ExtraClause =
    / Conjunction // 接続詞
    / Adverb  // 副詞
    / Adnominal  // 連体詞

/* 接続詞 CONJ.txt */

Conjunction = { // e.g., こうして
    @choice('dic/CONJ.txt') #Conj 
}

/* 副詞 ADV.txt */
Adverb = {   // e.g., 少し 順次
    @choice('dic/ADV.txt') #Adverb 
}

/* 連体詞 ADNM */
Adnominal = { 
    @choice('dic/ADNM.txt') #Adnominal 
}

/*
名詞 Nounらしいもので始まる
*/

NounClause = 
    { Noun NounSuffix? #NounClause } 

NounSuffix = 
    / { [はも] #Subject }
    / { [をに] #Object }
    / { 'から' #From }
    / { 'まで' #Until }
    / { 'である' / 'だ' #Be }
    / { 'とする' #Let }
    / { 'する' / 'して' #Do }

/*
名詞 Noun
*/

Noun = 
    / Code
    / NonHiraNoun
    / NewNoun
    / HiraNoun

/* コードは名詞扱い */

Code = {
    [A-Za-z0-9]+
    #Code
}

/* ひらがな以外は全て名詞扱い */

NonHiraNoun = {
    NONHIRA+
    #Noun
}

NewNoun =
    / '\「' { (!'\」' .)* #Noun } '\」'
    / '\『' { (!'\』' .)* #Noun } '\』'
    / '\【' { (!'\】' .)* #Noun } '\】'
    / '\〔' { (!'\〕' .)* #Noun } '\〕'

HiraNoun = {
    @choice('dic/NOUN.txt')
    / 'ところ'
    / 'ため'
    / 'こと'
    / 'もの'
    / 'うち'
    / 'あと'
    / ([こそど]/'あそ') 'こ'
    / [こそあど] 'れ'
    / NUM [つこ個] [め目]?
    / ('ひと'/'ふた'/'みっ'/'よっ'/'いつ'/'むっ'/'なな'/'やっ'/'ここの') 'つ' [め目]?
}

/*
動詞の語幹と活用語尾
活用順にしており、活用語尾が同じものまとめている
*/

example VerbClause 歩く
example VerbClause 認める
example VerbClause 会う
example VerbClause 止む
example VerbClause 止まる
example VerbClause 止める

example VerbClause 話したく思う    // 連文節
example VerbClause 攻撃され始めた
example VerbClause 話し終わる
example VerbClause 駆け上がる
example VerbClause 立ち止まる
example VerbClause 止まる
example VerbClause 止まらない

/*
動詞の未然形活用
未然形はほぼ確実に助動詞が繋がるため先読みで誤マッチを抑制している
これで「止まる」の先頭文字列「止ま」が「止む」の未然形「止ま」にマッチしない
以下、他の活用も同様
*/

VerbClause = {
      { VERB5KA #Verb } VerbSuffix5KA
    / { VERB5SA #Verb } VerbSuffix5SA 
    / { VERB5TA #Verb } VerbSuffix5TA
    / { VERB5NA #Verb } VerbSuffix5NA
    / { VERB5MA #Verb } VerbSuffix5MA
    / { VERB5RA #Verb } VerbSuffix5RA
    / { VERB5WA #Verb } VerbSuffix5WA
    / { VERB5GA #Verb } VerbSuffix5GA
    / { VERB5BA #Verb } VerbSuffix5BA
    / { VERB1 #Verb }   VerbSuffix1
    #VerbClause    
}

/*
活用型ごとの動詞の語幹を辞書から生成
「行く」はカ行五段活用だが、イ音便ではなく促音便で活用が異なるため別枠
*/

VERB5KA = @choice('dic/Verb/VERB5KA.txt') &[か-こい]
VERB5SA = @choice('dic/Verb/VERB5SA.txt') &[さ-そ]
VERB5TA = @choice('dic/Verb/VERB5TA.txt') &[た-とっ]
VERB5NA = @choice('dic/Verb/VERB5NA.txt') &[な-のん]
VERB5MA = @choice('dic/Verb/VERB5MA.txt') &[ま-もん]
VERB5RA = @choice('dic/Verb/VERB5RA.txt') &[ら-ろっ]
VERB5WA = @choice('dic/Verb/VERB5WA.txt') &[わいうえおっ]
VERB5GA = @choice('dic/Verb/VERB5GA.txt') &[が-ごい]
VERB5BA = @choice('dic/Verb/VERB5BA.txt') &[ば-ぼん]
VERB1 = @choice('dic/Verb/VERB1.txt')

VerbSuffix5KA = // 書
    / {'かない' #DoNot }
    / {'く' ('とき' / 'なら'/ '場合') / 'いたら' / 'けば' #If }
    / {'き' ('つつ' / 'ながら') #While }
    / {'く' / 'き' / 'いて' / 'け' / 'こう' #Do }

VerbSuffix5SA = // 探
    / {'さない' #DoNot }
    / {'す' ('とき' / 'なら'/ '場合') / 'したら' / 'せば' #Case }
    / {'す' / 'して' / 'し' / 'せ' / 'そう' #Do }

VerbSuffix5TA = // 勝
    / {'たない' #DoNot }
    / {'つ' ('とき' / 'なら'/ '場合') / 'ったら' / 'てば' #Case }
    / {'つ' / 'ち' / 'って' / 'て' / 'とう' #Do }

VerbSuffix5NA = // 死
    / {'なない' #DoNot }
    / {'ぬ' ('とき' / 'なら'/ '場合') / 'ねば' #Case }
    / {'ぬ' / 'に' / 'んで' / 'ね' / 'のう' #Do }

VerbSuffix5MA = // 読
    / {'まない' #DoNot }
    / {'む' ('とき' / 'なら'/ '場合') / 'せば' #Case }
    / {'む' / 'み' / 'んで' / 'め' / 'もう' #Do }

VerbSuffix5RA = // 切
    / {'らない' #DoNot }
    / {'る' ('とき' / 'なら'/ '場合') / 'れば' #Case }
    / {'る' / 'り' / 'って' / 'れ' / 'ろう' #Do }

VerbSuffix5WA = // 笑
    / {'わない' #DoNot }
    / {'う' ('とき' / 'なら'/ '場合') / 'えば' #Case }
    / {'う' / 'い' / 'って' / 'え' / 'おう' #Do }

VerbSuffix5GA = // 防
    / {'がない' #DoNot }
    / {'ぐ' ('とき' / 'なら'/ '場合') / 'げば' #Case }
    / {'ぐ' / 'ぎ' / 'いで' / 'げ' / 'ごう' #Do }

VerbSuffix5BA = // 遊
    / {'ばない' #DoNot }
    / {'ぶ' ('とき' / 'なら'/ '場合') / 'べば' #Case }
    / {'ぶ' / 'び' / 'んで' / 'べ' / 'ぼう' #Do }

VerbSuffix1 = // 着る
    / {'ない' #DoNot }
    / {'る' ('とき' / 'なら'/ '場合') / 'れば' #Case }
    / { TEN / 'て' / [よろ] / 'よう' #Do }

example AdjvClause 赤いボール
example AdjvClause 赤かったボール
example AdjvClause うるさい人
example AdjvClause うるさかった人
example AdjvClause うるさくない人
example AdjvClause うるさかろう人
example AdjvClause うるさそうだった人
example AdjvClause うるさいらしい人
example AdjvClause うるさいでしょう
example AdjvClause 赤くないボール
example AdjvClause 赤くなく青いボール
example AdjvClause 赤くかっこいい人
example AdjvClause 健康な人
example AdjvClause 健康だった人
example AdjvClause 健康でしょう
example AdjvClause ぞっこんだ
example AdjvClause なめいもの
example AdjvClause なめかったもの
example AdjvClause なめらかだ
example AdjvClause なめらかなやわらかい布

AdjvClause =
    / { Adj AdjSuffix #AdjClause }
    / { AdjVerb AdjVerbSuffix #AdjClause }

// 美し
Adj = { 
    @choice('dic/ADJ.txt') &[かくけい] #Adj 
}

// 素敵
ADJVERB = 
    / @cat(@choice('dic/ADJV.txt'), 'そう')
    / @choice('dic/ADJV.txt')
AdjVerb = { ADJVERB #Adjv }

AdjSuffix =
    / { 'ければ' #If }
    / { 'くない' #Not }
    / { [くい] #Be }

AdjVerbSuffix =
    / { 'ならば' #If }
    / { 'でない' #Not }
    / { [にでだな] #Be }

/* 文字コード */

Ten = { TEN #Ten }

EOF = !.
EOS = [。．.\n]? EOF
TEN = [、，,\n]
KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]
HIRA = [ぁ-ん]
NONHIRA = (!HIRA .)
ALPHA = [a-zA-Zａ-ｚＡ-Ｚ]
NUM = [\.,0-9０-９一二三四五六七八九十百千万億兆京垓秭穰溝澗正載極]
SYMBOL = [^＾~〜\-ー_＿!！?？#＃$＄&＆=＝@＠*＊+＋/・]


