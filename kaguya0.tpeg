/* KAGUYA文法 */

/*
トップの規則
入力をChunkの繰り返しとみる
*/
Sentence = {
    (Chunk/TEN)* EOS
    #Sentence
}

/*
形容詞や形容動詞、動詞や連体詞などのひとかたまりごとにマッチする規則
*/
Chunk = {
      AdjectivePhrase  // 形容詞or形容動詞
    / VerbPhrase
    / Adverb  // 副詞
    / Adnominal  // 連体詞
    / Noun Postp?
    / Postp
    #Chunk
}

example Sentence 要求は認められないので代案を用意せよ
example Sentence 僕は転びそうだったが持ちこたえた
example Sentence 紅茶を入れますわ
example Sentence 駐車場に車を止める
example Sentence 駐車場に車が止まる
example Sentence 駐車場に車が止まらない
example Sentence 雨が止む
example Sentence 酒は飲んでも飲まれるな
example Sentence スマホを充電してあげる
example Sentence 変数を定義して数値を代入する
example Sentence 会いたくて震える
example Sentence 期限を過ぎなければ提出を認めるだろう
example Sentence 階段を駆け上がったので息が切れた
example Sentence 雨が降ったらしいけれど家に居たので何も知らなかった
example Sentence 変数に数値を代入し計算を実行し結果をコンソールに出力する
example Sentence 変数に数値を代入し、計算を実行し、結果をコンソールに出力する。

EOF = !.
EOS = [。．.\n] / EOF
EOL = [。．.\n] / EOF / TEN
TEN = [、，,\n]
KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]
ALPHA = [a-zA-Zａ-ｚＡ-Ｚ]
NUM = [0-9０-９]
SYMBOL = [-~〜ー.,]


/*
動詞節
スモールケースから順に
1. 動詞の活用した形のみの場合：話す、話し
2. 動詞の活用した形+助動詞の場合：話した、話される
3. 動詞の活用した形+動詞節の場合：話し始める
4. 動詞の活用した形+助動詞+動詞節の場合：話され始める
*/
VerbPhrase = {
      Verb AuxVerbs VerbPhrase?
    / Verb VerbPhrase?
    #VerbPhrase
}

example VerbPhrase 話したく思う
example VerbPhrase 攻撃され始めた
example VerbPhrase 話し終わる
example VerbPhrase 駆け上がる
example VerbPhrase 立ち止まる
example VerbPhrase 止まる
example VerbPhrase 止まらない

/*
形容詞または形容動詞
組み方は動詞と一緒
*/
AdjectivePhrase = {
      (Adjective/AdjectiveVerb) AuxVerbs AdjectivePhrase?
    / (Adjective/AdjectiveVerb) AdjectivePhrase?
    #AdjectivePhrase
}

example AdjectivePhrase 赤いボール
example AdjectivePhrase 赤かったボール
example AdjectivePhrase うるさい
example AdjectivePhrase うるさかった
example AdjectivePhrase うるさくない
example AdjectivePhrase うるさかろう
example AdjectivePhrase うるさそうだった
example AdjectivePhrase うるさいらしい
example AdjectivePhrase うるさいでしょう
example AdjectivePhrase 赤くない
example AdjectivePhrase 赤くなく青い
example AdjectivePhrase 赤くかっこいい
example AdjectivePhrase 健康な人
example AdjectivePhrase 健康だった
example AdjectivePhrase 健康でしょう
example AdjectivePhrase ぞっこんだ
example AdjectivePhrase なめい
example AdjectivePhrase なめかった
example AdjectivePhrase なめらかだ
example AdjectivePhrase なめらかなやわらかい布

/*
形容詞の語幹と活用語尾
実験的に助動詞や助詞、動詞を先読みで入れている
語幹は辞書の"Adjective.dic"と"Noun.nai.dic"から生成
*/
Adjective = {
      {ADJ 'かろ' #ADJMizen} &'う'
    / {ADJ 'かっ' #ADJRenyo} &'た'
    / {ADJ 'く' #ADJRenyo} &([ては]/Verb/Adjective/AdjectiveVerb)
    / {ADJ 'けれ' #ADJKatei} &'ば'
    / {ADJ 'い' #ADJSyushiRentai} &('そう'/'らし'/'でしょ'/'だろ'/NOUN/EOL)
    / {ADJ #ADJGokan} &'そう'
    #LookAHead
}

ADJ = @choice('dic/ADJ.txt')

/*
形容動詞の語幹と活用語尾
形容詞とやっていることは同じ
語幹は辞書の"Noun.adjv.dic"から生成
*/
AdjectiveVerb = {
      {ADJV 'だろ' #ADJVMizen} &'う'
    / {ADJV 'だっ' #ADJVRenyo} &'た'
    / {ADJV 'で' #ADJVRenyo} &([はも]/SUBARU/SUBNAI)
    / {ADJV 'に' #ADJVRenyo} &(Verb/Adjective/AdjectiveVerb)
    / {ADJV 'なら' #ADJVKatei}
    / {ADJV 'だ' #ADJVSyushi} &('そう'/EOL)
    / {ADJV 'な' #ADJVRentai} &('ようだ'/NOUN/AdjectivePhrase)
    / {ADJV #ADJVGokan} &('そう'/'らし'/'でしょ')
    #LookAHead
}

ADJV = @choice('dic/ADJV.txt')

/*
補助動詞の「ある」と「ない」
*/
SUBARU = 'あ' [らりるれろっ]
SUBNAI = 'な' ('かろ' / 'かっ' / 'く' / 'い' / 'けれ')


/*
動詞の語幹と活用語尾
活用順にしており、活用語尾が同じものまとめている
*/
Verb =
    / Mizen
    / Renyo
    / SyushiRentai
    / Katei
    / Meirei
    / KateiMeirei
    / Common

example Verb 歩く
example Verb 認める
example Verb 会う
example Verb 止む
example Verb 止まる
example Verb 止める

/*
動詞の未然形活用
未然形はほぼ確実に助動詞が繋がるため先読みで誤マッチを抑制している
これで「止まる」の先頭文字列「止ま」が「止む」の未然形「止ま」にマッチしない
以下、他の活用も同様
*/
Mizen = {
      {VERB5KA [かこ] #Mizen} &AuxVerbs
    / {VERB5SA [さそ] #Mizen} &AuxVerbs
    / {VERB5TA [たと] #Mizen} &AuxVerbs
    / {VERB5NA [なの] #Mizen} &AuxVerbs
    / {VERB5MA [まも] #Mizen} &AuxVerbs
    / {VERB5RA [らろ] #Mizen} &AuxVerbs
    / {VERB5WA [わお] #Mizen} &AuxVerbs
    / {VERB5GA [がご] #Mizen} &AuxVerbs
    / {VERB5BA [ばぼ] #Mizen} &AuxVerbs
    / {VERB5KASOKU [かこ] #Mizen} &AuxVerbs
    / {SAHEN_SURU [さ] #Mizen} &AuxVerbs
    / {SAHEN_ZURU [ざ] #Mizen} &AuxVerbs
    / {(KANJI/KATA)* [さ] #Mizen} &AuxVerbs
    #LookAHead
}

Renyo = {
      {VERB5KA [きい] #Renyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {VERB5SA [し] #Renyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {VERB5TA [ちっ] #Renyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {VERB5NA [にん] #Renyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {VERB5MA [みん] #Renyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {VERB5RA [りっ] #Renyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {VERB5WA [いっ] #Renyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {VERB5GA [ぎい] #Renyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {VERB5BA [びん] #Renyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {VERB5KASOKU [きっ] #Renyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {KAHEN? [き] #Renyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    #LookAHead
}

SyushiRentai = {
      {VERB5KA [く] #SyushiRentai} &(AuxVerbs/EOL/'')
    / {VERB5SA [す] #SyushiRentai} &(AuxVerbs/EOL/'')
    / {VERB5TA [つ] #SyushiRentai} &(AuxVerbs/EOL/'')
    / {VERB5NA [ぬ] #SyushiRentai} &(AuxVerbs/EOL/'')
    / {VERB5MA [む] #SyushiRentai} &(AuxVerbs/EOL/'')
    / {VERB5RA [る] #SyushiRentai} &(AuxVerbs/EOL/'')
    / {VERB5WA [う] #SyushiRentai} &(AuxVerbs/EOL/'')
    / {VERB5GA [ぐ] #SyushiRentai} &(AuxVerbs/EOL/'')
    / {VERB5BA [ぶ] #SyushiRentai} &(AuxVerbs/EOL/'')
    / {VERB5KASOKU [く] #SyushiRentai} &(AuxVerbs/EOL/'')
    / {VERB1 [る] #SyushiRentai} &(AuxVerbs/EOL/'')
    / {KAHEN? ('くる' / '来る') #SyushiRentai} &(AuxVerbs/EOL/'')
    / {SAHEN_SURU 'する' #SyushiRentai} &(AuxVerbs/EOL/'')
    / {SAHEN_ZURU 'ずる' #SyushiRentai} &(AuxVerbs/EOL/'')
    / {(KANJI/KATA)* 'する' #SyushiRentai} &(AuxVerbs/EOL/'')
    #LookAHead
}

Katei = {
      {VERB1 [れ] #Katei} &'ば'
    / {KAHEN? ('くれ' / '来れ') #Katei} &'ば'
    / {SAHEN_SURU 'すれ' #Katei} &'ば'
    / {SAHEN_ZURU 'ずれ' #Katei} &'ば'
    / {(KANJI/KATA)+ 'すれ' #Katei} &'ば'
    #LookAHead
}

Meirei = {
      {VERB1 [ろよ] #Meirei} &EOL
    / {KAHEN? ('こい' / '来い') #Meirei} &EOL
    / {SAHEN_SURU ('しろ' / 'せよ') #Meirei} &EOL
    / {SAHEN_ZURU ('じろ' / 'ぜよ') #Meirei} &EOL
    / {(KANJI/KATA)+ ('しろ' / 'せよ') #Meirei} &EOL
    #LookAHead
}

KateiMeirei = {
      {VERB5KA [け] #KateiMeirei} &('ば'/EOL)
    / {VERB5SA [せ] #KateiMeirei} &('ば'/EOL)
    / {VERB5TA [て] #KateiMeirei} &('ば'/EOL)
    / {VERB5NA [ね] #KateiMeirei} &('ば'/EOL)
    / {VERB5MA [め] #KateiMeirei} &('ば'/EOL)
    / {VERB5RA [れ] #KateiMeirei} &('ば'/EOL)
    / {VERB5WA [え] #KateiMeirei} &('ば'/EOL)
    / {VERB5GA [げ] #KateiMeirei} &('ば'/EOL)
    / {VERB5BA [べ] #KateiMeirei} &('ば'/EOL)
    / {VERB5KASOKU [け] #KateiMeirei} &('ば'/EOL)
    #LookAHead
}

Common = {
      {VERB1 #MizenRenyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {KAHEN? [来] #MizenRenyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {KAHEN? [こ] #Mizen} &AuxVerbs  // 命令形の活用語尾「こい」との競合避け
    / {SAHEN_SURU [し] #MizenRenyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {SAHEN_ZURU [じ] #MizenRenyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {(KANJI/KATA)* [し] #MizenRenyo} &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / {SAHEN_SURU [せ] #Mizen} &AuxVerbs  // 命令形の活用語尾「せよ」との競合避け
    / {SAHEN_ZURU [ぜ] #Mizen} &AuxVerbs  // 命令形の活用語尾「ぜよ」との競合避け
    / {(KANJI/KATA)* [せ] #Mizen} &AuxVerbs  // 命令形の活用語尾「せよ」との競合避け
    #LookAHead
}

/*
活用型ごとの動詞の語幹を辞書から生成
「行く」はカ行五段活用だが、イ音便ではなく促音便で活用が異なるため別枠
*/
VERB5KA = @choice('dic/Verb/VERB5KA.txt')
VERB5SA = @choice('dic/Verb/VERB5SA.txt')
VERB5TA = @choice('dic/Verb/VERB5TA.txt')
VERB5NA = @choice('dic/Verb/VERB5NA.txt')
VERB5MA = @choice('dic/Verb/VERB5MA.txt')
VERB5RA = @choice('dic/Verb/VERB5RA.txt')
VERB5WA = @choice('dic/Verb/VERB5WA.txt')
VERB5GA = @choice('dic/Verb/VERB5GA.txt')
VERB5BA = @choice('dic/Verb/VERB5BA.txt')
VERB1 = @choice('dic/Verb/VERB1.txt')
KAHEN = @choice('dic/Verb/KAHEN.txt')
SAHEN_SURU = @choice('dic/Verb/SAHEN_SURU.txt')
SAHEN_ZURU = @choice('dic/Verb/SAHEN_ZURU.txt')
VERB5KASOKU = '行'


/* 助動詞、gen_auxverb.pyで生成 */
AuxVerbs = {
    AUXVERB
    #AuxVerbs
}
AUXVERB = @choice('dic/Auxverb/AUXVERB.txt')


/* 助詞、手入力 */
Postp = {
    POSTP
    #Postp
}
POSTP = @choice('dic/POSTP.txt')


/* 連体詞、辞書から生成 */
Adnominal = {
    ADNM
    #Adnominal
}
ADNM = @choice('dic/ADNM.txt')


/* 副詞、辞書から生成 */
Adverb = {
    ADV
    #Adverb
}
ADV = @choice('dic/ADV.txt')


/* 名詞 */
Noun = {
    NOUN
    #Noun
}

/*
漢字カタカナアルファベット数字
混合した場合も可（例：アップル社
括弧で括ったものも名詞
*/
NOUN =
    / (KANJI/KATA/ALPHA/NUM)+
    / B_START (!B_END .)* B_END

B_START = [\[\{\(\"\'<（「『”’［｛【〔“]
B_END   = [\]\}\)\"\'>）」』”’］｝】〕“]

