/* KAGUYA文法 */

/*
トップの規則
句点で終わる一文に対応している
文をChunkの繰り返しとみる
*/
Sentence = {
    // (TEN/Chunk)* [。．.\n]? {.* #Remain}  // デバッグモード
    (TEN/Chunk)* EOS
    #Sentence
}


/*
形容詞句や動詞句などのひとかたまりごとにマッチする規則
チョイスの順番はテストして最適な順番を見つけるしかない
*/
Chunk =
    / ConjunctionPhrase  // 接続詞
    / AdverbPhrase  // 副詞
    / AdnominalPhrase  // 連体詞
    / AdjectivePhrase  // 形容詞句
    / AdjectiveVerbPhrase  // 形容動詞句
    / VerbPhrase  // 動詞句
    / NounPhrase  // 名詞句


EOF = !.
EOS = [!！?？。．.\n]? EOF
TEN = [、，,:：\n]
Eol = { (TEN/EOS) #Eol }
// KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]+
ALPHA = [a-zA-Zａ-ｚＡ-Ｚ]+
NUM = [\-ー\.,．，0-9０-９一二三四五六七八九十百千万億兆]+
SYMBOL =
    / [^＾~〜\-ー_＿!！?？#＃$＄&＆=＝@＠*＊+＋/・]
    / [\[\]\(\)\<\>\{\}\'\"\`「」（）＜＞『』［］【】”“｀”“’‘｛｝〔〕]


/*
動詞句
スモールケースから順に
1. 動詞の活用した形のみの場合：話す、話し
2. 動詞の活用した形+助動詞の場合：話した、話される
3. 動詞の活用した形+動詞句の場合：話し始める
4. 動詞の活用した形+助動詞+動詞句の場合：話され始める
*/
VerbPhrase = {
      Verb AuxVerbForVerb PostpPhrase
    / Verb AuxVerbForVerb !!(Chunk/Eol)
    / Verb PostpPhrase
    / Verb !!(Chunk/Eol)
    #VerbPhrase
}
// Verbの各活用形に対して接続しないものも試しているので失敗するかも
AuxVerbForVerb = {
      Aux_Seru
    / Aux_Saseru
    / Aux_Reru
    / Aux_Rareru
    / Aux_Nai
    / Aux_Nu
    / Aux_U
    / Aux_You
    / Aux_Mai
    / Aux_Tai
    / Aux_Tagaru
    / Aux_Ta
    / Aux_Da1
    / Aux_Souda1
    / Aux_Souda2
    / Aux_Youda
    / Aux_Rashii
    / Aux_Masu
    / Aux_Daro
    / Aux_Desyo
    #AuxVerbForVerb
}


/*
形容詞
組み方は動詞と一緒
*/
AdjectivePhrase = {
      Adjective AuxVerbForAdj PostpPhrase
    / Adjective AuxVerbForAdj !!(Chunk/Eol)
    / Adjective PostpPhrase
    / Adjective !!(Chunk/Eol)
    #AdjectivePhrase
}
AuxVerbForAdj = {
      Aux_U
    / Aux_Ta
    / Aux_Souda1
    / Aux_Souda2
    / Aux_Youda
    / Aux_Rashii
    / Aux_Daro
    / Aux_Desyo
    #AuxVerbForAdj
}


/*
形容動詞
組み方は動詞と一緒
*/
AdjectiveVerbPhrase = {
      AdjectiveVerb AuxVerbForAdjv PostpPhrase
    / AdjectiveVerb AuxVerbForAdjv !!(Chunk/Eol)
    / AdjectiveVerb PostpPhrase
    / AdjectiveVerb !!(Chunk/Eol)
    #AdjectiveVerbPhrase
}
AuxVerbForAdjv = {
      Aux_U
    / Aux_Ta
    / Aux_Souda1
    / Aux_Souda2
    / Aux_Youda
    / Aux_Rashii
    / Aux_Desu
    #AuxVerbForAdj
}


/*
名詞句
名詞にも助動詞が付属語としてつきうる
*/
NounPhrase = {
      Noun AuxVerbForNoun PostpPhrase
    / Noun AuxVerbForNoun !!(Chunk/Eol)
    / Noun PostpPhrase
    / Noun !!(Chunk/Eol)
    #NounPhrase
}
AuxVerbForNoun = {
      Aux_Rashii
    / Aux_Da2
    / Aux_Desu
    #AuxVerbForNoun
}


/*
接続詞句
接続詞に助詞が続くことはないものとしている
*/
ConjunctionPhrase = {
    //   Conjunction PostpPhrase
    Conjunction !!(Chunk/Eol)
    #ConjunctionPhrase
}


/*
連体詞句
「この・その・あの・どの」には助動詞「ようだ」がつきうる
連体詞には助詞は続かない（はず）
*/
AdnominalPhrase = {
      KoSoADo AuxVerbForAdnm PostpPhrase
    / KoSoADo AuxVerbForAdnm !!(Chunk/Eol)
    / Adnominal !!(Chunk/Eol)
    // / Adnominal !PostPhrase !!(Chunk/Eol)
    #AdnominalPhrase
}
AuxVerbForAdnm = {
    Aux_Youda
    #AuxVerbForAdnm
}
KoSoADo = { [こそあど] 'の' #Adnominal }


/*
副詞句
*/
AdverbPhrase = {
      Adverb AuxVerbForAdv PostpPhrase  // すぐ です ので
    / Adverb PostpNo !!(Chunk/Eol)  // しばらく の 間
    / Adverb AuxVerbForAdv !!(Chunk/Eol)  // すぐ です
    / Adverb !!(Chunk/Eol)  // もっと
    #AdverbPhrase
}
AuxVerbForAdv = {
      Aux_Rashii
    / Aux_Da2
    / Aux_Desu
    #AuxVerbForAdv
}


/*
助詞句
助詞の「の」だけに続く助動詞があるため別扱いしている
助詞に続く助動詞「な」はその後が限定的なので別扱いしている
チョイスの順番決めが難しい
*/
PostpPhrase = {
      Postp AuxVerbNa !!('の') PostpPhrase
    / PostpNo AfterPostpNo PostpPhrase
    / PostpNo AfterPostpNo !!(Chunk/Eol)
    / PostpNo PostpPhrase
    / PostpNo !!(Chunk/Eol)
    / Postp AfterPostp PostpPhrase
    / Postp AfterPostp !!(Chunk/Eol)
    / Postp PostpPhrase
    / Postp !!(Chunk/Eol)
    #PostpPhrase
}
PostpNo = { 'の' #PostpNo }
AfterPostpNo = { Aux_Youda #AuxVerbForPostpNo }
AfterPostp = { (Aux_Rashii/Aux_Da2/Aux_Desu) #AuxVerbForPostp }
AuxVerbNa = { 'な' #AuxVerbForPostp }


/*
動詞の語幹と活用語尾
活用順にしており、活用語尾が同じものまとめている
*/
Verb =
    / Mizen
    / Renyo
    / Syushi
    / Rentai
    / Katei
    / Meirei


/*
動詞の未然形活用
未然形はほぼ確実に助動詞が繋がるため先読みで誤マッチを抑制している
これで「止まる」の先頭文字列「止ま」が「止む」の未然形「止ま」にマッチしない
以下、他の活用も同様
*/
Mizen = {
      VERB5KA ( [か] !!(AuxV5Mizen) / [こ] !!(Aux_U) )
    / VERB5SA ( [さ] !!(AuxV5Mizen) / [そ] !!(Aux_U) )
    / VERB5TA ( [た] !!(AuxV5Mizen) / [と] !!(Aux_U) )
    / VERB5NA ( [な] !!(AuxV5Mizen) / [の] !!(Aux_U) )
    / VERB5MA ( [ま] !!(AuxV5Mizen) / [も] !!(Aux_U) )
    / VERB5RA ( [ら] !!(AuxV5Mizen) / [ろ] !!(Aux_U) )
    / VERB5WA ( [わ] !!(AuxV5Mizen) / [お] !!(Aux_U) )
    / VERB5GA ( [が] !!(AuxV5Mizen) / [ご] !!(Aux_U) )
    / VERB5BA ( [ば] !!(AuxV5Mizen) / [ぼ] !!(Aux_U) )
    / VERB5IK ( [か] !!(AuxV5Mizen) / [こ] !!(Aux_U) )
    / VERB1 !!(AuxV1Mizen)
    / NOUN? ( [さ] !!(AuxVSaMizen) / [し] !!(AuxVSiMizen) / [せ] !!(Aux_Nu) )
    / SAHEN_SURU ( [さ] !!(AuxVSaMizen) / [し] !!(AuxVSiMizen) / [せ] !!(Aux_Nu) )
    / SAHEN_ZURU ( [ざ] !!(AuxVSaMizen) / [じ] !!(AuxVSiMizen) / [ぜ] !!(Aux_Nu) )
    / KAHEN? [来こ] !!(AuxVKaMizen)
    // / KAHEN? [こ] !!(AuxVKaMizen)  // 命令形の活用語尾「こい」と競合するかも
    #Mizen
}
AuxV5Mizen = Aux_Seru/Aux_Reru/Aux_Nai/Aux_Nu
AuxV1Mizen = Aux_Saseru/Aux_Rareru/Aux_Nai/Aux_You/Aux_Mai/Aux_Nu
AuxVSaMizen = Aux_Seru/Aux_Reru
AuxVSiMizen = Aux_Nai/Aux_You/Aux_Mai
AuxVKaMizen = Aux_Saseru/Aux_Rareru/Aux_Nai/Aux_Nu/Aux_You/Aux_Mai


Renyo = {
      VERB5KA ( [き] !!(AuxV5Renyo/AfterRenyo) / [い] !!(Aux_Ta/[て]) )
    / VERB5SA ( [し] !!(AuxV5Renyo/AfterRenyo/[て]) )
    / VERB5TA ( [ち] !!(AuxV5Renyo/AfterRenyo) / [っ] !!(Aux_Ta/[て]) )
    / VERB5NA ( [に] !!(AuxV5Renyo/AfterRenyo) / [ん] !!(Aux_Da1/[で]) )
    / VERB5MA ( [み] !!(AuxV5Renyo/AfterRenyo) / [ん] !!(Aux_Da1/[で]) )
    / VERB5RA ( [り] !!(AuxV5Renyo/AfterRenyo) / [っ] !!(Aux_Ta/[て]) )
    / VERB5WA ( [い] !!(AuxV5Renyo/AfterRenyo) / [っ] !!(Aux_Ta/[て]) )
    / VERB5GA ( [ぎ] !!(AuxV5Renyo/AfterRenyo) / [い] !!(Aux_Da1/[で]) )
    / VERB5BA ( [び] !!(AuxV5Renyo/AfterRenyo) / [ん] !!(Aux_Da1/[で]) )
    / VERB5IK ( [き] !!(AuxV5Renyo/AfterRenyo) / [っ] !!(Aux_Ta/[て]) )
    / VERB1 !!(AuxV1Renyo/AfterRenyo/[て])
    / KAHEN? [来き] !!(AuxVKaRenyo/AfterRenyo/[て])
    / NOUN? [し] !!(AuxVSaRenyo/AfterRenyo/[て])
    / SAHEN_SURU [し] !!(AuxVSaRenyo/AfterRenyo/[て])
    / SAHEN_ZURU [じ] !!(AuxVSaRenyo/AfterRenyo/[て])
    #Renyo
}
AuxV5Renyo = Aux_Tai/Aux_Tagaru/Aux_Souda1/Aux_Masu
AuxV1Renyo = Aux_Tai/Aux_Tagaru/Aux_Ta/Aux_Souda1/Aux_Masu
AuxVSaRenyo = Aux_Tai/Aux_Tagaru/Aux_Ta/Aux_Souda1/Aux_Masu
AuxVKaRenyo = Aux_Tai/Aux_Tagaru/Aux_Ta/Aux_Souda1/Aux_Masu

AfterRenyo = {
      VerbPhrase
    / TEN
    / PostpPhrase
    #AfterRenyo
}

Syushi = {
      VERB5KA [く] !!(AuxV5Syushi/AfterSyushi)
    / VERB5SA [す] !!(AuxV5Syushi/AfterSyushi)
    / VERB5TA [つ] !!(AuxV5Syushi/AfterSyushi)
    / VERB5NA [ぬ] !!(AuxV5Syushi/AfterSyushi)
    / VERB5MA [む] !!(AuxV5Syushi/AfterSyushi)
    / VERB5RA [る] !!(AuxV5Syushi/AfterSyushi)
    / VERB5WA [う] !!(AuxV5Syushi/AfterSyushi)
    / VERB5GA [ぐ] !!(AuxV5Syushi/AfterSyushi)
    / VERB5BA [ぶ] !!(AuxV5Syushi/AfterSyushi)
    / VERB5IK [く] !!(AuxV5Syushi/AfterSyushi)
    / VERB1 [る] !!(AuxV1Syushi/AfterSyushi)
    / KAHEN? ('くる' / '来る') !!(AuxVKaSyushi/AfterSyushi)
    / NOUN? 'する' !!(AuxVSaSyushi/AfterSyushi)
    / SAHEN_SURU 'する' !!(AuxVSaSyushi/AfterSyushi)
    / SAHEN_ZURU 'ずる' !!(AuxVSaSyushi/AfterSyushi)
    #Syushi
}
AuxV5Syushi = Aux_Mai/Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo
AuxV1Syushi = Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo
AuxVSaSyushi = Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo
AuxVKaSyushi = Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo

AfterSyushi = {
      PostpPhrase
    / Eol
    #AfterSyushi
}


Rentai = {
      VERB5KA [く] !!(AuxV5Rentai/AfterRentai)
    / VERB5SA [す] !!(AuxV5Rentai/AfterRentai)
    / VERB5TA [つ] !!(AuxV5Rentai/AfterRentai)
    / VERB5NA [ぬ] !!(AuxV5Rentai/AfterRentai)
    / VERB5MA [む] !!(AuxV5Rentai/AfterRentai)
    / VERB5RA [る] !!(AuxV5Rentai/AfterRentai)
    / VERB5WA [う] !!(AuxV5Rentai/AfterRentai)
    / VERB5GA [ぐ] !!(AuxV5Rentai/AfterRentai)
    / VERB5BA [ぶ] !!(AuxV5Rentai/AfterRentai)
    / VERB5IK [く] !!(AuxV5Rentai/AfterRentai)
    / VERB1 [る] !!(AuxV1Rentai/AfterRentai)
    / KAHEN? ('くる' / '来る') !!(AuxVKaRentai/AfterRentai)
    / NOUN? 'する' !!(AuxVSaRentai/AfterRentai)
    / SAHEN_SURU 'する' !!(AuxVSaRentai/AfterRentai)
    / SAHEN_ZURU 'ずる' !!(AuxVSaRentai/AfterRentai)
    #Rentai
}
AuxV5Rentai = Aux_Youda
AuxV1Rentai = Aux_Youda
AuxVSaRentai = Aux_Youda
AuxVKaRentai = Aux_Youda

AfterRentai = {
      NounPhrase
    / PostpPhrase
    #AfterRentai
}


Katei = {
      VERB5KA [け] !!'ば'
    / VERB5SA [せ] !!'ば'
    / VERB5TA [て] !!'ば'
    / VERB5NA [ね] !!'ば'
    / VERB5MA [め] !!'ば'
    / VERB5RA [れ] !!'ば'
    / VERB5WA [え] !!'ば'
    / VERB5GA [げ] !!'ば'
    / VERB5BA [べ] !!'ば'
    / VERB5IK [け] !!'ば'
    / VERB1 [れ] !!'ば'
    / KAHEN? ('くれ' / '来れ') !!'ば'
    / NOUN? 'すれ' !!'ば'
    / SAHEN_SURU 'すれ' !!'ば'
    / SAHEN_ZURU 'ずれ' !!'ば'
    #Katei
}


Meirei = {
      VERB5KA [け] !!(Eol)
    / VERB5SA [せ] !!(Eol)
    / VERB5TA [て] !!(Eol)
    / VERB5NA [ね] !!(Eol)
    / VERB5MA [め] !!(Eol)
    / VERB5RA [れ] !!(Eol)
    / VERB5WA [え] !!(Eol)
    / VERB5GA [げ] !!(Eol)
    / VERB5BA [べ] !!(Eol)
    / VERB5IK [け] !!(Eol)
    / VERB1 [ろよ] !!(Eol)
    / KAHEN? ('こい' / '来い') !!(Eol)
    / NOUN? ('しろ' / 'せよ') !!(Eol)
    / SAHEN_SURU ('しろ' / 'せよ') !!(Eol)
    / SAHEN_ZURU ('じろ' / 'ぜよ') !!(Eol)
    / ('ください'/'下さい') !!(Eol)  // 敬語動詞「下さる」の命令形は特殊
    #Meirei
}


/*
活用の種類ごとの動詞の語幹を辞書から生成
「行く」はカ行五段活用だが、イ音便ではなく促音便で活用が異なるため別枠
*/
VERB5KA = @choice('dic/Verb/VERB5KA.txt')
VERB5SA = @choice('dic/Verb/VERB5SA.txt')
VERB5TA = @choice('dic/Verb/VERB5TA.txt')
VERB5NA = @choice('dic/Verb/VERB5NA.txt')
VERB5MA = @choice('dic/Verb/VERB5MA.txt')
VERB5RA = @choice('dic/Verb/VERB5RA.txt')
VERB5WA = @choice('dic/Verb/VERB5WA.txt')
VERB5GA = @choice('dic/Verb/VERB5GA.txt')
VERB5BA = @choice('dic/Verb/VERB5BA.txt')
VERB1 = @choice('dic/Verb/VERB1.txt')
KAHEN = @choice('dic/Verb/KAHEN.txt')
SAHEN_SURU = @choice('dic/Verb/SAHEN_SURU.txt')
SAHEN_ZURU = @choice('dic/Verb/SAHEN_ZURU.txt')
VERB5IK = '行'



/*
形容詞の語幹と活用語尾
実験的に助動詞や助詞、動詞を先読みで入れている
語幹は辞書の"Adjective.dic"と"Noun.nai.dic"から生成
*/
Adjective =
    / {ADJ 'かろ' !!(Aux_U) #ADJMizen}
    / {ADJ 'かっ' !!(Aux_Ta/'たり') #ADJRenyo}
    / {ADJ 'く' !!([ては]/Chunk) #ADJRenyo}
    / {ADJ 'けれ' !!('ば') #ADJKatei}
    / {ADJ 'い' !!(Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo/PostpPhrase/Eol) #ADJSyushi}
    / {ADJ 'い' !!(Aux_Youda/NounPhrase/PostpPhrase) #ADJRentai}
    / {ADJ !!(Aux_Souda1) #ADJGokan}

ADJ = @choice('dic/ADJ.txt')

/*
形容動詞の語幹と活用語尾
形容詞とやっていることは同じ
語幹は辞書の"Noun.adjv.dic"から生成
*/
AdjectiveVerb =
    / {ADJV 'だろ' !!(Aux_U) #ADJVMizen}
    / {ADJV 'だっ' !!(Aux_Ta) #ADJVRenyo}
    / {ADJV 'で' !!([はも]/SUBARU/SUBNAI/TEN) #ADJVRenyo}
    / {ADJV 'に' !!(Chunk) #ADJVRenyo}
    / {ADJV 'だ' !!(Aux_Souda2/PostpPhrase/Eol) #ADJVSyushi}
    / {ADJV 'な' !!(Aux_Youda/NounPhrase) #ADJVRentai}
    / {ADJV 'なら' !!('ば'?) #ADJVKatei}
    / {ADJV !!(Aux_Souda1/Aux_Rashii/Aux_Desu) #ADJVGokan}

ADJV = @choice('dic/ADJV.txt')

/*
補助動詞の「ある」
補助形容詞の「ない」
*/
SUBARU = 'あ' [らりるれろっ]
SUBNAI = 'な' ('かろ' / 'かっ' / 'く' / 'い' / 'けれ')


/*
名詞
括弧で括ったものも名詞
数字と数え上げは事前にある程度定義している（名詞にない、とれない）
カタカナや英語は名詞に入っている
*/
Noun = {
      {BLOCK #BLOCK}
    // / {KATA #KATA}
    // / {ALPHA #ALPHA}
    / {NUM [つこ個]? [め目]? #NUM}
    / {('ひと'/'ふた'/'みっ'/'よっ'/'いつ'/'むっ'/'なな'/'やっ'/'ここの') 'つ' [め目]? #COUNT}
    // / {SYMBOL #SYMBOL}
    / {NOUN #NOUN}
    #Noun
}

// 名詞依存のエラーはここの充実で下がる
NOUN =
    / @choice('dic/NOUN.txt')
    / @choice('dic/Noun/rust.txt')
    // / @choice('dic/Noun/ethereum.txt')

BLOCK =
    / '\[' (!'\]' .)* '\]'
    / '\{' (!'\}' .)* '\}'
    / '\(' (!'\)' .)* '\)'
    / '\"' (!'\"' .)* '\"'
    / '\'' (!'\'' .)* '\''
    / '\<' (!'\>' .)* '\>'
    / '\（' (!'\）' .)* '\）'
    / '\「' (!'\」' .)* '\」'
    / '\『' (!'\』' .)* '\』'
    / '\“' (!'\”' .)* '\”'
    / '\‘' (!'\’' .)* '\’'
    / '\【' (!'\】' .)* '\】'
    / '\［' (!'\］' .)* '\］'
    / '\｛' (!'\｝' .)* '\｝'
    / '\〔' (!'\〕' .)* '\〕'


/* 助詞、手入力 */
Postp = {
    POSTP
    #Postp
}
POSTP = @choice('dic/POSTP.txt')


/* 連体詞、辞書から生成 */
Adnominal = {
    ADNM
    #Adnominal
}
ADNM = @choice('dic/ADNM.txt')


/* 副詞、辞書から生成 */
Adverb = {
    ADV
    #Adverb
}
ADV = @choice('dic/ADV.txt')


/* 接続詞、辞書から生成 */
Conjunction = {
    CONJ
    #Conjunction
}
CONJ = @choice('dic/CONJ.txt')




/* 助動詞 */
// 買わせる
Aux_Seru =
    / {'せ' #Mizen} (Aux_Rareru/Aux_Nai/Aux_You/Aux_Mai)
    / {'せ' #Renyo} (Aux_Tai/Aux_Tagaru/Aux_Ta/Aux_Souda1/Aux_Masu/!!(Eol/[はて]/SUBNAI))
    / {'せる' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo/!!(Eol))
    / {'せる' #Rentai} (Aux_Youda)
    / {'せれ' #Katei} (!!('ば'))
    / {'せろ' #Meirei} (!!(Eol))
    / {'せよ' #Meirei} (!!(Eol))

// 起きさせる
Aux_Saseru =
    / {'させ' #Mizen} (Aux_Rareru/Aux_Nai/Aux_You/Aux_Mai)
    / {'させ' #Renyo} (Aux_Tai/Aux_Tagaru/Aux_Ta/Aux_Souda1/Aux_Masu/!!(Eol/[はて]/SUBNAI))
    / {'させる' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo/!!(Eol))
    / {'させる' #Rentai} (Aux_Youda)
    / {'させれ' #Katei} (!!('ば'))
    / {'させろ' #Meirei} (!!(Eol))
    / {'させよ' #Meirei} (!!(Eol))

// 話される
Aux_Reru =
    / {'れ' #Mizen} (Aux_Nai/Aux_You/Aux_Mai)
    / {'れ' #Renyo} (Aux_Tai/Aux_Tagaru/Aux_Ta/Aux_Souda1/Aux_Masu/!!(Eol/[はて]/SUBNAI))
    / {'れる' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo/!!(Eol))
    / {'れる' #Rentai} (Aux_Youda)
    / {'れれ' #Katei} (!!('ば'))
    / {'れろ' #Meirei} (!!(Eol))
    / {'れよ' #Meirei} (!!(Eol))

// 起きられる
Aux_Rareru =
    / {'られ' #Mizen} (Aux_Nai/Aux_You/Aux_Mai)
    / {'られ' #Renyo} (Aux_Tai/Aux_Tagaru/Aux_Ta/Aux_Souda1/Aux_Masu/!!(Eol/[はて]/SUBNAI))
    / {'られる' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo/!!(Eol))
    / {'られる' #Rentai} (Aux_Youda)
    / {'られれ' #Katei} (!!('ば'))
    / {'られろ' #Meirei} (!!(Eol))
    / {'られよ' #Meirei} (!!(Eol))

// 話さない
Aux_Nai =
    / {'なかろ' #Mizen} (Aux_U)
    / {'なかっ' #Renyo} (Aux_Ta)
    / {'なく' #Renyo} (!!(Eol/[はて]/SUBNAI))
    / {'ない' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo/!!(Eol))
    / {'ない' #Rentai} (Aux_Youda)
    / {'なけれ' #Katei} (!!('ば'))

// 話さず
Aux_Nu =
    / {'ず' #Renyo} (!!(Eol/[に]))
    / {'ぬ' #Syushi} (!!(Eol))
    / {'ぬ' #Rentai} (Aux_Youda)
    / {'ね' #Katei} (!!('ば'))

// しません
Aux_Nn =
    / {'ん' #Syushi} (!!(Eol))
    / {'ん' #Rentai} (!!(Eol))  // FIX

// 話しましょう
Aux_U =
    / {'う' #Syushi} (!!(Eol))
    / {'う' #Rentai} (!!(Eol))  // FIX

// 起きよう
Aux_You =
    / {'よう' #Syushi} (!!(Eol))
    / {'よう' #Rentai} (!!(Eol))  // FIX

// 話すまい
Aux_Mai =
    / {'まい' #Syushi} (!!(Eol))
    / {'まい' #Rentai} (!!(Eol))  // FIX

// 話したい
Aux_Tai =
    / {'たかろ' #Mizen} (Aux_U)
    / {'たかっ' #Renyo} (Aux_Ta)
    / {'たく' #Renyo} (!!(Eol/[はて]))
    / {'たい' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo/!!(Eol))
    / {'たい' #Rentai} (Aux_Youda)
    / {'たけれ' #Katei} (!!('ば'))

// 話したがる
Aux_Tagaru =
    / {'たがら' #Mizen} (Aux_Nai/Aux_Nu)
    / {'たがり' #Renyo} (Aux_Souda1/Aux_Masu/!!(Eol))
    / {'たがっ' #Renyo} (Aux_Ta/!!([て]))
    / {'たがる' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo/!!(Eol))
    / {'たがる' #Rentai} (Aux_Youda)
    / {'たがれ' #Katei} (!!('ば'))

// 話した
Aux_Ta =
    / {'たろ' #Mizen} (Aux_U)
    / {'た' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo/!!(Eol))
    / {'た' #Rentai} (Aux_Youda)
    / {'たら' #Katei} (!!('ば')/!!(Eol))

// 呼んだ
Aux_Da1 =
    / {'だろ' #Mizen} (Aux_U)
    / {'だ' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo/!!(Eol))
    / {'だ' #Rentai} (Aux_Youda)
    / {'だら' #Katei} (!!('ば')/!!(Eol))

// 起きそうだ
Aux_Souda1 =
    / {'そうだろ' #Mizen} (Aux_U)
    / {'そうだっ' #Renyo} (Aux_Ta)
    / {'そうで' #Renyo} (!!(Eol))
    / {'そうに' #Renyo} (!!(SUBNAI))  // 補助形容詞の「ない」
    / {'そうだ' #Syushi} (Aux_Souda2/Aux_Rashii/!!(Eol))
    / {'そうな' #Rentai} (Aux_Youda)
    / {'そうなら' #Katei} (!!('ば')/!!(Eol))

// 起きるそうだ
Aux_Souda2 =
    / {'そうで' #Renyo} (!!(Eol))
    / {'そうだ' #Syushi} (!!(Eol))

// 話すようだ
Aux_Youda =
    / {'ようだろ' #Mizen} (Aux_U)
    / {'ようだっ' #Renyo} (Aux_Ta)
    / {'ようで' #Renyo} (!!(Eol))
    / {'ように' #Renyo} (!!(Eol))
    / {'ようだ' #Syushi} (Aux_Souda2/Aux_Rashii/!!(Eol))
    / {'ような' #Rentai} (!!(Eol))
    / {'ようなら' #Katei} (!!('ば')/!!(Eol))


// 話すらしい
Aux_Rashii =
    / {'らしかっ' #Renyo} (Aux_Ta)
    / {'らしく' #Renyo} (!!(Eol/[はて]))
    / {'らしい' #Syushi} (Aux_Souda2/Aux_Daro/Aux_Desyo/!!(Eol))
    / {'らしい' #Rentai} (Aux_Youda)
    / {'らしけれ' #Katei} (!!('ば')/!!(Eol))

// 話しましょう
Aux_Masu =
    / {'ませ' #Mizen} (Aux_Nn)
    / {'ましょ' #Mizen} (Aux_U)
    / {'まし' #Renyo} (Aux_Ta)
    / {'ます' #Syushi} (Aux_Mai/Aux_Desyo/!!(Eol))
    / {'ます' #Rentai} (!!(Eol))  // FIX
    / {'ますれ' #Katei} (!!('ば'))
    / {'ませ' #Meirei} (!!(Eol))
    / {'まし' #Meirei} (!!(Eol))

// 夢だ
Aux_Da2 =
    / {'だろ' #Mizen} (Aux_U)
    / {'だっ' #Renyo} (Aux_Ta)
    / {'で' #Renyo} (!!(SUBNAI))
    / {'だ' #Syushi} (Aux_Souda2/Aux_Rashii/!!(Eol))
    / {'な' #Rentai} (Aux_Youda)
    / {'なら' #Katei} (!!('ば')/!!(Eol))

// 話すだろう
Aux_Daro =
    / {'だろ' #Mizen} (Aux_U)
    / {'で' #Renyo} (!!(Eol))  // 用法不明
    / {'なら' #Katei} (!!('ば')/!!(Eol))

// 夢です
Aux_Desu =
    / {'でしょ' #Mizen} (Aux_U)
    / {'でし' #Renyo} (Aux_Ta)
    / {'です' #Syushi} (!!(Eol))
    / {'です' #Rentai} (!!(Eol))  // FIX

// 話すでしょう
Aux_Desyo =
    / {'でしょ' #Mizen} (Aux_U)
