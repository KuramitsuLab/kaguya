/* KAGUYA文法 */

/*
トップの規則
句点で終わる一文に対応している
文をChunkの繰り返しとみる
*/
Sentence = {
    // (TEN/Chunk)* [。．.\n]? {.* #Remain}  // デバッグモード
    (TEN/Chunk)* EOS
    #Sentence
}


/*
形容詞句や動詞句などのひとかたまりごとにマッチする規則
チョイスの順番はテストして最適な順番を見つけるしかない
*/
Chunk =
    / ConjunctionPhrase  // 接続詞
    / AdverbPhrase  // 副詞
    / AdnominalPhrase  // 連体詞
    / AdjectivePhrase  // 形容詞句
    / AdjectiveVerbPhrase  // 形容動詞句
    / VerbPhrase  // 動詞句
    / NounPhrase  // 名詞句


EOF = !.
EOS = [。．.\n]? EOF
TEN = [、，,:：\n]
Eol = { (TEN/EOS) #Eol }
KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]+
ALPHA = [a-zA-Zａ-ｚＡ-Ｚ]+
NUM = [\-ー\.,．，0-9０-９一二三四五六七八九十百千万億兆]
SYMBOL =
    / [^＾~〜\-ー_＿!！?？#＃$＄&＆=＝@＠*＊+＋/・]
    / [\[\]\(\)\<\>\{\}\'\"\`「」（）＜＞『』［］【】”“｀”“’‘｛｝〔〕]


/*
動詞句
スモールケースから順に
1. 動詞の活用した形のみの場合：話す、話し
2. 動詞の活用した形+助動詞の場合：話した、話される
3. 動詞の活用した形+動詞句の場合：話し始める
4. 動詞の活用した形+助動詞+動詞句の場合：話され始める
*/
VerbPhrase = {
      Verb AuxVerbForVerb PostpPhrase
    / Verb AuxVerbForVerb !!(Chunk/Eol)
    / Verb PostpPhrase
    / Verb !!(Chunk/Eol)
    #VerbPhrase
}
AuxVerbForVerb = {
    AFTERVERB
    #AuxVerbForVerb
}


/*
形容詞
組み方は動詞と一緒
*/
AdjectivePhrase = {
      Adjective AuxVerbForAdj PostpPhrase
    / Adjective AuxVerbForAdj !!(Chunk/Eol)
    / Adjective PostpPhrase
    / Adjective !!(Chunk/Eol)
    #AdjectivePhrase
}
AuxVerbForAdj = {
    AFTERADJ
    #AuxVerbForAdj
}


/*
形容動詞
組み方は動詞と一緒
*/
AdjectiveVerbPhrase = {
      AdjectiveVerb AuxVerbForAdjv PostpPhrase
    / AdjectiveVerb AuxVerbForAdjv !!(Chunk/Eol)
    / AdjectiveVerb PostpPhrase
    / AdjectiveVerb !!(Chunk/Eol)
    #AdjectiveVerbPhrase
}
AuxVerbForAdjv = {
    AFTERADJV
    #AuxVerbForAdj
}


/*
名詞句
名詞にも助動詞が付属語としてつきうる
*/
NounPhrase = {
      Noun AuxVerbForNoun PostpPhrase
    / Noun AuxVerbForNoun !!(Chunk/Eol)
    / Noun PostpPhrase
    / Noun !!(Chunk/Eol)
    #NounPhrase
}
AuxVerbForNoun = {
    AFTERNOUN
    #AuxVerbForNoun
}


/*
接続詞句
接続詞に助詞が続くことはないものとしている
*/
ConjunctionPhrase = {
    //   Conjunction PostpPhrase
    Conjunction !!(Chunk/Eol)
    #ConjunctionPhrase
}


/*
連体詞句
「この・その・あの・どの」には助動詞「ようだ」がつきうる
連体詞には助詞は続かない（はず）
*/
AdnominalPhrase = {
      KoSoADo AuxVerbForAdnm PostpPhrase
    / KoSoADo AuxVerbForAdnm !!(Chunk/Eol)
    / Adnominal !!(Chunk/Eol)
    // / Adnominal !PostPhrase !!(Chunk/Eol)
    #AdnominalPhrase
}
AuxVerbForAdnm = {
    AFTERADNM
    #AuxVerbForAdnm
}
KoSoADo = { [こそあど] 'の' #Adnominal }


/*
副詞句
*/
AdverbPhrase = {
      Adverb AuxVerbForAdv PostpPhrase  // すぐ です ので
    / Adverb PostpNo !!(Chunk/Eol)  // しばらく の 間
    / Adverb AuxVerbForAdv !!(Chunk/Eol)  // すぐ です
    / Adverb !!(Chunk/Eol)  // もっと
    #AdverbPhrase
}
AuxVerbForAdv = {
    AFTERNOUN
    #AuxVerbForAdv
}


/*
助詞句
助詞の「の」だけに続く助動詞があるため別扱いしている
助詞に続く助動詞「な」はその後が限定的なので別扱いしている
チョイスの順番決めが難しい
*/
PostpPhrase = {
      Postp AuxVerbNa !!('の') PostpPhrase
    / PostpNo AfterPostpNo PostpPhrase
    / PostpNo AfterPostpNo !!(Chunk/Eol)
    / PostpNo PostpPhrase
    / PostpNo !!(Chunk/Eol)
    / Postp AfterPostp PostpPhrase
    / Postp AfterPostp !!(Chunk/Eol)
    / Postp PostpPhrase
    / Postp !!(Chunk/Eol)
    #PostpPhrase
}
PostpNo = { 'の' #PostpNo }
AfterPostpNo = { AFTERPOSTPNO #AuxVerbForPostpNo }
AfterPostp = { AFTERPOSTP #AuxVerbForPostp }
AuxVerbNa = { 'な' #AuxVerbForPostp }


/*
動詞の語幹と活用語尾
活用順にしており、活用語尾が同じものまとめている
*/
Verb =
    / Mizen
    / Renyo
    / Syushi
    / Rentai
    / Katei
    / Meirei
    // / HiraVerb


/*
動詞の未然形活用
未然形はほぼ確実に助動詞が繋がるため先読みで誤マッチを抑制している
これで「止まる」の先頭文字列「止ま」が「止む」の未然形「止ま」にマッチしない
以下、他の活用も同様
*/
Mizen = {
      VERB5KA [かこ] !!(AFTERV5MIZEN)
    / VERB5SA [さそ] !!(AFTERV5MIZEN)
    / VERB5TA [たと] !!(AFTERV5MIZEN)
    / VERB5NA [なの] !!(AFTERV5MIZEN)
    / VERB5MA [まも] !!(AFTERV5MIZEN)
    / VERB5RA [らろ] !!(AFTERV5MIZEN)
    / VERB5WA [わお] !!(AFTERV5MIZEN)
    / VERB5GA [がご] !!(AFTERV5MIZEN)
    / VERB5BA [ばぼ] !!(AFTERV5MIZEN)
    / VERB5KASOKU [かこ] !!(AFTERV5MIZEN)
    / VERB1 !!(AFTERV1MIZEN)
    / SAHEN_SURU [さ] !!(AFTERVSAMIZEN)
    / SAHEN_ZURU [ざ] !!(AFTERVSAMIZEN)
    / (KANJI/KATA)* [さ] !!(AFTERVSAMIZEN)
    / KAHEN? [来] !!(AFTERVKAMIZEN)
    / SAHEN_SURU [し] !!(AFTERVSAMIZEN)
    / SAHEN_ZURU [じ] !!(AFTERVSAMIZEN)
    / (KANJI/KATA)* [し] !!(AFTERVSAMIZEN)
    / KAHEN? [こ] !!(AFTERVKAMIZEN)  // 命令形の活用語尾「こい」と競合するかも
    / SAHEN_SURU [せ] !!(AFTERVSAMIZEN)  // 命令形の活用語尾「せよ」と競合するかも
    / SAHEN_ZURU [ぜ] !!(AFTERVSAMIZEN)  // 命令形の活用語尾「ぜよ」と競合するかも
    / (KANJI/KATA)* [せ] !!(AFTERVSAMIZEN)  // 命令形の活用語尾「せよ」と競合するかも
    #Mizen
}

Renyo = {
      VERB5KA [きい] !!(AFTERV5RENYO/AfterRenyo)
    / VERB5SA [し] !!(AFTERV5RENYO/AfterRenyo)
    / VERB5TA [ちっ] !!(AFTERV5RENYO/AfterRenyo)
    / VERB5NA [にん] !!(AFTERV5RENYO/AfterRenyo)
    / VERB5MA [みん] !!(AFTERV5RENYO/AfterRenyo)
    / VERB5RA [りっ] !!(AFTERV5RENYO/AfterRenyo)
    / VERB5WA [いっ] !!(AFTERV5RENYO/AfterRenyo)
    / VERB5GA [ぎい] !!(AFTERV5RENYO/AfterRenyo)
    / VERB5BA [びん] !!(AFTERV5RENYO/AfterRenyo)
    / VERB5KASOKU [きっ] !!(AFTERV5RENYO/AfterRenyo)
    / VERB1 !!(AFTERV1RENYO/AfterRenyo)
    / KAHEN? [き] !!(AFTERVKARENYO/AfterRenyo)
    / KAHEN? [来] !!(AFTERVKARENYO/AfterRenyo)
    / SAHEN_SURU [し] !!(AFTERVSARENYO/AfterRenyo)
    / SAHEN_ZURU [じ] !!(AFTERVSARENYO/AfterRenyo)
    / (KANJI/KATA)* [し] !!(AFTERVSARENYO/AfterRenyo)
    #Renyo
}
AfterRenyo = {
      [てで]
    / VerbPhrase
    / TEN
    / PostpPhrase
    #AfterRenyo
}

Syushi = {
      VERB5KA [く] !!(AFTERV5SYUSHI/AfterSyushi)
    / VERB5SA [す] !!(AFTERV5SYUSHI/AfterSyushi)
    / VERB5TA [つ] !!(AFTERV5SYUSHI/AfterSyushi)
    / VERB5NA [ぬ] !!(AFTERV5SYUSHI/AfterSyushi)
    / VERB5MA [む] !!(AFTERV5SYUSHI/AfterSyushi)
    / VERB5RA [る] !!(AFTERV5SYUSHI/AfterSyushi)
    / VERB5WA [う] !!(AFTERV5SYUSHI/AfterSyushi)
    / VERB5GA [ぐ] !!(AFTERV5SYUSHI/AfterSyushi)
    / VERB5BA [ぶ] !!(AFTERV5SYUSHI/AfterSyushi)
    / VERB5KASOKU [く] !!(AFTERV5SYUSHI/AfterSyushi)
    / VERB1 [る] !!(AFTERV1SYUSHI/AfterSyushi)
    / KAHEN? ('くる' / '来る') !!(AFTERVKASYUSHI/AfterSyushi)
    / SAHEN_SURU 'する' !!(AFTERVSASYUSHI/AfterSyushi)
    / SAHEN_ZURU 'ずる' !!(AFTERVSASYUSHI/AfterSyushi)
    / (KANJI/KATA)* 'する' !!(AFTERVSASYUSHI/AfterSyushi)
    #Syushi
}
AfterSyushi = {
      PostpPhrase
    / Eol
    #AfterSyushi
}

Rentai = {
      VERB5KA [く] !!(AFTERV5RENTAI/AfterRentai)
    / VERB5SA [す] !!(AFTERV5RENTAI/AfterRentai)
    / VERB5TA [つ] !!(AFTERV5RENTAI/AfterRentai)
    / VERB5NA [ぬ] !!(AFTERV5RENTAI/AfterRentai)
    / VERB5MA [む] !!(AFTERV5RENTAI/AfterRentai)
    / VERB5RA [る] !!(AFTERV5RENTAI/AfterRentai)
    / VERB5WA [う] !!(AFTERV5RENTAI/AfterRentai)
    / VERB5GA [ぐ] !!(AFTERV5RENTAI/AfterRentai)
    / VERB5BA [ぶ] !!(AFTERV5RENTAI/AfterRentai)
    / VERB5KASOKU [く] !!(AFTERV5RENTAI/AfterRentai)
    / VERB1 [る] !!(AFTERV1RENTAI/AfterRentai)
    / KAHEN? ('くる' / '来る') !!(AFTERVKARENTAI/AfterRentai)
    / SAHEN_SURU 'する' !!(AFTERVSARENTAI/AfterRentai)
    / SAHEN_ZURU 'ずる' !!(AFTERVSARENTAI/AfterRentai)
    / (KANJI/KATA)* 'する' !!(AFTERVSARENTAI/AfterRentai)
    #Rentai
}
AfterRentai = {
      NounPhrase
    / PostpPhrase
    #AfterRentai
}


Katei = {
      VERB5KA [け] !!'ば'
    / VERB5SA [せ] !!'ば'
    / VERB5TA [て] !!'ば'
    / VERB5NA [ね] !!'ば'
    / VERB5MA [め] !!'ば'
    / VERB5RA [れ] !!'ば'
    / VERB5WA [え] !!'ば'
    / VERB5GA [げ] !!'ば'
    / VERB5BA [べ] !!'ば'
    / VERB5KASOKU [け] !!'ば'
    / VERB1 [れ] !!'ば'
    / KAHEN? ('くれ' / '来れ') !!'ば'
    / SAHEN_SURU 'すれ' !!'ば'
    / SAHEN_ZURU 'ずれ' !!'ば'
    / (KANJI/KATA)+ 'すれ' !!'ば'
    #Katei
}

Meirei = {
      VERB5KA [け] !!(Eol)
    / VERB5SA [せ] !!(Eol)
    / VERB5TA [て] !!(Eol)
    / VERB5NA [ね] !!(Eol)
    / VERB5MA [め] !!(Eol)
    / VERB5RA [れ] !!(Eol)
    / VERB5WA [え] !!(Eol)
    / VERB5GA [げ] !!(Eol)
    / VERB5BA [べ] !!(Eol)
    / VERB5KASOKU [け] !!(Eol)
    / VERB1 [ろよ] !!(Eol)
    / KAHEN? ('こい' / '来い') !!(Eol)
    / SAHEN_SURU ('しろ' / 'せよ') !!(Eol)
    / SAHEN_ZURU ('じろ' / 'ぜよ') !!(Eol)
    / (KANJI/KATA)+ ('しろ' / 'せよ') !!(Eol)
    / ('ください'/'下さい') !!(Eol)  // 敬語動詞「下さる」の命令形は特殊
    #Meirei
}

/*
ひらがな動詞も辞書に含めたので不要
HiraVerb =
    / {HV [らろ] #Mizen} &({AFTERV5MIZEN})
    / {HV [りっ] #Renyo} &({AFTERV5RENYO}/{[てで]}/Chunk/{TEN})
    / {HV 'る' #Syushi} &({AFTERV5SYUSHI}/PostpPhrase/Eol)
    / {HV 'る' #Rentai} &({AFTERV5RENTAI}/NounPhrase/PostpPhrase)
    / {HV 'れ' #KateiMeirei} &({'ば'}/Eol)

HV = [あい]
*/

/*
活用型ごとの動詞の語幹を辞書から生成
「行く」はカ行五段活用だが、イ音便ではなく促音便で活用が異なるため別枠
*/
VERB5KA = @choice('dic/Verb/VERB5KA.txt')
VERB5SA = @choice('dic/Verb/VERB5SA.txt')
VERB5TA = @choice('dic/Verb/VERB5TA.txt')
VERB5NA = @choice('dic/Verb/VERB5NA.txt')
VERB5MA = @choice('dic/Verb/VERB5MA.txt')
VERB5RA = @choice('dic/Verb/VERB5RA.txt')
VERB5WA = @choice('dic/Verb/VERB5WA.txt')
VERB5GA = @choice('dic/Verb/VERB5GA.txt')
VERB5BA = @choice('dic/Verb/VERB5BA.txt')
VERB1 = @choice('dic/Verb/VERB1.txt')
KAHEN = @choice('dic/Verb/KAHEN.txt')
SAHEN_SURU = @choice('dic/Verb/SAHEN_SURU.txt')
SAHEN_ZURU = @choice('dic/Verb/SAHEN_ZURU.txt')
VERB5KASOKU = '行'



/*
形容詞の語幹と活用語尾
実験的に助動詞や助詞、動詞を先読みで入れている
語幹は辞書の"Adjective.dic"と"Noun.nai.dic"から生成
*/
Adjective =
    / {ADJ 'かろ' !!(AFTERADJMIZEN) #ADJMizen}
    / {ADJ 'かっ' !!(AFTERADJRENYO/'たり') #ADJRenyo}
    / {ADJ 'く' !!([ては]/Chunk) #ADJRenyo}
    / {ADJ 'けれ' !!('ば') #ADJKatei}
    / {ADJ 'い' !!(AFTERADJSYUSHI/PostpPhrase/Eol) #ADJSyushi}
    / {ADJ 'い' !!(AFTERADJRENTAI/NounPhrase/PostpPhrase) #ADJRentai}
    / {ADJ !!(AFTERADJGOKAN) #ADJGokan}

ADJ = @choice('dic/ADJ.txt')

/*
形容動詞の語幹と活用語尾
形容詞とやっていることは同じ
語幹は辞書の"Noun.adjv.dic"から生成
*/
AdjectiveVerb =
    / {ADJV 'だろ' !!(AFTERADJVMIZEN) #ADJVMizen}
    / {ADJV 'だっ' !!(AFTERADJVRENYO) #ADJVRenyo}
    / {ADJV 'で' !!([はも]/SUBARU/SUBNAI/TEN) #ADJVRenyo}
    / {ADJV 'に' !!(Chunk) #ADJVRenyo}
    / {ADJV 'だ' !!(AFTERADJVSYUSHI/PostpPhrase/Eol) #ADJVSyushi}
    / {ADJV 'な' !!(AFTERADJVRENTAI/NounPhrase) #ADJVRentai}
    / {ADJV 'なら' !!('ば'?) #ADJVKatei}
    / {ADJV !!(AFTERADJVGOKAN) #ADJVGokan}

ADJV = @choice('dic/ADJV.txt')

/*
補助動詞の「ある」
補助形容詞の「ない」
*/
SUBARU = 'あ' [らりるれろっ]
SUBNAI = 'な' ('かろ' / 'かっ' / 'く' / 'い' / 'けれ')


/*
名詞
漢字カタカナアルファベット数字
混合した場合も可（例：アップル社）
括弧で括ったものも名詞
*/
Noun = {
      BLOCK
    / {NOUN* #NounInDict}
    / KATA
    / ALPHA
    / NUM [つこ個] [め目]?
    / ('ひと'/'ふた'/'みっ'/'よっ'/'いつ'/'むっ'/'なな'/'やっ'/'ここの') 'つ' [め目]?
    / NUM
    / SYMBOL
    #Noun
}


NOUN = @choice('dic/NOUN.txt') / @choice('dic/Noun/rust.txt')

BLOCK =
    / '\[' (!'\]' .)* '\]'
    / '\{' (!'\}' .)* '\}'
    / '\(' (!'\)' .)* '\)'
    / '\"' (!'\"' .)* '\"'
    / '\'' (!'\'' .)* '\''
    / '\<' (!'\>' .)* '\>'
    / '\（' (!'\）' .)* '\）'
    / '\「' (!'\」' .)* '\」'
    / '\『' (!'\』' .)* '\』'
    / '\“' (!'\”' .)* '\”'
    / '\‘' (!'\’' .)* '\’'
    / '\【' (!'\】' .)* '\】'
    / '\［' (!'\］' .)* '\］'
    / '\｛' (!'\｝' .)* '\｝'
    / '\〔' (!'\〕' .)* '\〕'


/* 助動詞、gen_auxverb.pyで生成 */
AuxVerb = {
    AUXVERB
    #AuxVerbs
}
AUXVERB = @choice('dic/AUXVERB.txt')

AFTERADJMIZEN = @choice('dic/Auxverb/ADJMIZEN.txt')
AFTERADJRENYO = @choice('dic/Auxverb/ADJRENYO.txt')
AFTERADJSYUSHI = @choice('dic/Auxverb/ADJSYUSHI.txt')
AFTERADJRENTAI = @choice('dic/Auxverb/ADJRENTAI.txt')
AFTERADJGOKAN = @choice('dic/Auxverb/ADJGOKAN.txt')

AFTERADJVMIZEN = @choice('dic/Auxverb/ADJVMIZEN.txt')
AFTERADJVRENYO = @choice('dic/Auxverb/ADJVRENYO.txt')
AFTERADJVSYUSHI = @choice('dic/Auxverb/ADJVSYUSHI.txt')
AFTERADJVRENTAI = @choice('dic/Auxverb/ADJVRENTAI.txt')
AFTERADJVGOKAN = @choice('dic/Auxverb/ADJVGOKAN.txt')

AFTERV1MIZEN = @choice('dic/Auxverb/V1MIZEN.txt')
AFTERV1RENYO = @choice('dic/Auxverb/V1RENYO.txt')
AFTERV1SYUSHI = @choice('dic/Auxverb/V1SYUSHI.txt')
AFTERV1RENTAI = @choice('dic/Auxverb/V1RENTAI.txt')

AFTERV5MIZEN = @choice('dic/Auxverb/V5MIZEN.txt')
AFTERV5RENYO = @choice('dic/Auxverb/V5RENYO.txt')
AFTERV5SYUSHI = @choice('dic/Auxverb/V5SYUSHI.txt')
AFTERV5RENTAI = @choice('dic/Auxverb/V5RENTAI.txt')

AFTERVKAMIZEN = @choice('dic/Auxverb/VKAMIZEN.txt')
AFTERVKARENYO = @choice('dic/Auxverb/VKARENYO.txt')
AFTERVKASYUSHI = @choice('dic/Auxverb/VKASYUSHI.txt')
AFTERVKARENTAI = @choice('dic/Auxverb/VKARENTAI.txt')

AFTERVSAMIZEN = @choice('dic/Auxverb/VSAMIZEN.txt')
AFTERVSARENYO = @choice('dic/Auxverb/VSARENYO.txt')
AFTERVSASYUSHI = @choice('dic/Auxverb/VSASYUSHI.txt')
AFTERVSARENTAI = @choice('dic/Auxverb/VSARENTAI.txt')

AFTERVERB = @choice('dic/Auxverb/VERB.txt')
AFTERADJ = @choice('dic/Auxverb/ADJ.txt')
AFTERADJV = @choice('dic/Auxverb/ADJV.txt')
AFTERADNM = @choice('dic/Auxverb/ADNM.txt')
AFTERNOUN = @choice('dic/Auxverb/NOUN.txt')
AFTERPOSTPNO = @choice('dic/Auxverb/POSTPNO.txt')
AFTERPOSTP = @choice('dic/Auxverb/POSTP.txt')


/* 助詞、手入力 */
Postp = {
    POSTP
    #Postp
}
POSTP = @choice('dic/POSTP.txt')


/* 連体詞、辞書から生成 */
Adnominal = {
    ADNM
    #Adnominal
}
ADNM = @choice('dic/ADNM.txt')


/* 副詞、辞書から生成 */
Adverb = {
    ADV
    #Adverb
}
ADV = @choice('dic/ADV.txt')


/* 接続詞、辞書から生成 */
Conjunction = {
    CONJ
    #Conjunction
}
CONJ = @choice('dic/CONJ.txt')
