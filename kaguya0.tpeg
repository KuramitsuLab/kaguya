/* KAGUYA文法 */

/*
トップの規則
入力をChunkの繰り返しとみる
*/
Sentence = {
    (Chunk/TEN)* EOS
    #Sentence
}

/*
形容詞や形容動詞、動詞や連体詞などのひとかたまりごとにマッチする規則
*/
Chunk = {
      AdjectivePhrase  // 形容詞or形容動詞 助動詞?
    / VerbPhrase  // 動詞 助動詞?
    / NounPhrase  // 名詞 助動詞?
    / {[こそあど] 'の' AFTERADNM #AdnominalPhrase}  // 助動詞がつきうる連体詞
    / Adnominal  // 連体詞
    / {[の] AFTERPOSTPNO #PostpPhrase}
    / Adverb  // 副詞
    / {Postp AFTERPOSTP #PostpPhrase}
    / Postp
    #Chunk
}

example Sentence 要求は認められないので代案を用意せよ
example Sentence 僕は転びそうだったが持ちこたえた
example Sentence 紅茶を入れますわ
example Sentence 駐車場に車を止める
example Sentence 駐車場に車が止まる
example Sentence 駐車場に車が止まらない
example Sentence 雨が止む
example Sentence 酒は飲んでも飲まれるな
example Sentence スマホを充電してあげる
example Sentence 変数を定義して数値を代入する
example Sentence 会いたくて震える
example Sentence 期限を過ぎなければ提出を認めるだろう
example Sentence 階段を駆け上がったので息が切れた
example Sentence 雨が降ったらしいけれど家に居たので何も知らなかった
example Sentence 変数に数値を代入し計算を実行し結果をコンソールに出力する
example Sentence 変数に数値を代入し、計算を実行し、結果をコンソールに出力する。

EOF = !.
EOS = [。．.\n] / EOF
EOL = [。．.\n] / TEN / [:：] / EOF
TEN = [、，,\n]
KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]
ALPHA = [a-zA-Zａ-ｚＡ-Ｚ]
NUM = [0-9０-９一二三四五六七八九十百千万億兆京垓秭穰溝澗正載極]
    /'恒河沙'/'阿僧祇'/'那由他'/'不可思議'/'無量大数'
SYMBOL = [-~〜ー.,]


/*
動詞節
スモールケースから順に
1. 動詞の活用した形のみの場合：話す、話し
2. 動詞の活用した形+助動詞の場合：話した、話される
3. 動詞の活用した形+動詞節の場合：話し始める
4. 動詞の活用した形+助動詞+動詞節の場合：話され始める
*/
VerbPhrase = {
      Verb AuxVerbForVerb VerbPhrase
    / {Verb AuxVerbForVerb &Chunk #LookAHead}
    / Verb VerbPhrase
    / Verb
    #VerbPhrase
}

AuxVerbForVerb = {
    AFTERVERB
    #AuxVerbForVerb
}

example VerbPhrase 話したく思う
example VerbPhrase 攻撃され始めた
example VerbPhrase 話し終わる
example VerbPhrase 駆け上がる
example VerbPhrase 立ち止まる
example VerbPhrase 止まる
example VerbPhrase 止まらない

/*
形容詞または形容動詞
組み方は動詞と一緒
*/
AdjectivePhrase = {
      (Adjective/AdjectiveVerb) AuxVerbForAdj AdjectivePhrase
    / {(Adjective/AdjectiveVerb) AuxVerbForAdj &Chunk #LookAHead}
    / (Adjective/AdjectiveVerb) AdjectivePhrase
    / (Adjective/AdjectiveVerb)
    #AdjectivePhrase
}
AuxVerbForAdj = {
    AFTERADJ
    #AuxVerbForAdj
}

example AdjectivePhrase 赤いボール
example AdjectivePhrase 赤かったボール
example AdjectivePhrase うるさい
example AdjectivePhrase うるさかった
example AdjectivePhrase うるさくない
example AdjectivePhrase うるさかろう
example AdjectivePhrase うるさそうだった
example AdjectivePhrase うるさいらしい
example AdjectivePhrase うるさいでしょう
example AdjectivePhrase 赤くない
example AdjectivePhrase 赤くなく青い
example AdjectivePhrase 赤くかっこいい
example AdjectivePhrase 健康な人
example AdjectivePhrase 健康だった
example AdjectivePhrase 健康でしょう
example AdjectivePhrase ぞっこんだ
example AdjectivePhrase なめい
example AdjectivePhrase なめかった
example AdjectivePhrase なめらかだ
example AdjectivePhrase なめらかなやわらかい布

/*
名詞（体言）
体言にも助動詞が付属語としてつきうる
*/
NounPhrase = {
    Noun AuxVerbForNoun?
    #NounPhrase
}
AuxVerbForNoun = {
    AFTERNOUN
    #AuxVerbForNoun
}


/*
動詞の語幹と活用語尾
活用順にしており、活用語尾が同じものまとめている
*/
Verb =
    / Mizen
    / Renyo
    / Syushi
    / Rentai
    / Katei
    / Meirei
    / HiraVerb

example Verb 歩く
example Verb 認める
example Verb 会う
example Verb 止む
example Verb 止まる
example Verb 止める

/*
動詞の未然形活用
未然形はほぼ確実に助動詞が繋がるため先読みで誤マッチを抑制している
これで「止まる」の先頭文字列「止ま」が「止む」の未然形「止ま」にマッチしない
以下、他の活用も同様
*/
Mizen = {
      {VERB5KA [かこ] #Mizen} &AFTERV5MIZEN
    / {VERB5SA [さそ] #Mizen} &AFTERV5MIZEN
    / {VERB5TA [たと] #Mizen} &AFTERV5MIZEN
    / {VERB5NA [なの] #Mizen} &AFTERV5MIZEN
    / {VERB5MA [まも] #Mizen} &AFTERV5MIZEN
    / {VERB5RA [らろ] #Mizen} &AFTERV5MIZEN
    / {VERB5WA [わお] #Mizen} &AFTERV5MIZEN
    / {VERB5GA [がご] #Mizen} &AFTERV5MIZEN
    / {VERB5BA [ばぼ] #Mizen} &AFTERV5MIZEN
    / {VERB5KASOKU [かこ] #Mizen} &AFTERV5MIZEN
    / {VERB1 #Mizen} &AFTERV1MIZEN
    / {SAHEN_SURU [さ] #Mizen} &AFTERVSAMIZEN
    / {SAHEN_ZURU [ざ] #Mizen} &AFTERVSAMIZEN
    / {(KANJI/KATA)* [さ] #Mizen} &AFTERVSAMIZEN
    / {KAHEN? [来] #Mizen} &AFTERVKAMIZEN
    / {SAHEN_SURU [し] #Mizen} &AFTERVSAMIZEN
    / {SAHEN_ZURU [じ] #Mizen} &AFTERVSAMIZEN
    / {(KANJI/KATA)* [し] #Mizen} &AFTERVSAMIZEN
    / {KAHEN? [こ] #Mizen} &AFTERVKAMIZEN  // 命令形の活用語尾「こい」と競合するかも
    / {SAHEN_SURU [せ] #Mizen} &AFTERVSAMIZEN  // 命令形の活用語尾「せよ」と競合するかも
    / {SAHEN_ZURU [ぜ] #Mizen} &AFTERVSAMIZEN  // 命令形の活用語尾「ぜよ」と競合するかも
    / {(KANJI/KATA)* [せ] #Mizen} &AFTERVSAMIZEN  // 命令形の活用語尾「せよ」と競合するかも
    #LookAHead
}

Renyo = {
      {VERB5KA [きい] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5SA [し] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5TA [ちっ] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5NA [にん] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5MA [みん] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5RA [りっ] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5WA [いっ] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5GA [ぎい] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5BA [びん] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5KASOKU [きっ] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB1 #Renyo} &(AFTERV1RENYO/[てで]/Verb/TEN)
    / {KAHEN? [き] #Renyo} &(AFTERVKARENYO/[てで]/Verb/TEN)
    / {KAHEN? [来] #Renyo} &(AFTERVKARENYO/[てで]/Verb/TEN)
    / {SAHEN_SURU [し] #Renyo} &(AFTERVSARENYO/[てで]/Verb/TEN)
    / {SAHEN_ZURU [じ] #Renyo} &(AFTERVSARENYO/[てで]/Verb/TEN)
    / {(KANJI/KATA)* [し] #Renyo} &(AFTERVSARENYO/[てで]/Verb/TEN)
    #LookAHead
}

Syushi = {
      {VERB5KA [く] #Syushi} &(AFTERV5SYUSHI/POSTP/EOL)
    / {VERB5SA [す] #Syushi} &(AFTERV5SYUSHI/POSTP/EOL)
    / {VERB5TA [つ] #Syushi} &(AFTERV5SYUSHI/POSTP/EOL)
    / {VERB5NA [ぬ] #Syushi} &(AFTERV5SYUSHI/POSTP/EOL)
    / {VERB5MA [む] #Syushi} &(AFTERV5SYUSHI/POSTP/EOL)
    / {VERB5RA [る] #Syushi} &(AFTERV5SYUSHI/POSTP/EOL)
    / {VERB5WA [う] #Syushi} &(AFTERV5SYUSHI/POSTP/EOL)
    / {VERB5GA [ぐ] #Syushi} &(AFTERV5SYUSHI/POSTP/EOL)
    / {VERB5BA [ぶ] #Syushi} &(AFTERV5SYUSHI/POSTP/EOL)
    / {VERB5KASOKU [く] #Syushi} &(AFTERV5SYUSHI/POSTP/EOL)
    / {VERB1 [る] #Syushi} &(AFTERV1SYUSHI/POSTP/EOL)
    / {KAHEN? ('くる' / '来る') #Syushi} &(AFTERVKASYUSHI/POSTP/EOL)
    / {SAHEN_SURU 'する' #Syushi} &(AFTERVSASYUSHI/POSTP/EOL)
    / {SAHEN_ZURU 'ずる' #Syushi} &(AFTERVSASYUSHI/POSTP/EOL)
    / {(KANJI/KATA)* 'する' #Syushi} &(AFTERVSASYUSHI/POSTP/EOL)
    #LookAHead
}

Rentai = {
      {VERB5KA [く] #Rentai} &(AFTERV5RENTAI/Noun/POSTP)
    / {VERB5SA [す] #Rentai} &(AFTERV5RENTAI/Noun/POSTP)
    / {VERB5TA [つ] #Rentai} &(AFTERV5RENTAI/Noun/POSTP)
    / {VERB5NA [ぬ] #Rentai} &(AFTERV5RENTAI/Noun/POSTP)
    / {VERB5MA [む] #Rentai} &(AFTERV5RENTAI/Noun/POSTP)
    / {VERB5RA [る] #Rentai} &(AFTERV5RENTAI/Noun/POSTP)
    / {VERB5WA [う] #Rentai} &(AFTERV5RENTAI/Noun/POSTP)
    / {VERB5GA [ぐ] #Rentai} &(AFTERV5RENTAI/Noun/POSTP)
    / {VERB5BA [ぶ] #Rentai} &(AFTERV5RENTAI/Noun/POSTP)
    / {VERB5KASOKU [く] #Rentai} &(AFTERV5RENTAI/Noun/POSTP)
    / {VERB1 [る] #Rentai} &(AFTERV1RENTAI/Noun/POSTP)
    / {KAHEN? ('くる' / '来る') #Rentai} &(AFTERVKARENTAI/Noun/POSTP)
    / {SAHEN_SURU 'する' #Rentai} &(AFTERVSARENTAI/Noun/POSTP)
    / {SAHEN_ZURU 'ずる' #Rentai} &(AFTERVSARENTAI/Noun/POSTP)
    / {(KANJI/KATA)* 'する' #Rentai} &(AFTERVSARENTAI/Noun/POSTP)
    #LookAHead
}


Katei = {
      {VERB5KA [け] #Katei} &'ば'
    / {VERB5SA [せ] #Katei} &'ば'
    / {VERB5TA [て] #Katei} &'ば'
    / {VERB5NA [ね] #Katei} &'ば'
    / {VERB5MA [め] #Katei} &'ば'
    / {VERB5RA [れ] #Katei} &'ば'
    / {VERB5WA [え] #Katei} &'ば'
    / {VERB5GA [げ] #Katei} &'ば'
    / {VERB5BA [べ] #Katei} &'ば'
    / {VERB5KASOKU [け] #Katei} &'ば'
    / {VERB1 [れ] #Katei} &'ば'
    / {KAHEN? ('くれ' / '来れ') #Katei} &'ば'
    / {SAHEN_SURU 'すれ' #Katei} &'ば'
    / {SAHEN_ZURU 'ずれ' #Katei} &'ば'
    / {(KANJI/KATA)+ 'すれ' #Katei} &'ば'
    #LookAHead
}

Meirei = {
      {VERB5KA [け] #Meirei} &EOL
    / {VERB5SA [せ] #Meirei} &EOL
    / {VERB5TA [て] #Meirei} &EOL
    / {VERB5NA [ね] #Meirei} &EOL
    / {VERB5MA [め] #Meirei} &EOL
    / {VERB5RA [れ] #Meirei} &EOL
    / {VERB5WA [え] #Meirei} &EOL
    / {VERB5GA [げ] #Meirei} &EOL
    / {VERB5BA [べ] #Meirei} &EOL
    / {VERB5KASOKU [け] #Meirei} &EOL
    / {VERB1 [ろよ] #Meirei} &EOL
    / {KAHEN? ('こい' / '来い') #Meirei} &EOL
    / {SAHEN_SURU ('しろ' / 'せよ') #Meirei} &EOL
    / {SAHEN_ZURU ('じろ' / 'ぜよ') #Meirei} &EOL
    / {(KANJI/KATA)+ ('しろ' / 'せよ') #Meirei} &EOL
    #LookAHead
}

HiraVerb = {
      {HV [らろ] #Mizen} &AFTERV5MIZEN
    / {HV [りっ] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {HV 'る' #Syushi} &(AFTERV5SYUSHI/POSTP/EOL)
    / {HV 'る' #Rentai} &(AFTERV5RENTAI/Noun/POSTP)
    / {HV 'れ' #KateiMeirei} &('ば'/EOL)
    #LookAHead
}
HV = [あい]

/*
活用型ごとの動詞の語幹を辞書から生成
「行く」はカ行五段活用だが、イ音便ではなく促音便で活用が異なるため別枠
*/
VERB5KA = @choice('dic/Verb/VERB5KA.txt')
VERB5SA = @choice('dic/Verb/VERB5SA.txt')
VERB5TA = @choice('dic/Verb/VERB5TA.txt')
VERB5NA = @choice('dic/Verb/VERB5NA.txt')
VERB5MA = @choice('dic/Verb/VERB5MA.txt')
VERB5RA = @choice('dic/Verb/VERB5RA.txt')
VERB5WA = @choice('dic/Verb/VERB5WA.txt')
VERB5GA = @choice('dic/Verb/VERB5GA.txt')
VERB5BA = @choice('dic/Verb/VERB5BA.txt')
VERB1 = @choice('dic/Verb/VERB1.txt')
KAHEN = @choice('dic/Verb/KAHEN.txt')
SAHEN_SURU = @choice('dic/Verb/SAHEN_SURU.txt')
SAHEN_ZURU = @choice('dic/Verb/SAHEN_ZURU.txt')
VERB5KASOKU = '行'



/*
形容詞の語幹と活用語尾
実験的に助動詞や助詞、動詞を先読みで入れている
語幹は辞書の"Adjective.dic"と"Noun.nai.dic"から生成
*/
Adjective = {
      {ADJ 'かろ' #ADJMizen} &AFTERADJMIZEN
    / {ADJ 'かっ' #ADJRenyo} &AFTERADJRENYO
    / {ADJ 'く' #ADJRenyo} &([ては]/Verb/Adjective/AdjectiveVerb)
    / {ADJ 'けれ' #ADJKatei} &'ば'
    / {ADJ 'い' #ADJSyushi} &(AFTERADJSYUSHI/POSTP/EOL)
    / {ADJ 'い' #ADJRentai} &(AFTERADJRENTAI/NOUN/POSTP)
    / {ADJ #ADJGokan} &AFTERADJGOKAN
    #LookAHead
}
ADJ = @choice('dic/ADJ.txt') / 'な'

/*
形容動詞の語幹と活用語尾
形容詞とやっていることは同じ
語幹は辞書の"Noun.adjv.dic"から生成
*/
AdjectiveVerb = {
      {ADJV 'だろ' #ADJVMizen} &AFTERADJVMIZEN
    / {ADJV 'だっ' #ADJVRenyo} &AFTERADJVRENYO
    / {ADJV 'で' #ADJVRenyo} &([はも]/SUBARU/SUBNAI/TEN)
    / {ADJV 'に' #ADJVRenyo} &(VerbPhrase/AdjectivePhrase)
    / {ADJV 'だ' #ADJVSyushi} &(AFTERADJVSYUSHI/POSTP/EOL)
    / {ADJV 'な' #ADJVRentai} &(AFTERADJVRENTAI/NOUN/AdjectivePhrase)
    / {ADJV 'なら' #ADJVKatei} &('ば'?)
    / {ADJV #ADJVGokan} &AFTERADJVGOKAN
    #LookAHead
}
ADJV = @choice('dic/ADJV.txt')

/*
補助動詞の「ある」
補助形容詞の「ない」
*/
SUBARU = 'あ' [らりるれろっ]
SUBNAI = 'な' ('かろ' / 'かっ' / 'く' / 'い' / 'けれ')



/*
名詞
漢字カタカナアルファベット数字
混合した場合も可（例：アップル社）
括弧で括ったものも名詞
*/
Noun = {
      NOUN
    / 'ため'
    / 'こと'
    / ([こそど]/'あそ') 'こ'
    / [こそあど] 'れ'
    / NUM [つこ個] [め目]?
    / ('ひと'/'ふた'/'みっ'/'よっ'/'いつ'/'むっ'/'なな'/'やっ'/'ここの') 'つ' [め目]?
    #Noun
}

NOUN =
    / (KANJI/KATA/ALPHA/NUM)+
    / B_START (!B_END .)* B_END

B_START = [\[\{\(\"\'<（「『”’［｛【〔“]
B_END   = [\]\}\)\"\'>）」』”’］｝】〕“]



/* 助動詞、gen_auxverb.pyで生成 */
AuxVerb = {
    AUXVERB
    #AuxVerbs
}
AUXVERB = @choice('dic/AUXVERB.txt')

AFTERADJMIZEN = @choice('dic/Auxverb/ADJMIZEN.txt')
AFTERADJRENYO = @choice('dic/Auxverb/ADJRENYO.txt')
AFTERADJSYUSHI = @choice('dic/Auxverb/ADJSYUSHI.txt')
AFTERADJRENTAI = @choice('dic/Auxverb/ADJRENTAI.txt')
AFTERADJGOKAN = @choice('dic/Auxverb/ADJGOKAN.txt')

AFTERADJVMIZEN = @choice('dic/Auxverb/ADJVMIZEN.txt')
AFTERADJVRENYO = @choice('dic/Auxverb/ADJVRENYO.txt')
AFTERADJVSYUSHI = @choice('dic/Auxverb/ADJVSYUSHI.txt')
AFTERADJVRENTAI = @choice('dic/Auxverb/ADJVRENTAI.txt')
AFTERADJVGOKAN = @choice('dic/Auxverb/ADJVGOKAN.txt')

AFTERV1MIZEN = @choice('dic/Auxverb/V1MIZEN.txt')
AFTERV1RENYO = @choice('dic/Auxverb/V1RENYO.txt')
AFTERV1SYUSHI = @choice('dic/Auxverb/V1SYUSHI.txt')
AFTERV1RENTAI = @choice('dic/Auxverb/V1RENTAI.txt')

AFTERV5MIZEN = @choice('dic/Auxverb/V5MIZEN.txt')
AFTERV5RENYO = @choice('dic/Auxverb/V5RENYO.txt')
AFTERV5SYUSHI = @choice('dic/Auxverb/V5SYUSHI.txt')
AFTERV5RENTAI = @choice('dic/Auxverb/V5RENTAI.txt')

AFTERVKAMIZEN = @choice('dic/Auxverb/VKAMIZEN.txt')
AFTERVKARENYO = @choice('dic/Auxverb/VKARENYO.txt')
AFTERVKASYUSHI = @choice('dic/Auxverb/VKASYUSHI.txt')
AFTERVKARENTAI = @choice('dic/Auxverb/VKARENTAI.txt')

AFTERVSAMIZEN = @choice('dic/Auxverb/VSAMIZEN.txt')
AFTERVSARENYO = @choice('dic/Auxverb/VSARENYO.txt')
AFTERVSASYUSHI = @choice('dic/Auxverb/VSASYUSHI.txt')
AFTERVSARENTAI = @choice('dic/Auxverb/VSARENTAI.txt')

AFTERVERB = @choice('dic/AUXVERB.txt')
AFTERADJ = @choice('dic/AUXVERB.txt')
AFTERADNM = @choice('dic/Auxverb/ADNM.txt')
AFTERNOUN = @choice('dic/Auxverb/NOUN.txt')
AFTERPOSTPNO = @choice('dic/Auxverb/POSTPNO.txt')
AFTERPOSTP = @choice('dic/Auxverb/POSTP.txt')


/* 助詞、手入力 */
Postp = {
    POSTP
    #Postp
}
POSTP = @choice('dic/POSTP.txt')


/* 連体詞、辞書から生成 */
Adnominal = {
    ADNM
    #Adnominal
}
ADNM = @choice('dic/ADNM.txt')


/* 副詞、辞書から生成 */
Adverb = {
    ADV
    #Adverb
}
ADV = @choice('dic/ADV.txt')



