/* KAGUYA文法 */

/*
トップの規則
入力をChunkの繰り返しとみる
*/
Sentence = {
    (Chunk/TEN)* EOS
    #Sentence
}

/*
形容詞や形容動詞、動詞や連体詞などのひとかたまりごとにマッチする規則
*/
Chunk = {
      AdjectivePhrase  // 形容詞or形容動詞 助動詞?
    / VerbPhrase  // 動詞 助動詞?
    / NounPhrase  // 名詞 助動詞?
    / {[こそあど] 'の' AFTERADNM #AdnominalPhrase}  // 助動詞がつきうる連体詞
    / Adnominal  // 連体詞
    / {[の] AFTERPOSTPNO #PostpPhrase}
    / Adverb  // 副詞
    / {Postp AFTERPOSTP #PostpPhrase}
    / Postp
    #Chunk
}

example Sentence 要求は認められないので代案を用意せよ
example Sentence 僕は転びそうだったが持ちこたえた
example Sentence 紅茶を入れますわ
example Sentence 駐車場に車を止める
example Sentence 駐車場に車が止まる
example Sentence 駐車場に車が止まらない
example Sentence 雨が止む
example Sentence 酒は飲んでも飲まれるな
example Sentence スマホを充電してあげる
example Sentence 変数を定義して数値を代入する
example Sentence 会いたくて震える
example Sentence 期限を過ぎなければ提出を認めるだろう
example Sentence 階段を駆け上がったので息が切れた
example Sentence 雨が降ったらしいけれど家に居たので何も知らなかった
example Sentence 変数に数値を代入し計算を実行し結果をコンソールに出力する
example Sentence 変数に数値を代入し、計算を実行し、結果をコンソールに出力する。

EOF = !.
EOS = [。．.\n] / EOF
EOL = [。．.\n] / EOF / TEN
TEN = [、，,\n]
KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]
ALPHA = [a-zA-Zａ-ｚＡ-Ｚ]
NUM = [0-9０-９一二三四五六七八九十百千万億兆京垓秭穰溝澗正載極]
    /'恒河沙'/'阿僧祇'/'那由他'/'不可思議'/'無量大数'
SYMBOL = [-~〜ー.,]


/*
動詞節
スモールケースから順に
1. 動詞の活用した形のみの場合：話す、話し
2. 動詞の活用した形+助動詞の場合：話した、話される
3. 動詞の活用した形+動詞節の場合：話し始める
4. 動詞の活用した形+助動詞+動詞節の場合：話され始める
*/
VerbPhrase = {
      Verb AuxVerbForVerb VerbPhrase?
    / Verb VerbPhrase?
    #VerbPhrase
}

AuxVerbForVerb = {
      AFTERV1MIZEN
    / AFTERV1RENYO
    / AFTERV1SYUSHI
    / AFTERV1RENTAI
    / AFTERV5MIZEN
    / AFTERV5RENYO
    / AFTERV5SYUSHI
    / AFTERV5RENTAI
    / AFTERVKAMIZEN
    / AFTERVKARENYO
    / AFTERVKASYUSHI
    / AFTERVKARENTAI
    / AFTERVSAMIZEN
    / AFTERVSARENYO
    / AFTERVSASYUSHI
    / AFTERVSARENTAI
    #AuxVerbForVerb
}

example VerbPhrase 話したく思う
example VerbPhrase 攻撃され始めた
example VerbPhrase 話し終わる
example VerbPhrase 駆け上がる
example VerbPhrase 立ち止まる
example VerbPhrase 止まる
example VerbPhrase 止まらない

/*
形容詞または形容動詞
組み方は動詞と一緒
*/
AdjectivePhrase = {
      (Adjective/AdjectiveVerb) AuxVerbForAdj AdjectivePhrase?
    / (Adjective/AdjectiveVerb) AdjectivePhrase?
    #AdjectivePhrase
}
AuxVerbForAdj = {
      AFTERADJMIZEN
    / AFTERADJRENYO
    / AFTERADJSYUSHI
    / AFTERADJRENTAI
    / AFTERADJGOKAN
    / AFTERADJVMIZEN
    / AFTERADJVRENYO
    / AFTERADJVSYUSHI
    / AFTERADJVRENTAI
    / AFTERADJVGOKAN
    #AuxVerbForAdj
}

example AdjectivePhrase 赤いボール
example AdjectivePhrase 赤かったボール
example AdjectivePhrase うるさい
example AdjectivePhrase うるさかった
example AdjectivePhrase うるさくない
example AdjectivePhrase うるさかろう
example AdjectivePhrase うるさそうだった
example AdjectivePhrase うるさいらしい
example AdjectivePhrase うるさいでしょう
example AdjectivePhrase 赤くない
example AdjectivePhrase 赤くなく青い
example AdjectivePhrase 赤くかっこいい
example AdjectivePhrase 健康な人
example AdjectivePhrase 健康だった
example AdjectivePhrase 健康でしょう
example AdjectivePhrase ぞっこんだ
example AdjectivePhrase なめい
example AdjectivePhrase なめかった
example AdjectivePhrase なめらかだ
example AdjectivePhrase なめらかなやわらかい布

/*
名詞（体言）
体言にも助動詞が付属語としてつきうる
*/
NounPhrase = {
    Noun AuxVerbForNoun?
    #NounPhrase
}
AuxVerbForNoun = {
    AFTERNOUN
    #AuxVerbForNoun
}


/*
動詞の語幹と活用語尾
活用順にしており、活用語尾が同じものまとめている
*/
Verb =
    / Mizen
    / Renyo
    / SyushiRentai
    / Katei
    / Meirei
    / KateiMeirei
    / Common
    / HiraVerb

example Verb 歩く
example Verb 認める
example Verb 会う
example Verb 止む
example Verb 止まる
example Verb 止める

/*
動詞の未然形活用
未然形はほぼ確実に助動詞が繋がるため先読みで誤マッチを抑制している
これで「止まる」の先頭文字列「止ま」が「止む」の未然形「止ま」にマッチしない
以下、他の活用も同様
*/
Mizen = {
      {VERB5KA [かこ] #Mizen} &AFTERV5MIZEN
    / {VERB5SA [さそ] #Mizen} &AFTERV5MIZEN
    / {VERB5TA [たと] #Mizen} &AFTERV5MIZEN
    / {VERB5NA [なの] #Mizen} &AFTERV5MIZEN
    / {VERB5MA [まも] #Mizen} &AFTERV5MIZEN
    / {VERB5RA [らろ] #Mizen} &AFTERV5MIZEN
    / {VERB5WA [わお] #Mizen} &AFTERV5MIZEN
    / {VERB5GA [がご] #Mizen} &AFTERV5MIZEN
    / {VERB5BA [ばぼ] #Mizen} &AFTERV5MIZEN
    / {VERB5KASOKU [かこ] #Mizen} &AFTERV5MIZEN
    / {SAHEN_SURU [さ] #Mizen} &AFTERVSAMIZEN
    / {SAHEN_ZURU [ざ] #Mizen} &AFTERVSAMIZEN
    / {(KANJI/KATA)* [さ] #Mizen} &AFTERVSAMIZEN
    #LookAHead
}

Renyo = {
      {VERB5KA [きい] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5SA [し] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5TA [ちっ] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5NA [にん] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5MA [みん] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5RA [りっ] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5WA [いっ] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5GA [ぎい] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5BA [びん] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {VERB5KASOKU [きっ] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {KAHEN? [き] #Renyo} &(AFTERVKARENYO/[てで]/Verb/TEN)
    #LookAHead
}

SyushiRentai = {
      {VERB5KA [く] #SyushiRentai} &(AFTERV5SYUSHI/AFTERV5RENTAI/Noun/POSTP/EOL)
    / {VERB5SA [す] #SyushiRentai} &(AFTERV5SYUSHI/AFTERV5RENTAI/Noun/POSTP/EOL)
    / {VERB5TA [つ] #SyushiRentai} &(AFTERV5SYUSHI/AFTERV5RENTAI/Noun/POSTP/EOL)
    / {VERB5NA [ぬ] #SyushiRentai} &(AFTERV5SYUSHI/AFTERV5RENTAI/Noun/POSTP/EOL)
    / {VERB5MA [む] #SyushiRentai} &(AFTERV5SYUSHI/AFTERV5RENTAI/Noun/POSTP/EOL)
    / {VERB5RA [る] #SyushiRentai} &(AFTERV5SYUSHI/AFTERV5RENTAI/Noun/POSTP/EOL)
    / {VERB5WA [う] #SyushiRentai} &(AFTERV5SYUSHI/AFTERV5RENTAI/Noun/POSTP/EOL)
    / {VERB5GA [ぐ] #SyushiRentai} &(AFTERV5SYUSHI/AFTERV5RENTAI/Noun/POSTP/EOL)
    / {VERB5BA [ぶ] #SyushiRentai} &(AFTERV5SYUSHI/AFTERV5RENTAI/Noun/POSTP/EOL)
    / {VERB5KASOKU [く] #SyushiRentai} &(AFTERV5SYUSHI/AFTERV5RENTAI/Noun/POSTP/EOL)
    / {VERB1 [る] #SyushiRentai} &(AFTERV1SYUSHI/AFTERV1RENTAI/Noun/POSTP/EOL)
    / {KAHEN? ('くる' / '来る') #SyushiRentai} &(AFTERVKASYUSHI/AFTERVKARENTAI/Noun/POSTP/EOL)
    / {SAHEN_SURU 'する' #SyushiRentai} &(AFTERVSASYUSHI/AFTERVSARENTAI/Noun/POSTP/EOL)
    / {SAHEN_ZURU 'ずる' #SyushiRentai} &(AFTERVSASYUSHI/AFTERVSARENTAI/Noun/POSTP/EOL)
    / {(KANJI/KATA)* 'する' #SyushiRentai} &(AFTERVSASYUSHI/AFTERVSARENTAI/Noun/POSTP/EOL)
    #LookAHead
}

Katei = {
      {VERB1 [れ] #Katei} &'ば'
    / {KAHEN? ('くれ' / '来れ') #Katei} &'ば'
    / {SAHEN_SURU 'すれ' #Katei} &'ば'
    / {SAHEN_ZURU 'ずれ' #Katei} &'ば'
    / {(KANJI/KATA)+ 'すれ' #Katei} &'ば'
    #LookAHead
}

Meirei = {
      {VERB1 [ろよ] #Meirei} &EOL
    / {KAHEN? ('こい' / '来い') #Meirei} &EOL
    / {SAHEN_SURU ('しろ' / 'せよ') #Meirei} &EOL
    / {SAHEN_ZURU ('じろ' / 'ぜよ') #Meirei} &EOL
    / {(KANJI/KATA)+ ('しろ' / 'せよ') #Meirei} &EOL
    #LookAHead
}

KateiMeirei = {
      {VERB5KA [け] #KateiMeirei} &('ば'/EOL)
    / {VERB5SA [せ] #KateiMeirei} &('ば'/EOL)
    / {VERB5TA [て] #KateiMeirei} &('ば'/EOL)
    / {VERB5NA [ね] #KateiMeirei} &('ば'/EOL)
    / {VERB5MA [め] #KateiMeirei} &('ば'/EOL)
    / {VERB5RA [れ] #KateiMeirei} &('ば'/EOL)
    / {VERB5WA [え] #KateiMeirei} &('ば'/EOL)
    / {VERB5GA [げ] #KateiMeirei} &('ば'/EOL)
    / {VERB5BA [べ] #KateiMeirei} &('ば'/EOL)
    / {VERB5KASOKU [け] #KateiMeirei} &('ば'/EOL)
    #LookAHead
}

Common = {
      {VERB1 #MizenRenyo} &(AFTERV1MIZEN/AFTERV1RENYO/[てで]/Verb/TEN)
    / {KAHEN? [来] #MizenRenyo} &(AFTERVKAMIZEN/AFTERVKARENYO/[てで]/Verb/TEN)
    / {SAHEN_SURU [し] #MizenRenyo} &(AFTERVSAMIZEN/AFTERVSARENYO/[てで]/Verb/TEN)
    / {SAHEN_ZURU [じ] #MizenRenyo} &(AFTERVSAMIZEN/AFTERVSARENYO/[てで]/Verb/TEN)
    / {(KANJI/KATA)* [し] #MizenRenyo} &(AFTERVSAMIZEN/AFTERVSARENYO/[てで]/Verb/TEN)
    / {KAHEN? [こ] #Mizen} &AFTERVKAMIZEN  // 命令形の活用語尾「こい」との競合避け
    / {SAHEN_SURU [せ] #Mizen} &AFTERVSAMIZEN  // 命令形の活用語尾「せよ」との競合避け
    / {SAHEN_ZURU [ぜ] #Mizen} &AFTERVSAMIZEN  // 命令形の活用語尾「ぜよ」との競合避け
    / {(KANJI/KATA)* [せ] #Mizen} &AFTERVSAMIZEN  // 命令形の活用語尾「せよ」との競合避け
    #LookAHead
}

HiraVerb = {
      {HV [らろ] #Mizen} &AFTERV5MIZEN
    / {HV [りっ] #Renyo} &(AFTERV5RENYO/[てで]/Verb/TEN)
    / {HV 'る' #SyushiRentai} &(AFTERV5SYUSHI/AFTERV5RENTAI/Noun/POSTP/EOL)
    / {HV 'れ' #KateiMeirei} &('ば'/EOL)
    #LookAHead
}
HV = [あい]

/*
活用型ごとの動詞の語幹を辞書から生成
「行く」はカ行五段活用だが、イ音便ではなく促音便で活用が異なるため別枠
*/
VERB5KA = @choice('dic/Verb/VERB5KA.txt')
VERB5SA = @choice('dic/Verb/VERB5SA.txt')
VERB5TA = @choice('dic/Verb/VERB5TA.txt')
VERB5NA = @choice('dic/Verb/VERB5NA.txt')
VERB5MA = @choice('dic/Verb/VERB5MA.txt')
VERB5RA = @choice('dic/Verb/VERB5RA.txt')
VERB5WA = @choice('dic/Verb/VERB5WA.txt')
VERB5GA = @choice('dic/Verb/VERB5GA.txt')
VERB5BA = @choice('dic/Verb/VERB5BA.txt')
VERB1 = @choice('dic/Verb/VERB1.txt')
KAHEN = @choice('dic/Verb/KAHEN.txt')
SAHEN_SURU = @choice('dic/Verb/SAHEN_SURU.txt')
SAHEN_ZURU = @choice('dic/Verb/SAHEN_ZURU.txt')
VERB5KASOKU = '行'



/*
形容詞の語幹と活用語尾
実験的に助動詞や助詞、動詞を先読みで入れている
語幹は辞書の"Adjective.dic"と"Noun.nai.dic"から生成
*/
Adjective = {
      {ADJ 'かろ' #ADJMizen} &AFTERADJMIZEN
    / {ADJ 'かっ' #ADJRenyo} &AFTERADJRENYO
    / {ADJ 'く' #ADJRenyo} &([ては]/Verb/Adjective/AdjectiveVerb)
    / {ADJ 'けれ' #ADJKatei} &'ば'
    / {ADJ 'い' #ADJSyushiRentai} &(AFTERADJSYUSHI/AFTERADJRENTAI/NOUN/EOL)
    / {ADJ #ADJGokan} &AFTERADJGOKAN
    #LookAHead
}

ADJ = @choice('dic/ADJ.txt') / 'な'

/*
形容動詞の語幹と活用語尾
形容詞とやっていることは同じ
語幹は辞書の"Noun.adjv.dic"から生成
*/
AdjectiveVerb = {
      {ADJV 'だろ' #ADJVMizen} &AFTERADJVMIZEN
    / {ADJV 'だっ' #ADJVRenyo} &AFTERADJVRENYO
    / {ADJV 'で' #ADJVRenyo} &([はも]/SUBARU/SUBNAI)
    / {ADJV 'に' #ADJVRenyo} &(Verb/Adjective/AdjectiveVerb)
    / {ADJV 'なら' #ADJVKatei}
    / {ADJV 'だ' #ADJVSyushi} &(AFTERADJVSYUSHI/'')
    / {ADJV 'な' #ADJVRentai} &(AFTERADJVRENTAI/NOUN/Adjective/AdjectiveVerb)
    / {ADJV #ADJVGokan} &AFTERADJVGOKAN
    #LookAHead
}

ADJV = @choice('dic/ADJV.txt')

/*
補助動詞の「ある」
補助形容詞の「ない」
*/
SUBARU = 'あ' [らりるれろっ]
SUBNAI = 'な' ('かろ' / 'かっ' / 'く' / 'い' / 'けれ')



/*
名詞
漢字カタカナアルファベット数字
混合した場合も可（例：アップル社）
括弧で括ったものも名詞
*/
Noun = {
      NOUN
    / 'ため'
    / 'こと'
    / ([こそど]/'あそ') 'こ'
    / [こそあど] 'れ'
    / NUM [つこ個] [め目]?
    / ('ひと'/'ふた'/'みっ'/'よっ'/'いつ'/'むっ'/'なな'/'やっ'/'ここの') 'つ' [め目]?
    #Noun
}

NOUN =
    / (KANJI/KATA/ALPHA/NUM)+
    / B_START (!B_END .)* B_END

B_START = [\[\{\(\"\'<（「『”’［｛【〔“]
B_END   = [\]\}\)\"\'>）」』”’］｝】〕“]



/* 助動詞、gen_auxverb.pyで生成 */
AuxVerb = {
    AUXVERB
    #AuxVerbs
}
AUXVERB = @choice('dic/AUXVERB.txt')

AFTERADJMIZEN = @choice('dic/Auxverb/ADJMIZEN.txt')
AFTERADJRENYO = @choice('dic/Auxverb/ADJRENYO.txt')
AFTERADJSYUSHI = @choice('dic/Auxverb/ADJSYUSHI.txt')
AFTERADJRENTAI = @choice('dic/Auxverb/ADJRENTAI.txt')
AFTERADJGOKAN = @choice('dic/Auxverb/ADJGOKAN.txt')

AFTERADJVMIZEN = @choice('dic/Auxverb/ADJVMIZEN.txt')
AFTERADJVRENYO = @choice('dic/Auxverb/ADJVRENYO.txt')
AFTERADJVSYUSHI = @choice('dic/Auxverb/ADJVSYUSHI.txt')
AFTERADJVRENTAI = @choice('dic/Auxverb/ADJVRENTAI.txt')
AFTERADJVGOKAN = @choice('dic/Auxverb/ADJVGOKAN.txt')

AFTERV1MIZEN = @choice('dic/Auxverb/V1MIZEN.txt')
AFTERV1RENYO = @choice('dic/Auxverb/V1RENYO.txt')
AFTERV1SYUSHI = @choice('dic/Auxverb/V1SYUSHI.txt')
AFTERV1RENTAI = @choice('dic/Auxverb/V1RENTAI.txt')

AFTERV5MIZEN = @choice('dic/Auxverb/V5MIZEN.txt')
AFTERV5RENYO = @choice('dic/Auxverb/V5RENYO.txt')
AFTERV5SYUSHI = @choice('dic/Auxverb/V5SYUSHI.txt')
AFTERV5RENTAI = @choice('dic/Auxverb/V5RENTAI.txt')

AFTERVKAMIZEN = @choice('dic/Auxverb/VKAMIZEN.txt')
AFTERVKARENYO = @choice('dic/Auxverb/VKARENYO.txt')
AFTERVKASYUSHI = @choice('dic/Auxverb/VKASYUSHI.txt')
AFTERVKARENTAI = @choice('dic/Auxverb/VKARENTAI.txt')

AFTERVSAMIZEN = @choice('dic/Auxverb/VSAMIZEN.txt')
AFTERVSARENYO = @choice('dic/Auxverb/VSARENYO.txt')
AFTERVSASYUSHI = @choice('dic/Auxverb/VSASYUSHI.txt')
AFTERVSARENTAI = @choice('dic/Auxverb/VSARENTAI.txt')

AFTERADNM = @choice('dic/Auxverb/ADNM.txt')
AFTERNOUN = @choice('dic/Auxverb/NOUN.txt')
AFTERPOSTPNO = @choice('dic/Auxverb/POSTPNO.txt')
AFTERPOSTP = @choice('dic/Auxverb/POSTP.txt')


/* 助詞、手入力 */
Postp = {
    POSTP
    #Postp
}
POSTP = @choice('dic/POSTP.txt')


/* 連体詞、辞書から生成 */
Adnominal = {
    ADNM
    #Adnominal
}
ADNM = @choice('dic/ADNM.txt')


/* 副詞、辞書から生成 */
Adverb = {
    ADV
    #Adverb
}
ADV = @choice('dic/ADV.txt')



