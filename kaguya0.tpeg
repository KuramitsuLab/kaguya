/* KAGUYA文法 */

/*
トップの規則
入力をChunkの繰り返しとみる
*/
Sentence = {
    // (TEN/[:：_＿]/Chunk)* [。．.\n]? {.* #Remain}  // デバッグモード
    (TEN/Chunk)* EOS
    #Sentence
}

/*
形容詞や形容動詞や動詞や連体詞などのひとかたまりごとにマッチする規則
チョイスの順番はテストして最適な順番を見つけるしかない
*/
Chunk =
    / AdjectivePhrase  // 形容詞句or形容動詞句
    / VerbPhrase  // 動詞句
    / NounPhrase  // 名詞句
    / {[こそあど] 'の' AFTERADNM #AdnominalPhrase} PostpPhrase?  // 連体詞句
    / {Conjunction &(Chunk/PostpPhrase/TEN) #LookAHead_1} PostpPhrase?  // 接続詞
    / Adnominal PostpPhrase?  // 連体詞
    // / PostpPhrase  // 助詞句
    / Adverb PostpPhrase?  // 副詞


example Sentence 要求は認められないので代案を用意せよ
example Sentence 僕は転びそうだったが持ちこたえた
example Sentence 紅茶を入れますわ
example Sentence 駐車場に車を止める
example Sentence 駐車場に車が止まる
example Sentence 駐車場に車が止まらない
example Sentence 雨が止む
example Sentence 酒は飲んでも飲まれるな
example Sentence スマホを充電してあげる
example Sentence 変数を定義して数値を代入する
example Sentence 会いたくて震える
example Sentence 期限を過ぎなければ提出を認めるだろう
example Sentence 階段を駆け上がったので息が切れた
example Sentence 雨が降ったらしいけれど家に居たので何も知らなかった
example Sentence 変数に数値を代入し計算を実行し結果をコンソールに出力する
example Sentence 変数に数値を代入し、計算を実行し、結果をコンソールに出力する。

EOF = !.
EOS = [。．.\n]? EOF
TEN = [、，,:：\n]
Eol = { (TEN/EOS) #Eol }
KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]+
ALPHA = [a-zA-Zａ-ｚＡ-Ｚ]+
NUM = [\-ー\.,．，0-9０-９一二三四五六七八九十百千万億兆]
SYMBOL =
    / [^＾~〜\-ー_＿!！?？#＃$＄&＆=＝@＠*＊+＋/・]
    / [\[\]\(\)\<\>\{\}\'\"\`「」（）＜＞『』［］【】”“｀”“’‘｛｝〔〕]


/*
動詞句
スモールケースから順に
1. 動詞の活用した形のみの場合：話す、話し
2. 動詞の活用した形+助動詞の場合：話した、話される
3. 動詞の活用した形+動詞句の場合：話し始める
4. 動詞の活用した形+助動詞+動詞句の場合：話され始める
*/
VerbPhrase = {
    (
          Verb AuxVerbForVerb VerbPhrase
        / {Verb AuxVerbForVerb &(Chunk/PostpPhrase/Eol) #LookAHead_2}
        / Verb VerbPhrase
        / {Verb &(Chunk/PostpPhrase/Eol) #LookAHead_1}
    )
    PostpPhrase?
    #VerbPhrase
}

AuxVerbForVerb = {
    AFTERVERB
    #AuxVerbForVerb
}

example VerbPhrase 話したく思う
example VerbPhrase 攻撃され始めた
example VerbPhrase 話し終わる
example VerbPhrase 駆け上がる
example VerbPhrase 立ち止まる
example VerbPhrase 止まる
example VerbPhrase 止まらない

/*
形容詞または形容動詞
組み方は動詞と一緒
*/
AdjectivePhrase = {
    (
          (Adjective/AdjectiveVerb) AuxVerbForAdj AdjectivePhrase
        / {(Adjective/AdjectiveVerb) AuxVerbForAdj &(Chunk/PostpPhrase/Eol) #LookAHead_2}
        / (Adjective/AdjectiveVerb) AdjectivePhrase
        / {(Adjective/AdjectiveVerb) &(Chunk/PostpPhrase/Eol) #LookAHead_1}
    )
    PostpPhrase?
    #AdjectivePhrase
}
AuxVerbForAdj = {
    AFTERADJ
    #AuxVerbForAdj
}

example AdjectivePhrase 赤いボール
example AdjectivePhrase 赤かったボール
example AdjectivePhrase うるさい人
example AdjectivePhrase うるさかった人
example AdjectivePhrase うるさくない人
example AdjectivePhrase うるさかろう人
example AdjectivePhrase うるさそうだった人
example AdjectivePhrase うるさいらしい人
example AdjectivePhrase うるさいでしょう
example AdjectivePhrase 赤くないボール
example AdjectivePhrase 赤くなく青いボール
example AdjectivePhrase 赤くかっこいい人
example AdjectivePhrase 健康な人
example AdjectivePhrase 健康だった人
example AdjectivePhrase 健康でしょう
example AdjectivePhrase ぞっこんだ
example AdjectivePhrase なめいもの
example AdjectivePhrase なめかったもの
example AdjectivePhrase なめらかだ
example AdjectivePhrase なめらかなやわらかい布

/*
名詞（体言）
体言にも助動詞が付属語としてつきうる
*/
NounPhrase = {
    (
          Noun AuxVerbForNoun NounPhrase
        / {Noun AuxVerbForNoun &(Chunk/PostpPhrase/Eol) #LookAHead_2}
        / Noun NounPhrase
        / {Noun &(Chunk/PostpPhrase/Eol) #LookAHead_1}
    )
    PostpPhrase?
    #NounPhrase
}
AuxVerbForNoun = {
    AFTERNOUN
    #AuxVerbForNoun
}

/*
助詞句
助詞の「の」だけに続く助動詞があるため別扱いしている
助詞に続く助動詞「な」はその後が限定的なので別扱いしている
*/
PostpPhrase = {
      PostpNo AfterPostpNo PostpPhrase
    / Postp AfterPostp PostpPhrase
    / Postp AuxVerbNa {'の' #PostpPhrase} PostpPhrase
    / {PostpNo AfterPostpNo &(Chunk/PostpPhrase/Eol) #LookAHead_2}
    / {Postp AfterPostp &(Chunk/PostpPhrase/Eol) #LookAHead_2}
    / Postp PostpPhrase
    / {Postp &(Chunk/PostpPhrase/Eol) #LookAHead_1}
    #PostpPhrase
}

PostpNo = { 'の' #PostpNo }
AfterPostpNo = { AFTERPOSTPNO #AuxVerbForPostpNo }
AfterPostp = { AFTERPOSTP #AuxVerbForPostp }
AuxVerbNa = { 'な' #AuxVerbForPostp }


/*
動詞の語幹と活用語尾
活用順にしており、活用語尾が同じものまとめている
*/
Verb =
    / Mizen
    / Renyo
    / Syushi
    / Rentai
    / Katei
    / Meirei
    / Kudasai
    / HiraVerb

example Verb 歩く
example Verb 認める
example Verb 会う
example Verb 止む
example Verb 止まる
example Verb 止める

/*
動詞の未然形活用
未然形はほぼ確実に助動詞が繋がるため先読みで誤マッチを抑制している
これで「止まる」の先頭文字列「止ま」が「止む」の未然形「止ま」にマッチしない
以下、他の活用も同様
*/
Mizen = {
      {VERB5KA [かこ] #Mizen} &{AFTERV5MIZEN}
    / {VERB5SA [さそ] #Mizen} &{AFTERV5MIZEN}
    / {VERB5TA [たと] #Mizen} &{AFTERV5MIZEN}
    / {VERB5NA [なの] #Mizen} &{AFTERV5MIZEN}
    / {VERB5MA [まも] #Mizen} &{AFTERV5MIZEN}
    / {VERB5RA [らろ] #Mizen} &{AFTERV5MIZEN}
    / {VERB5WA [わお] #Mizen} &{AFTERV5MIZEN}
    / {VERB5GA [がご] #Mizen} &{AFTERV5MIZEN}
    / {VERB5BA [ばぼ] #Mizen} &{AFTERV5MIZEN}
    / {VERB5KASOKU [かこ] #Mizen} &{AFTERV5MIZEN}
    / {VERB1 #Mizen} &{AFTERV1MIZEN}
    / {SAHEN_SURU [さ] #Mizen} &{AFTERVSAMIZEN}
    / {SAHEN_ZURU [ざ] #Mizen} &{AFTERVSAMIZEN}
    / {(KANJI/KATA)* [さ] #Mizen} &{AFTERVSAMIZEN}
    / {KAHEN? [来] #Mizen} &{AFTERVKAMIZEN}
    / {SAHEN_SURU [し] #Mizen} &{AFTERVSAMIZEN}
    / {SAHEN_ZURU [じ] #Mizen} &{AFTERVSAMIZEN}
    / {(KANJI/KATA)* [し] #Mizen} &{AFTERVSAMIZEN}
    / {KAHEN? [こ] #Mizen} &{AFTERVKAMIZEN}  // 命令形の活用語尾「こい」と競合するかも
    / {SAHEN_SURU [せ] #Mizen} &{AFTERVSAMIZEN}  // 命令形の活用語尾「せよ」と競合するかも
    / {SAHEN_ZURU [ぜ] #Mizen} &{AFTERVSAMIZEN}  // 命令形の活用語尾「ぜよ」と競合するかも
    / {(KANJI/KATA)* [せ] #Mizen} &{AFTERVSAMIZEN}  // 命令形の活用語尾「せよ」と競合するかも
    #LookAHead_1
}

Renyo = {
      {VERB5KA [きい] #Renyo} &({AFTERV5RENYO}/AfterRenyo)
    / {VERB5SA [し] #Renyo} &({AFTERV5RENYO}/AfterRenyo)
    / {VERB5TA [ちっ] #Renyo} &({AFTERV5RENYO}/AfterRenyo)
    / {VERB5NA [にん] #Renyo} &({AFTERV5RENYO}/AfterRenyo)
    / {VERB5MA [みん] #Renyo} &({AFTERV5RENYO}/AfterRenyo)
    / {VERB5RA [りっ] #Renyo} &({AFTERV5RENYO}/AfterRenyo)
    / {VERB5WA [いっ] #Renyo} &({AFTERV5RENYO}/AfterRenyo)
    / {VERB5GA [ぎい] #Renyo} &({AFTERV5RENYO}/AfterRenyo)
    / {VERB5BA [びん] #Renyo} &({AFTERV5RENYO}/AfterRenyo)
    / {VERB5KASOKU [きっ] #Renyo} &({AFTERV5RENYO}/AfterRenyo)
    / {VERB1 #Renyo} &({AFTERV1RENYO}/AfterRenyo)
    / {KAHEN? [き] #Renyo} &({AFTERVKARENYO}/AfterRenyo)
    / {KAHEN? [来] #Renyo} &({AFTERVKARENYO}/AfterRenyo)
    / {SAHEN_SURU [し] #Renyo} &({AFTERVSARENYO}/AfterRenyo)
    / {SAHEN_ZURU [じ] #Renyo} &({AFTERVSARENYO}/AfterRenyo)
    / {(KANJI/KATA)* [し] #Renyo} &({AFTERVSARENYO}/AfterRenyo)
    #LookAHead_1
}
AfterRenyo = {
      [てで]
    / VerbPhrase
    / TEN
    / PostpPhrase
    #AfterRenyo
}

Syushi = {
      {VERB5KA [く] #Syushi} &({AFTERV5SYUSHI}/AfterSyushi)
    / {VERB5SA [す] #Syushi} &({AFTERV5SYUSHI}/AfterSyushi)
    / {VERB5TA [つ] #Syushi} &({AFTERV5SYUSHI}/AfterSyushi)
    / {VERB5NA [ぬ] #Syushi} &({AFTERV5SYUSHI}/AfterSyushi)
    / {VERB5MA [む] #Syushi} &({AFTERV5SYUSHI}/AfterSyushi)
    / {VERB5RA [る] #Syushi} &({AFTERV5SYUSHI}/AfterSyushi)
    / {VERB5WA [う] #Syushi} &({AFTERV5SYUSHI}/AfterSyushi)
    / {VERB5GA [ぐ] #Syushi} &({AFTERV5SYUSHI}/AfterSyushi)
    / {VERB5BA [ぶ] #Syushi} &({AFTERV5SYUSHI}/AfterSyushi)
    / {VERB5KASOKU [く] #Syushi} &({AFTERV5SYUSHI}/AfterSyushi)
    / {VERB1 [る] #Syushi} &({AFTERV1SYUSHI}/AfterSyushi)
    / {KAHEN? ('くる' / '来る') #Syushi} &({AFTERVKASYUSHI}/AfterSyushi)
    / {SAHEN_SURU 'する' #Syushi} &({AFTERVSASYUSHI}/AfterSyushi)
    / {SAHEN_ZURU 'ずる' #Syushi} &({AFTERVSASYUSHI}/AfterSyushi)
    / {(KANJI/KATA)* 'する' #Syushi} &({AFTERVSASYUSHI}/AfterSyushi)
    #LookAHead_1
}
AfterSyushi = {
      PostpPhrase
    / Eol
    #AfterSyushi
}

Rentai = {
      {VERB5KA [く] #Rentai} &({AFTERV5RENTAI}/AfterRentai)
    / {VERB5SA [す] #Rentai} &({AFTERV5RENTAI}/AfterRentai)
    / {VERB5TA [つ] #Rentai} &({AFTERV5RENTAI}/AfterRentai)
    / {VERB5NA [ぬ] #Rentai} &({AFTERV5RENTAI}/AfterRentai)
    / {VERB5MA [む] #Rentai} &({AFTERV5RENTAI}/AfterRentai)
    / {VERB5RA [る] #Rentai} &({AFTERV5RENTAI}/AfterRentai)
    / {VERB5WA [う] #Rentai} &({AFTERV5RENTAI}/AfterRentai)
    / {VERB5GA [ぐ] #Rentai} &({AFTERV5RENTAI}/AfterRentai)
    / {VERB5BA [ぶ] #Rentai} &({AFTERV5RENTAI}/AfterRentai)
    / {VERB5KASOKU [く] #Rentai} &({AFTERV5RENTAI}/AfterRentai)
    / {VERB1 [る] #Rentai} &({AFTERV1RENTAI}/AfterRentai)
    / {KAHEN? ('くる' / '来る') #Rentai} &({AFTERVKARENTAI}/AfterRentai)
    / {SAHEN_SURU 'する' #Rentai} &({AFTERVSARENTAI}/AfterRentai)
    / {SAHEN_ZURU 'ずる' #Rentai} &({AFTERVSARENTAI}/AfterRentai)
    / {(KANJI/KATA)* 'する' #Rentai} &({AFTERVSARENTAI}/AfterRentai)
    #LookAHead_1
}
AfterRentai = {
      NounPhrase
    / PostpPhrase
    #AfterRentai
}


Katei =
    / {VERB5KA [け] #Katei} &'ば'
    / {VERB5SA [せ] #Katei} &'ば'
    / {VERB5TA [て] #Katei} &'ば'
    / {VERB5NA [ね] #Katei} &'ば'
    / {VERB5MA [め] #Katei} &'ば'
    / {VERB5RA [れ] #Katei} &'ば'
    / {VERB5WA [え] #Katei} &'ば'
    / {VERB5GA [げ] #Katei} &'ば'
    / {VERB5BA [べ] #Katei} &'ば'
    / {VERB5KASOKU [け] #Katei} &'ば'
    / {VERB1 [れ] #Katei} &'ば'
    / {KAHEN? ('くれ' / '来れ') #Katei} &'ば'
    / {SAHEN_SURU 'すれ' #Katei} &'ば'
    / {SAHEN_ZURU 'ずれ' #Katei} &'ば'
    / {(KANJI/KATA)+ 'すれ' #Katei} &'ば'


Meirei = {
      {VERB5KA [け] #Meirei} &(Eol)
    / {VERB5SA [せ] #Meirei} &(Eol)
    / {VERB5TA [て] #Meirei} &(Eol)
    / {VERB5NA [ね] #Meirei} &(Eol)
    / {VERB5MA [め] #Meirei} &(Eol)
    / {VERB5RA [れ] #Meirei} &(Eol)
    / {VERB5WA [え] #Meirei} &(Eol)
    / {VERB5GA [げ] #Meirei} &(Eol)
    / {VERB5BA [べ] #Meirei} &(Eol)
    / {VERB5KASOKU [け] #Meirei} &(Eol)
    / {VERB1 [ろよ] #Meirei} &(Eol)
    / {KAHEN? ('こい' / '来い') #Meirei} &(Eol)
    / {SAHEN_SURU ('しろ' / 'せよ') #Meirei} &(Eol)
    / {SAHEN_ZURU ('じろ' / 'ぜよ') #Meirei} &(Eol)
    / {(KANJI/KATA)+ ('しろ' / 'せよ') #Meirei} &(Eol)
    #LookAHead_1
}

Kudasai = {
      {KUDASAI [らろ] #Mizen} &({AFTERV5MIZEN})
    / {KUDASAI [りっ] #Renyo} &({AFTERV5RENYO}/{[てで]}/VerbPhrase/{TEN})
    / {KUDASAI 'る' #Syushi} &({AFTERV5SYUSHI}/Eol/PostpPhrase)
    / {KUDASAI 'る' #Rentai} &({AFTERV5RENTAI}/NounPhrase/PostpPhrase)
    / {KUDASAI 'れ' #Katei} &({'ば'})
    / {KUDASAI 'い' #Meirei} &(Eol)
    #LookAHead_1
}
KUDASAI = 'くださ' / '下さ'

HiraVerb = {
      {HV [らろ] #Mizen} &({AFTERV5MIZEN})
    / {HV [りっ] #Renyo} &({AFTERV5RENYO}/{[てで]}/VerbPhrase/{TEN})
    / {HV 'る' #Syushi} &({AFTERV5SYUSHI}/PostpPhrase/Eol)
    / {HV 'る' #Rentai} &({AFTERV5RENTAI}/NounPhrase/PostpPhrase)
    / {HV 'れ' #KateiMeirei} &({'ば'}/Eol)
    #LookAHead_1
}
HV = [あい]

/*
活用型ごとの動詞の語幹を辞書から生成
「行く」はカ行五段活用だが、イ音便ではなく促音便で活用が異なるため別枠
*/
VERB5KA = @choice('dic/Verb/VERB5KA.txt')
VERB5SA = @choice('dic/Verb/VERB5SA.txt')
VERB5TA = @choice('dic/Verb/VERB5TA.txt')
VERB5NA = @choice('dic/Verb/VERB5NA.txt')
VERB5MA = @choice('dic/Verb/VERB5MA.txt')
VERB5RA = @choice('dic/Verb/VERB5RA.txt')
VERB5WA = @choice('dic/Verb/VERB5WA.txt')
VERB5GA = @choice('dic/Verb/VERB5GA.txt')
VERB5BA = @choice('dic/Verb/VERB5BA.txt')
VERB1 = @choice('dic/Verb/VERB1.txt')
KAHEN = @choice('dic/Verb/KAHEN.txt')
SAHEN_SURU = @choice('dic/Verb/SAHEN_SURU.txt')
SAHEN_ZURU = @choice('dic/Verb/SAHEN_ZURU.txt')
VERB5KASOKU = '行'



/*
形容詞の語幹と活用語尾
実験的に助動詞や助詞、動詞を先読みで入れている
語幹は辞書の"Adjective.dic"と"Noun.nai.dic"から生成
*/
Adjective = {
      {ADJ 'かろ' #ADJMizen} &({AFTERADJMIZEN})
    / {ADJ 'かっ' #ADJRenyo} &({AFTERADJRENYO})
    / {ADJ 'く' #ADJRenyo} &({[ては]}/VerbPhrase/AdjectivePhrase)
    / {ADJ 'けれ' #ADJKatei} &({'ば'})
    / {ADJ 'い' #ADJSyushi} &({AFTERADJSYUSHI}/PostpPhrase/Eol)
    / {ADJ 'い' #ADJRentai} &({AFTERADJRENTAI}/NounPhrase/PostpPhrase)
    / {ADJ #ADJGokan} &({AFTERADJGOKAN})
    #LookAHead_1
}
ADJ = @choice('dic/ADJ.txt') / 'な'

/*
形容動詞の語幹と活用語尾
形容詞とやっていることは同じ
語幹は辞書の"Noun.adjv.dic"から生成
*/
AdjectiveVerb = {
      {ADJV 'だろ' #ADJVMizen} &({AFTERADJVMIZEN})
    / {ADJV 'だっ' #ADJVRenyo} &({AFTERADJVRENYO})
    / {ADJV 'で' #ADJVRenyo} &({[はも]/SUBARU/SUBNAI/TEN})
    / {ADJV 'に' #ADJVRenyo} &(VerbPhrase/AdjectivePhrase)
    / {ADJV 'だ' #ADJVSyushi} &({AFTERADJVSYUSHI}/PostpPhrase/Eol)
    / {ADJV 'な' #ADJVRentai} &({AFTERADJVRENTAI}/NounPhrase/AdjectivePhrase)
    / {ADJV 'なら' #ADJVKatei} &('ば'?)
    / {ADJV #ADJVGokan} &({AFTERADJVGOKAN})
    #LookAHead_1
}
ADJV = @choice('dic/ADJV.txt')

/*
補助動詞の「ある」
補助形容詞の「ない」
*/
SUBARU = 'あ' [らりるれろっ]
SUBNAI = 'な' ('かろ' / 'かっ' / 'く' / 'い' / 'けれ')



/*
名詞
漢字カタカナアルファベット数字
混合した場合も可（例：アップル社）
括弧で括ったものも名詞
*/
Noun = {
      BLOCK
    / {NOUN #NounInDict}
    / KATA
    / ALPHA
    / NUM
    / SYMBOL
    / KANJI
    #Noun
}

Noun_Old = {
      'ところ'
    / 'ため'
    / 'こと'
    / 'もの'
    / 'うち'
    / 'あと'
    / ([こそど]/'あそ') 'こ'
    / [こそあど] 'れ'
    / NUM [つこ個] [め目]?
    / ('ひと'/'ふた'/'みっ'/'よっ'/'いつ'/'むっ'/'なな'/'やっ'/'ここの') 'つ' [め目]?
    / BLOCK
    / (KANJI/KATA/ALPHA/NUM/SYMBOL)+
    #Noun
}


NOUN = @choice('dic/NOUN.txt')

BLOCK =
    / '\[' (!'\]' .)* '\]'
    / '\{' (!'\}' .)* '\}'
    / '\(' (!'\)' .)* '\)'
    / '\"' (!'\"' .)* '\"'
    / '\'' (!'\'' .)* '\''
    / '\<' (!'\>' .)* '\>'
    / '\（' (!'\）' .)* '\）'
    / '\「' (!'\」' .)* '\」'
    / '\『' (!'\』' .)* '\』'
    / '\“' (!'\”' .)* '\”'
    / '\‘' (!'\’' .)* '\’'
    / '\【' (!'\】' .)* '\】'
    / '\［' (!'\］' .)* '\］'
    / '\｛' (!'\｝' .)* '\｝'
    / '\〔' (!'\〕' .)* '\〕'


/* 助動詞、gen_auxverb.pyで生成 */
AuxVerb = {
    AUXVERB
    #AuxVerbs
}
AUXVERB = @choice('dic/AUXVERB.txt')

AFTERADJMIZEN = @choice('dic/Auxverb/ADJMIZEN.txt')
AFTERADJRENYO = @choice('dic/Auxverb/ADJRENYO.txt')
AFTERADJSYUSHI = @choice('dic/Auxverb/ADJSYUSHI.txt')
AFTERADJRENTAI = @choice('dic/Auxverb/ADJRENTAI.txt')
AFTERADJGOKAN = @choice('dic/Auxverb/ADJGOKAN.txt')

AFTERADJVMIZEN = @choice('dic/Auxverb/ADJVMIZEN.txt')
AFTERADJVRENYO = @choice('dic/Auxverb/ADJVRENYO.txt')
AFTERADJVSYUSHI = @choice('dic/Auxverb/ADJVSYUSHI.txt')
AFTERADJVRENTAI = @choice('dic/Auxverb/ADJVRENTAI.txt')
AFTERADJVGOKAN = @choice('dic/Auxverb/ADJVGOKAN.txt')

AFTERV1MIZEN = @choice('dic/Auxverb/V1MIZEN.txt')
AFTERV1RENYO = @choice('dic/Auxverb/V1RENYO.txt')
AFTERV1SYUSHI = @choice('dic/Auxverb/V1SYUSHI.txt')
AFTERV1RENTAI = @choice('dic/Auxverb/V1RENTAI.txt')

AFTERV5MIZEN = @choice('dic/Auxverb/V5MIZEN.txt')
AFTERV5RENYO = @choice('dic/Auxverb/V5RENYO.txt')
AFTERV5SYUSHI = @choice('dic/Auxverb/V5SYUSHI.txt')
AFTERV5RENTAI = @choice('dic/Auxverb/V5RENTAI.txt')

AFTERVKAMIZEN = @choice('dic/Auxverb/VKAMIZEN.txt')
AFTERVKARENYO = @choice('dic/Auxverb/VKARENYO.txt')
AFTERVKASYUSHI = @choice('dic/Auxverb/VKASYUSHI.txt')
AFTERVKARENTAI = @choice('dic/Auxverb/VKARENTAI.txt')

AFTERVSAMIZEN = @choice('dic/Auxverb/VSAMIZEN.txt')
AFTERVSARENYO = @choice('dic/Auxverb/VSARENYO.txt')
AFTERVSASYUSHI = @choice('dic/Auxverb/VSASYUSHI.txt')
AFTERVSARENTAI = @choice('dic/Auxverb/VSARENTAI.txt')

AFTERVERB = @choice('dic/AUXVERB.txt')
AFTERADJ = @choice('dic/AUXVERB.txt')
AFTERADNM = @choice('dic/Auxverb/ADNM.txt')
AFTERNOUN = @choice('dic/Auxverb/NOUN.txt')
AFTERPOSTPNO = @choice('dic/Auxverb/POSTPNO.txt')
AFTERPOSTP = @choice('dic/Auxverb/POSTP.txt')


/* 助詞、手入力 */
Postp = {
    POSTP
    #Postp
}
POSTP = @choice('dic/POSTP.txt')


/* 連体詞、辞書から生成 */
Adnominal = {
    ADNM
    #Adnominal
}
ADNM = @choice('dic/ADNM.txt')


/* 副詞、辞書から生成 */
Adverb = {
    ADV
    #Adverb
}
ADV = @choice('dic/ADV.txt')


/* 接続詞、辞書から生成 */
Conjunction = {
    CONJ
    #Conjunction
}
CONJ = @choice('dic/CONJ.txt')
