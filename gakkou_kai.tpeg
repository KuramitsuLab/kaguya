/*
先読みのない学校文法
ただし、助動詞や連体詞の「こそあど」や助詞の「の」などで接続し得ないものはチョイスから外している
*/

/*
トップの規則
句点で終わる一文に対応している
文をChunkの繰り返しとみる
*/
Sentence = {
    // (TEN/Chunk)* [。．.\n]? {.* #Remain}  // デバッグモード
    (TEN/Chunk)* EOS
    #Sentence
}

NOUN =
    // / @choice('dic/NOUN.txt')
    // / @choice('dic/Noun/rust.txt')
    / @choice('dic/Noun/python.txt')

/*
形容詞句や動詞句などのひとかたまりごとにマッチする規則
先読みのない"gakkou.tpeg"では、ここの順番が大きく影響する
*/
Chunk =
    / ConjunctionPhrase  // 接続詞
    / AdverbPhrase  // 副詞
    / AdnominalPhrase  // 連体詞
    / AdjectivePhrase  // 形容詞句
    / AdjectiveVerbPhrase  // 形容動詞句
    / VerbPhrase  // 動詞句
    / NounPhrase  // 名詞句


EOF = !.
EOS = [。．.\n]? EOF
TEN = [、，,:：\n]
Eol = { (TEN/EOS) #Eol }
KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]+
ALPHA = [a-zA-Zａ-ｚＡ-Ｚ]+
NUM = [\-ー\.,．，0-9０-９一二三四五六七八九十百千万億兆]
SYMBOL =
    / [^＾~〜\-ー_＿!！?？#＃$＄&＆=＝@＠*＊+＋/・]
    / [\[\]\(\)\<\>\{\}\'\"\`「」（）＜＞『』［］【】”“｀”“’‘｛｝〔〕]


/*
動詞句
スモールケースから順に
1. 動詞の活用した形のみの場合：話す、話し
2. 動詞の活用した形+助動詞の場合：話した、話される
3. 動詞の活用した形+動詞句の場合：話し始める
4. 動詞の活用した形+助動詞+動詞句の場合：話され始める
*/
VerbPhrase = {
    Verb AuxVerbForVerb? PostpPhrase?
    #VerbPhrase
}
AuxVerbForVerb = {
      Aux_Seru
    / Aux_Saseru
    / Aux_Reru
    / Aux_Rareru
    / Aux_Nai
    / Aux_Nu
    / Aux_U
    / Aux_You
    / Aux_Mai
    / Aux_Tai
    / Aux_Tagaru
    / Aux_Ta
    / Aux_Da1
    / Aux_Souda1
    / Aux_Souda2
    / Aux_Youda
    / Aux_Rashii
    / Aux_Masu
    / Aux_Daro
    / Aux_Desyo
    #AuxVerbForVerb
}


/*
形容詞
組み方は動詞と一緒
*/
AdjectivePhrase = {
    Adjective AuxVerbForAdj? PostpPhrase?
    #AdjectivePhrase
}
AuxVerbForAdj = {
      Aux_U
    / Aux_Ta
    / Aux_Souda1
    / Aux_Souda2
    / Aux_Youda
    / Aux_Rashii
    / Aux_Daro
    / Aux_Desyo
    #AuxVerbForAdj
}


/*
形容動詞
組み方は動詞と一緒
*/
AdjectiveVerbPhrase = {
    AdjectiveVerb AuxVerbForAdjv? PostpPhrase?
    #AdjectiveVerbPhrase
}
AuxVerbForAdjv = {
      Aux_U
    / Aux_Ta
    / Aux_Souda1
    / Aux_Souda2
    / Aux_Youda
    / Aux_Rashii
    / Aux_Desu
    #AuxVerbForAdj
}


/*
名詞句
名詞にも助動詞が付属語としてつきうる
*/
NounPhrase = {
    Noun AuxVerbForNoun? PostpPhrase?
    #NounPhrase
}
AuxVerbForNoun = {
      Aux_Rashii
    / Aux_Da2
    / Aux_Desu
    #AuxVerbForNoun
}


/*
接続詞句
接続詞に助詞が続くことはないものとしている
*/
ConjunctionPhrase = {
    Conjunction
    #ConjunctionPhrase
}


/*
連体詞句
「この・その・あの・どの」には助動詞「ようだ」がつきうる
連体詞には助詞は続かない
*/
AdnominalPhrase = {
      KoSoADo AuxVerbForAdnm PostpPhrase?
    / Adnominal
    #AdnominalPhrase
}
AuxVerbForAdnm = {
    Aux_Youda
    #AuxVerbForAdnm
}
KoSoADo = { [こそあど] 'の' #Adnominal }


/*
副詞句
*/
AdverbPhrase = {
      Adverb AuxVerbForAdv? PostpPhrase?
    / Adverb PostpNo
    #AdverbPhrase
}
AuxVerbForAdv = {
      Aux_Rashii
    / Aux_Da2
    / Aux_Desu
    #AuxVerbForAdv
}


/*
助詞句
助詞の「の」だけに続く助動詞があるため別扱いしている
助詞に続く助動詞「な」はその後が限定的なので別扱いしている
*/
PostpPhrase = {
      Postp AuxVerbNa {'の'} AfterPostpNo? PostpPhrase?
    / 'の' AfterPostpNo? PostpPhrase?
    / Postp AfterPostp? PostpPhrase?
    #PostpPhrase
}
PostpNo = { 'の' #PostpNo }
AfterPostpNo = { Aux_Youda #AuxVerbForPostpNo }
AfterPostp = { (Aux_Rashii/Aux_Da2/Aux_Desu) #AuxVerbForPostp }
AuxVerbNa = { 'な' #AuxVerbForPostp }


/*
動詞の語幹と活用語尾
活用順にしており、活用語尾が同じものまとめている
*/
Verb =
    / Mizen
    / Renyo
    / Syushi
    / Rentai
    / Katei
    / Meirei


Mizen = {
      VERB5KA [かこ]
    / VERB5SA [さそ]
    / VERB5TA [たと]
    / VERB5NA [なの]
    / VERB5MA [まも]
    / VERB5RA [らろ]
    / VERB5WA [わお]
    / VERB5GA [がご]
    / VERB5BA [ばぼ]
    / VERB5KASOKU [かこ]
    / VERB1
    / SAHEN_SURU [さ]
    / SAHEN_ZURU [ざ]
    / (KANJI/KATA)* [さ]
    / KAHEN? [来]
    / SAHEN_SURU [し]
    / SAHEN_ZURU [じ]
    / (KANJI/KATA)* [し]
    / KAHEN? [こ]  // 命令形の活用語尾「こい」と競合する
    / SAHEN_SURU [せ]  // 命令形の活用語尾「せよ」と競合する
    / SAHEN_ZURU [ぜ]  // 命令形の活用語尾「ぜよ」と競合する
    / (KANJI/KATA)* [せ]  // 命令形の活用語尾「せよ」と競合する
    #Mizen
}

Renyo = {
      VERB5KA [きい]
    / VERB5SA [し]
    / VERB5TA [ちっ]
    / VERB5NA [にん]
    / VERB5MA [みん]
    / VERB5RA [りっ]
    / VERB5WA [いっ]
    / VERB5GA [ぎい]
    / VERB5BA [びん]
    / VERB5KASOKU [きっ]
    / VERB1
    / KAHEN? [き]
    / KAHEN? [来]
    / SAHEN_SURU [し]
    / SAHEN_ZURU [じ]
    / (KANJI/KATA)* [し]
    #Renyo
}

Syushi = {
      VERB5KA [く]
    / VERB5SA [す]
    / VERB5TA [つ]
    / VERB5NA [ぬ]
    / VERB5MA [む]
    / VERB5RA [る]
    / VERB5WA [う]
    / VERB5GA [ぐ]
    / VERB5BA [ぶ]
    / VERB5KASOKU [く]
    / VERB1 [る]
    / KAHEN? ('くる' / '来る')
    / SAHEN_SURU 'する'
    / SAHEN_ZURU 'ずる'
    / (KANJI/KATA)* 'する'
    #Syushi
}

Rentai = {
      VERB5KA [く]
    / VERB5SA [す]
    / VERB5TA [つ]
    / VERB5NA [ぬ]
    / VERB5MA [む]
    / VERB5RA [る]
    / VERB5WA [う]
    / VERB5GA [ぐ]
    / VERB5BA [ぶ]
    / VERB5KASOKU [く]
    / VERB1 [る]
    / KAHEN? ('くる' / '来る')
    / SAHEN_SURU 'する'
    / SAHEN_ZURU 'ずる'
    / (KANJI/KATA)* 'する'
    #Rentai
}

Katei = {
      VERB5KA [け]
    / VERB5SA [せ]
    / VERB5TA [て]
    / VERB5NA [ね]
    / VERB5MA [め]
    / VERB5RA [れ]
    / VERB5WA [え]
    / VERB5GA [げ]
    / VERB5BA [べ]
    / VERB5KASOKU [け]
    / VERB1 [れ]
    / KAHEN? ('くれ' / '来れ')
    / SAHEN_SURU 'すれ'
    / SAHEN_ZURU 'ずれ'
    / (KANJI/KATA)+ 'すれ'
    #Katei
}

Meirei = {
      VERB5KA [け]
    / VERB5SA [せ]
    / VERB5TA [て]
    / VERB5NA [ね]
    / VERB5MA [め]
    / VERB5RA [れ]
    / VERB5WA [え]
    / VERB5GA [げ]
    / VERB5BA [べ]
    / VERB5KASOKU [け]
    / VERB1 [ろよ]
    / KAHEN? ('こい' / '来い')
    / SAHEN_SURU ('しろ' / 'せよ')
    / SAHEN_ZURU ('じろ' / 'ぜよ')
    / (KANJI/KATA)+ ('しろ' / 'せよ')
    / ('ください'/'下さい')  // 敬語動詞「下さる」の命令形は特殊
    #Meirei
}


/*
活用型ごとの動詞の語幹を辞書から生成
「行く」はカ行五段活用だが、イ音便ではなく促音便で活用が異なるため別枠
*/
VERB5KA = @choice('dic/Verb/VERB5KA.txt')
VERB5SA = @choice('dic/Verb/VERB5SA.txt')
VERB5TA = @choice('dic/Verb/VERB5TA.txt')
VERB5NA = @choice('dic/Verb/VERB5NA.txt')
VERB5MA = @choice('dic/Verb/VERB5MA.txt')
VERB5RA = @choice('dic/Verb/VERB5RA.txt')
VERB5WA = @choice('dic/Verb/VERB5WA.txt')
VERB5GA = @choice('dic/Verb/VERB5GA.txt')
VERB5BA = @choice('dic/Verb/VERB5BA.txt')
VERB1 = @choice('dic/Verb/VERB1.txt')
KAHEN = @choice('dic/Verb/KAHEN.txt')
SAHEN_SURU = @choice('dic/Verb/SAHEN_SURU.txt')
SAHEN_ZURU = @choice('dic/Verb/SAHEN_ZURU.txt')
VERB5KASOKU = '行'



/*
形容詞の語幹と活用語尾
実験的に助動詞や助詞、動詞を先読みで入れている
語幹は辞書の"Adjective.dic"と"Noun.nai.dic"から生成
*/
Adjective =
    / {ADJ 'かろ' #ADJMizen}
    / {ADJ 'かっ' #ADJRenyo}
    / {ADJ 'く' #ADJRenyo}
    / {ADJ 'けれ' #ADJKatei}
    / {ADJ 'い' #ADJSyushi}
    / {ADJ 'い' #ADJRentai}
    / {ADJ #ADJGokan}

ADJ = @choice('dic/ADJ.txt')

/*
形容動詞の語幹と活用語尾
形容詞とやっていることは同じ
語幹は辞書の"Noun.adjv.dic"から生成
*/
AdjectiveVerb =
    / {ADJV 'だろ' #ADJVMizen}
    / {ADJV 'だっ' #ADJVRenyo}
    / {ADJV 'で' #ADJVRenyo}
    / {ADJV 'に' #ADJVRenyo}
    / {ADJV 'だ' #ADJVSyushi}
    / {ADJV 'な' #ADJVRentai}
    / {ADJV 'なら' #ADJVKatei}
    / {ADJV #ADJVGokan}

ADJV = @choice('dic/ADJV.txt')


/*
名詞
漢字カタカナアルファベット数字
混合した場合も可（例：アップル社）
括弧で括ったものも名詞
*/
Noun =
    / { BLOCK #Block }
    / { NUM [つこ個]? [め目]? #Num }
    / { ('ひと'/'ふた'/'みっ'/'よっ'/'いつ'/'むっ'/'なな'/'やっ'/'ここの') 'つ' [め目]? #Count }
    / { NOUN #Noun }


BLOCK =
    / '\[' (!'\]' .)* '\]'
    / '\{' (!'\}' .)* '\}'
    / '\(' (!'\)' .)* '\)'
    / '\"' (!'\"' .)* '\"'
    / '\'' (!'\'' .)* '\''
    / '\<' (!'\>' .)* '\>'
    / '\（' (!'\）' .)* '\）'
    / '\「' (!'\」' .)* '\」'
    / '\『' (!'\』' .)* '\』'
    / '\“' (!'\”' .)* '\”'
    / '\‘' (!'\’' .)* '\’'
    / '\【' (!'\】' .)* '\】'
    / '\［' (!'\］' .)* '\］'
    / '\｛' (!'\｝' .)* '\｝'
    / '\〔' (!'\〕' .)* '\〕'


/* 助詞、手入力 */
Postp = {
    POSTP
    #Postp
}
POSTP = @choice('dic/POSTP.txt')


/* 連体詞、辞書から生成 */
Adnominal = {
    ADNM
    #Adnominal
}
ADNM = @choice('dic/ADNM.txt')


/* 副詞、辞書から生成 */
Adverb = {
    ADV
    #Adverb
}
ADV = @choice('dic/ADV.txt')


/* 接続詞、辞書から生成 */
Conjunction = {
    CONJ
    #Conjunction
}
CONJ = @choice('dic/CONJ.txt')




/* 助動詞 */
// 買わせる
Aux_Seru =
    / {'せ' #Mizen} (Aux_Rareru/Aux_Nai/Aux_You/Aux_Mai)?
    / {'せ' #Renyo} (Aux_Tai/Aux_Tagaru/Aux_Ta/Aux_Souda1/Aux_Masu)?
    / {'せる' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo)?
    / {'せる' #Rentai} (Aux_Youda)?
    / {'せれ' #Katei}
    / {'せろ' #Meirei}
    / {'せよ' #Meirei}

// 起きさせる
Aux_Saseru =
    / {'させ' #Mizen} (Aux_Rareru/Aux_Nai/Aux_You/Aux_Mai)?
    / {'させ' #Renyo} (Aux_Tai/Aux_Tagaru/Aux_Ta/Aux_Souda1/Aux_Masu)?
    / {'させる' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo)?
    / {'させる' #Rentai} (Aux_Youda)?
    / {'させれ' #Katei}
    / {'させろ' #Meirei}
    / {'させよ' #Meirei}

// 話される
Aux_Reru =
    / {'れ' #Mizen} (Aux_Nai/Aux_You/Aux_Mai)?
    / {'れ' #Renyo} (Aux_Tai/Aux_Tagaru/Aux_Ta/Aux_Souda1/Aux_Masu)?
    / {'れる' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo)?
    / {'れる' #Rentai} (Aux_Youda)?
    / {'れれ' #Katei}
    / {'れろ' #Meirei}
    / {'れよ' #Meirei}

// 起きられる
Aux_Rareru =
    / {'られ' #Mizen} (Aux_Nai/Aux_You/Aux_Mai)?
    / {'られ' #Renyo} (Aux_Tai/Aux_Tagaru/Aux_Ta/Aux_Souda1/Aux_Masu)?
    / {'られる' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo)?
    / {'られる' #Rentai} (Aux_Youda)?
    / {'られれ' #Katei}
    / {'られろ' #Meirei}
    / {'られよ' #Meirei}

// 話さない
Aux_Nai =
    / {'なかろ' #Mizen} (Aux_U)?
    / {'なかっ' #Renyo} (Aux_Ta)?
    / {'なく' #Renyo}
    / {'ない' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo)?
    / {'ない' #Rentai} (Aux_Youda)?
    / {'なけれ' #Katei}

// 話さず
Aux_Nu =
    / {'ず' #Renyo}
    / {'ぬ' #Syushi}
    / {'ぬ' #Rentai} (Aux_Youda)?
    / {'ね' #Katei}

// しません
Aux_Nn =
    / {'ん' #Syushi}
    / {'ん' #Rentai}

// 話しましょう
Aux_U =
    / {'う' #Syushi}
    / {'う' #Rentai}

// 起きよう
Aux_You =
    / {'よう' #Syushi}
    / {'よう' #Rentai}

// 話すまい
Aux_Mai =
    / {'まい' #Syushi}
    / {'まい' #Rentai}

// 話したい
Aux_Tai =
    / {'たかろ' #Mizen} (Aux_U)?
    / {'たかっ' #Renyo} (Aux_Ta)?
    / {'たく' #Renyo}
    / {'たい' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo)?
    / {'たい' #Rentai} (Aux_Youda)?
    / {'たけれ' #Katei}

// 話したがる
Aux_Tagaru =
    / {'たがら' #Mizen} (Aux_Nai/Aux_Nu)?
    / {'たがり' #Renyo} (Aux_Souda1/Aux_Masu)?
    / {'たがっ' #Renyo} (Aux_Ta)?
    / {'たがる' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo)?
    / {'たがる' #Rentai} (Aux_Youda)?
    / {'たがれ' #Katei}

// 話した
Aux_Ta =
    / {'たろ' #Mizen} (Aux_U)?
    / {'た' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo)?
    / {'た' #Rentai} (Aux_Youda)?
    / {'たら' #Katei}

// 呼んだ
Aux_Da1 =
    / {'だろ' #Mizen} (Aux_U)?
    / {'だ' #Syushi} (Aux_Souda2/Aux_Rashii/Aux_Daro/Aux_Desyo)?
    / {'だ' #Rentai} (Aux_Youda)?
    / {'だら' #Katei}

// 起きそうだ
Aux_Souda1 =
    / {'そうだろ' #Mizen} (Aux_U)?
    / {'そうだっ' #Renyo} (Aux_Ta)?
    / {'そうで' #Renyo}
    / {'そうに' #Renyo}
    / {'そうだ' #Syushi} (Aux_Souda2/Aux_Rashii)?
    / {'そうな' #Rentai} (Aux_Youda)?
    / {'そうなら' #Katei}

// 起きるそうだ
Aux_Souda2 =
    / {'そうで' #Renyo}
    / {'そうだ' #Syushi}

// 話すようだ
Aux_Youda =
    / {'ようだろ' #Mizen} (Aux_U)?
    / {'ようだっ' #Renyo} (Aux_Ta)?
    / {'ようで' #Renyo}
    / {'ように' #Renyo}
    / {'ようだ' #Syushi} (Aux_Souda2/Aux_Rashii)?
    / {'ような' #Rentai}
    / {'ようなら' #Katei}


// 話すらしい
Aux_Rashii =
    / {'らしかっ' #Renyo} (Aux_Ta)?
    / {'らしく' #Renyo}
    / {'らしい' #Syushi} (Aux_Souda2/Aux_Daro/Aux_Desyo)?
    / {'らしい' #Rentai} (Aux_Youda)?
    / {'らしけれ' #Katei}

// 話しましょう
Aux_Masu =
    / {'ませ' #Mizen} (Aux_Nn)?
    / {'ましょ' #Mizen} (Aux_U)?
    / {'まし' #Renyo} (Aux_Ta/Postp)?
    / {'ます' #Syushi} (Aux_Mai/Aux_Desyo)?
    / {'ます' #Rentai}
    / {'ますれ' #Katei}
    / {'ませ' #Meirei}
    / {'まし' #Meirei}

// 夢だ
Aux_Da2 =
    / {'だろ' #Mizen} (Aux_U)?
    / {'だっ' #Renyo} (Aux_Ta)?
    / {'で' #Renyo}
    / {'だ' #Syushi} (Aux_Souda2/Aux_Rashii)?
    / {'な' #Rentai} (Aux_Youda)?
    / {'なら' #Katei}

// 話すだろう
Aux_Daro =
    / {'だろ' #Mizen} (Aux_U)?
    / {'で' #Renyo}
    / {'なら' #Katei}

// 夢です
Aux_Desu =
    / {'でしょ' #Mizen} (Aux_U)?
    / {'でし' #Renyo} (Aux_Ta)?
    / {'です' #Syushi}
    / {'です' #Rentai}

// 話すでしょう
Aux_Desyo =
    / {'でしょ' #Mizen} (Aux_U)?
