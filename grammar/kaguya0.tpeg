/* KAGUYA文法 */

/*
トップの規則
入力をChunkの繰り返しとみる
*/
Sentence = {
    Chunck*
    #Sentence
}

/*
形容詞や形容動詞、動詞や連体詞などのひとかたまりごとにマッチする規則
*/
Chunck = {
      AdjectivePhrase
    / VerbPhrase
    / Adverb
    / Adnominal
    / Noun Postp
    / Postp
    / {TEN}
    #Chunck
}

example Sentence 要求は認められないので代案を用意せよ
example Sentence 僕は転びそうだったが持ちこたえた
example Sentence 紅茶を入れますわ
example Sentence 駐車場に車を止める
example Sentence 駐車場に車が止まる
example Sentence 駐車場に車が止まらない
example Sentence 雨が止む
example Sentence 酒は飲んでも飲まれるな
example Sentence スマホを充電してあげる
example Sentence 変数を定義して数値を代入する
example Sentence 会いたくて震える
example Sentence 期限を過ぎなければ提出を認めるだろう
example Sentence 階段を駆け上がったので息が切れた
example Sentence 雨が降ったらしいけれど家に居たので何も知らなかった
example Sentence 変数に数値を代入し計算を実行し結果をコンソールに出力する
example Sentence 変数に数値を代入し、計算を実行し、結果をコンソールに出力する。

EOF = !.
EOS = [。．.\n] / EOF
EOL = [。．.\n] / EOF / TEN
TEN = [、，,\n]
KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]
ALPHA = [a-zA-Zａ-ｚＡ-Ｚ]
NUM = [0-9０-９]


/*
動詞節
スモールケースから順に
1. 動詞の活用した形のみの場合：話す、話し
2. 動詞の活用した形+助動詞の場合：話した、話される
3. 動詞の活用した形+動詞節の場合：話し始める
4. 動詞の活用した形+助動詞+動詞節の場合：話され始める
*/
VerbPhrase = {
      Verb AuxVerbs VerbPhrase?
    / Verb VerbPhrase?
    #VerbPhrase
}

example VerbPhrase 話したく思う
example VerbPhrase 攻撃され始めた
example VerbPhrase 話し終わる
example VerbPhrase 駆け上がる
example VerbPhrase 立ち止まる
example VerbPhrase 止まる
example VerbPhrase 止まらない

/*
形容詞または形容動詞
組み方は動詞と一緒
*/
AdjectivePhrase = {
      (Adjective/AdjectiveVerb) AuxVerbs AdjectivePhrase?
    / (Adjective/AdjectiveVerb) AdjectivePhrase?
    #AdjectivePhrase
}

example AdjectivePhrase 赤いボール
example AdjectivePhrase 赤かったボール
example AdjectivePhrase うるさい
example AdjectivePhrase うるさかった
example AdjectivePhrase うるさくない
example AdjectivePhrase うるさかろう
example AdjectivePhrase うるさそうだった
example AdjectivePhrase うるさいらしい
example AdjectivePhrase うるさいでしょう
example AdjectivePhrase 赤くない
example AdjectivePhrase 赤くなく青い
example AdjectivePhrase 赤くかっこいい
example AdjectivePhrase 健康な人
example AdjectivePhrase 健康だった
example AdjectivePhrase 健康でしょう
example AdjectivePhrase ぞっこんだ
example AdjectivePhrase なめい
example AdjectivePhrase なめかった
example AdjectivePhrase なめらかだ
example AdjectivePhrase なめらかなやわらかい布

/*
形容詞の語幹と活用語尾
実験的に助動詞や助詞、動詞を先読みで入れている
語幹は辞書の"Adjective.dic"と"Noun.nai.dic"から生成
*/
Adjective =
    / {ADJ 'かろ' &'う' #ADJMizen}
    / {ADJ 'かっ' &'た' #ADJRenyo}
    / {ADJ 'く' &([ては]/Verb/Adjective/AdjectiveVerb) #ADJRenyo}
    / {ADJ 'けれ' &'ば' #ADJKatei}
    / {ADJ 'い' &('そう'/'らし'/'でしょ'/'だろ'/NOUN/EOL) #ADJSyushiRentai}
    / {ADJ  &'そう' #ADJGokan}

ADJ = @choice('dic/ADJ.txt')

/*
形容動詞の語幹と活用語尾
形容詞とやっていることは同じ
語幹は辞書の"Noun.adjv.dic"から生成
*/
AdjectiveVerb =
    / {ADJV 'だろ' &'う' #ADJVMizen}
    / {ADJV 'だっ' &'た' #ADJVRenyo}
    / {ADJV 'で' &([はも]/SUBARU/SUBNAI) #ADJVRenyo}
    / {ADJV 'に' &(Verb/Adjective/AdjectiveVerb) #ADJVRenyo}
    / {ADJV 'なら' #ADJVKatei}
    / {ADJV 'だ' &('そう'/EOL) #ADJVSyushi}
    / {ADJV 'な' &('ようだ'/NOUN/AdjectivePhrase) #ADJVRentai}
    / {ADJV &('そう'/'らし'/'でしょ') #ADJVGokan}

ADJV = @choice('dic/ADJV.txt')

/*
補助動詞の「ある」と「ない」
*/
SUBARU = 'あ' [らりるれろっ]
SUBNAI = 'な' ('かろ' / 'かっ' / 'く' / 'い' / 'けれ')


/*
動詞の語幹と活用語尾
活用順にしており、活用語尾が同じものまとめている
*/
Verb =
    / Mizen
    / Renyo
    / SyushiRentai
    / Katei
    / Meirei
    / KateiMeirei
    / Common

example Verb 歩く
example Verb 認める
example Verb 会う
example Verb 止む
example Verb 止まる
example Verb 止める

/*
動詞の未然形活用
未然形はほぼ確実に助動詞が繋がるため先読みで誤マッチを抑制している
これで「止まる」の先頭文字列「止ま」が「止む」の未然形「止ま」にマッチしない
以下、他の活用も同様
*/
Mizen = {
      VERB5KA [かこ] &AuxVerbs
    / VERB5SA [さそ] &AuxVerbs
    / VERB5TA [たと] &AuxVerbs
    / VERB5NA [なの] &AuxVerbs
    / VERB5MA [まも] &AuxVerbs
    / VERB5RA [らろ] &AuxVerbs
    / VERB5WA [わお] &AuxVerbs
    / VERB5GA [がご] &AuxVerbs
    / VERB5BA [ばぼ] &AuxVerbs
    / VERB5KASOKU [かこ] &AuxVerbs
    / SAHEN_SURU [さ] &AuxVerbs
    / SAHEN_ZURU [ざ] &AuxVerbs
    / (KANJI/KATA)* [さ] &AuxVerbs
    #Mizen
}

Renyo = {
      VERB5KA [きい] &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / VERB5SA [し] &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / VERB5TA [ちっ] &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / VERB5NA [にん] &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / VERB5MA [みん] &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / VERB5RA [りっ] &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / VERB5WA [いっ] &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / VERB5GA [ぎい] &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / VERB5BA [びん] &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / VERB5KASOKU [きっ] &(AuxVerbs/[てで]/Verb/TEN/EOL)
    / KAHEN? [き] &(AuxVerbs/[てで]/Verb/TEN/EOL)
    #Renyo
}

SyushiRentai = {
      VERB5KA [く] &(AuxVerbs/EOL/'')
    / VERB5SA [す] &(AuxVerbs/EOL/'')
    / VERB5TA [つ] &(AuxVerbs/EOL/'')
    / VERB5NA [ぬ] &(AuxVerbs/EOL/'')
    / VERB5MA [む] &(AuxVerbs/EOL/'')
    / VERB5RA [る] &(AuxVerbs/EOL/'')
    / VERB5WA [う] &(AuxVerbs/EOL/'')
    / VERB5GA [ぐ] &(AuxVerbs/EOL/'')
    / VERB5BA [ぶ] &(AuxVerbs/EOL/'')
    / VERB5KASOKU [く] &(AuxVerbs/EOL/'')
    / VERB1 [る] &(AuxVerbs/EOL/'')
    / KAHEN? ('くる' / '来る') &(AuxVerbs/EOL/'')
    / SAHEN_SURU 'する' &(AuxVerbs/EOL/'')
    / SAHEN_ZURU 'ずる' &(AuxVerbs/EOL/'')
    / (KANJI/KATA)* 'する' &(AuxVerbs/EOL/'')
    #SyushiRentai
}

Katei = {
      VERB1 [れ] &'ば'
    / KAHEN? ('くれ' / '来れ') &'ば'
    / SAHEN_SURU 'すれ' &'ば'
    / SAHEN_ZURU 'ずれ' &'ば'
    / (KANJI/KATA)+ 'すれ' &'ば'
    #Katei
}

Meirei = {
      VERB1 [ろよ] &EOL
    / KAHEN? ('こい' / '来い') &EOL
    / SAHEN_SURU ('しろ' / 'せよ') &EOL
    / SAHEN_ZURU ('じろ' / 'ぜよ') &EOL
    / (KANJI/KATA)+ ('しろ' / 'せよ') &EOL
    #Meirei
}

KateiMeirei = {
      VERB5KA [け] &('ば'/EOL)
    / VERB5SA [せ] &('ば'/EOL)
    / VERB5TA [て] &('ば'/EOL)
    / VERB5NA [ね] &('ば'/EOL)
    / VERB5MA [め] &('ば'/EOL)
    / VERB5RA [れ] &('ば'/EOL)
    / VERB5WA [え] &('ば'/EOL)
    / VERB5GA [げ] &('ば'/EOL)
    / VERB5BA [べ] &('ば'/EOL)
    / VERB5KASOKU [け] &('ば'/EOL)
    #KateiMeirei
}

Common =
    / {VERB1 &(AuxVerbs/[てで]/Verb/TEN/EOL) #MizenRenyo}
    / {KAHEN? [来] &(AuxVerbs/[てで]/Verb/TEN/EOL) #MizenRenyo}
    / {KAHEN? [こ] &AuxVerbs #Mizen}  // 命令形の活用語尾「こい」との競合避け
    / {SAHEN_SURU [し] &(AuxVerbs/[てで]/Verb/TEN/EOL) #MizenRenyo}
    / {SAHEN_ZURU [じ] &(AuxVerbs/[てで]/Verb/TEN/EOL) #MizenRenyo}
    / {(KANJI/KATA)* [し] &(AuxVerbs/[てで]/Verb/TEN/EOL) #MizenRenyo}
    / {SAHEN_SURU [せ] &AuxVerbs #Mizen}  // 命令形の活用語尾「せよ」との競合避け
    / {SAHEN_ZURU [ぜ] &AuxVerbs #Mizen}  // 命令形の活用語尾「ぜよ」との競合避け
    / {(KANJI/KATA)* [せ] &AuxVerbs #Mizen}  // 命令形の活用語尾「せよ」との競合避け

/*
活用型ごとの動詞の語幹を辞書から生成
「行く」はカ行五段活用だが、イ音便ではなく促音便で活用が異なるため別枠
*/
VERB5KA = @choice('dic/Verb/VERB5KA.txt')
VERB5SA = @choice('dic/Verb/VERB5SA.txt')
VERB5TA = @choice('dic/Verb/VERB5TA.txt')
VERB5NA = @choice('dic/Verb/VERB5NA.txt')
VERB5MA = @choice('dic/Verb/VERB5MA.txt')
VERB5RA = @choice('dic/Verb/VERB5RA.txt')
VERB5WA = @choice('dic/Verb/VERB5WA.txt')
VERB5GA = @choice('dic/Verb/VERB5GA.txt')
VERB5BA = @choice('dic/Verb/VERB5BA.txt')
VERB1 = @choice('dic/Verb/VERB1.txt')
KAHEN = @choice('dic/Verb/KAHEN.txt')
SAHEN_SURU = @choice('dic/Verb/SAHEN_SURU.txt')
SAHEN_ZURU = @choice('dic/Verb/SAHEN_ZURU.txt')
VERB5KASOKU = '行'


/* 助動詞、gen_auxverb.pyで生成 */
AuxVerbs = {
    AUXVERB
    #AuxVerbs
}
AUXVERB = @choice('dic/Auxverb/AUXVERB.txt')


/* 助詞、手入力 */
Postp = {
    POSTP
    #Postp
}
POSTP = @choice('dic/POSTP.txt')


/* 連体詞、辞書から生成 */
Adnominal = {
    ADNM
    #Adnominal
}
ADNM = @choice('dic/ADNM.txt')


/* 副詞、辞書から生成 */
Adverb = {
    ADV
    #Adverb
}
ADV = @choice('dic/ADV.txt')


/* 名詞 */
Noun = {
    NOUN
    #Noun
}

/*
漢字カタカナアルファベット数字
混合した場合も可（例：アップル社
括弧で括ったものも名詞
*/
NOUN =
    / (KANJI/KATA/ALPHA/NUM)+
    / B_START (!B_END .)* B_END

B_START = [\[\{\(\"\'<（「『”’［｛【〔“]
B_END   = [\]\}\)\"\'>）」』”’］｝】〕“]

