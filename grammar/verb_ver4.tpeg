/* 動詞の文法、choice版テスト */

/* MEMO
+ 「やってくる」が「やる」の活用「やっ」に吸われる
+ 「おきる（起きる）」が「おく（置く）」の活用「おき」に吸われる
    + ひらがな2文字（語幹がひらがな1文字）の動詞は除外した
+ 「止まる」が「止む」の未然形「止ま」に吸われる
    + 選択の順序の調整が必要
    + 直した
+ 「〜させてもらう」が助詞「ても」でエラーになる
+ 選択の最優先事項は動詞の長さ→活用内でソートしてもダメ
    + ただし、「止まる」の例で考えると、「止む+る」は接続としてありえないので、動詞の活用語尾を決定する規則内で先読みをすれば対応できそう→細分化が必要
+ 詰む（マ行五段）、詰める（下一段）、詰まる（ラ行五段）、詰まない、詰めない、詰まらない
*/

Sentence = {
    // Others
    (
        Sov
        Particle?
        [,、，\n]?
    )+
    EOS
    // Remain
    #Sentence
} EOF

Sov = {
    Subjects? VerbPhrase
    #SOV
}

example Sentence 要求は認められないので代案を用意せよ
example Sentence 僕は転びそうだったが持ちこたえた
example Sentence 紅茶を入れますわ
example Sentence 駐車場に車を止める
example Sentence 駐車場に車が止まる
example Sentence 駐車場に車が止まらない
example Sentence 雨が止む
example Sentence 酒は飲んでも飲まれるな
example Sentence スマホを充電してあげる
example Sentence 変数を定義して数値を代入する
example Sentence 会いたくて震える
example Sentence 期限を過ぎなければ提出を認めるだろう
example Sentence 階段を駆け上がったので息が切れた
example Sentence 雨が降ったらしいけれど家に居たので何も知らなかった
example Sentence 変数に数値を代入し計算を実行し結果をコンソールに出力する
example Sentence 変数に数値を代入し、計算を実行し、結果をコンソールに出力する。

EOF = !.
EOS = [。．.\n] / EOF
KANJI = [㐀-䶵一-龠々〇〻ーご]+
KATA = [ァ-ヶー・]+

Others = {
    (!VerbPhrase .)*
    #Others
}

Subjects = {
    (!VerbPhrase Subject)+
    #Subjects
}

Subject = {
    (KANJI/KATA) [はをにがのもで]
    #Temp
}

VerbPhrase = {
      Verb AuxVerbs VerbPhrase?
    / Verb VerbPhrase?
    #VerbPhrase
}


/* 助動詞 */
/* 参考：https://www.kokugobunpou.com/%E5%8A%A9%E5%8B%95%E8%A9%9E/%E5%8A%A9%E5%8B%95%E8%A9%9E%E6%B4%BB%E7%94%A8%E8%A1%A8/ */
AuxVerbs = {
    AUXVERB
    #AuxVerbs
}
AUXVERB = @choice('dic/auxverbs.txt')

example VerbPhrase 話したく思う
example VerbPhrase 攻撃され始めた
example VerbPhrase 話し終わる
example VerbPhrase 駆け上がる
example VerbPhrase 立ち止まる
example VerbPhrase 止まる

Remain = {
    (!EOS .)*
    #Remain
}


/* ここから助詞 */
/* 参考：https://www.kokugobunpou.com/%E5%8A%A9%E8%A9%9E/%E5%8A%A9%E8%A9%9E%E3%81%AE%E6%80%A7%E8%B3%AA%E3%81%A8%E5%83%8D%E3%81%8D/ */
Particle = {
    (PAR_CON / PAR_AUX / PAR_END)
    #Particle
}

PAR_CON =
    / 'けれども'
    / 'ところで'
    / 'けれど'
    / 'ながら'
    / 'ものの'
    / 'ても'
    / 'でも'
    / 'のに'
    / 'ので'
    / 'から'
    / 'つつ'
    / 'たり'
    / 'だり'
    / 'ば'
    / 'と'
    / 'が'
    / 'し'
    / 'て'
    / 'で'

PAR_AUX =
    / 'ばかり'
    / 'こそ'
    / 'さえ'
    / 'でも'
    / 'しか'
    / 'まで'
    / 'だけ'
    / 'ほど'
    / 'など'
    / 'なり'
    / 'やら'
    / 'だの'
    / 'きり'
    / 'ぎり'
    / 'は'
    / 'も'
    / 'か'

PAR_END =
    / 'なあ'
    / 'とも'
    / 'ねえ'
    / 'か'
    / 'な'
    / 'や'
    / 'ぞ'
    / 'よ'
    / 'の'
    / 'わ'
    / 'さ'
    / 'ね'


/* ここから動詞、生成部分は下の方 */
/* 参考：https://www.kokugobunpou.com/%E7%94%A8%E8%A8%80/%E5%8B%95%E8%A9%9E-3-%E6%B4%BB%E7%94%A8%E5%BD%A2%E3%81%A8%E3%81%9D%E3%81%AE%E7%94%A8%E6%B3%95/ */
Verb =
    / Mizen
    / Renyo
    / SyushiRentai
    / Katei
    / Meirei
    / KateMeirei
    / Common

example Verb 歩く
example Verb 認める
example Verb 会う
example Verb 止む
example Verb 止まる
example Verb 止める


Mizen = {
      VERB5KA [かこ]
    / VERB5SA [さそ]
    / VERB5TA [たと]
    / VERB5NA [なの]
    // / VERB5HA [はほ]
    / VERB5MA [まも] &AuxVerbs
    / VERB5RA [らろ]
    / VERB5WA [わお]
    / VERB5GA [がご]
    / VERB5BA [ばぼ]
    / VERB5KASOKU [かこ]
    / SAHEN_SURU [さ]
    / SAHEN_ZURU [ざ]
    / (KANJI/KATA)? [さ]
    #Mizen
}

Renyo = {
      VERB5KA [きい]
    / VERB5SA [し]
    / VERB5TA [ちっ]
    / VERB5NA [にん]
    // / VERB5HA [ひ]
    / VERB5MA [みん]
    / VERB5RA [りっ]
    / VERB5WA [いっ]
    / VERB5GA [ぎい]
    / VERB5BA [びん]
    / VERB5KASOKU [きっ]
    / KAHEN? [き]
    #Renyo
}

SyushiRentai = {
      VERB5KA [く]
    / VERB5SA [す]
    / VERB5TA [つ]
    / VERB5NA [ぬ]
    // / VERB5HA [ふ]
    / VERB5MA [む]
    / VERB5RA [る]
    / VERB5WA [う]
    / VERB5GA [ぐ]
    / VERB5BA [ぶ]
    / VERB5KASOKU [く]
    / VERB1 [る]
    / KAHEN? ('くる' / '来る')
    / SAHEN_SURU 'する'
    / SAHEN_ZURU 'ずる'
    / (KANJI/KATA)? 'する'
    #SyushiRentai
}

Katei = {
      VERB1 [れ]
    / KAHEN? ('くれ' / '来れ')
    / SAHEN_SURU 'すれ'
    / SAHEN_ZURU 'ずれ'
    / (KANJI/KATA) 'すれ'
    #Katei
}

Meirei = {
      VERB1 [ろよ]
    / KAHEN? ('こい' / '来い')
    / SAHEN_SURU ('しろ' / 'せよ')
    / SAHEN_ZURU ('じろ' / 'ぜよ')
    / (KANJI/KATA) ('しろ' / 'せよ')
    #Meirei
}

KateMeirei = {
      VERB5KA [け]
    / VERB5SA [せ]
    / VERB5TA [て]
    / VERB5NA [ね]
    // / VERB5HA [へ]
    / VERB5MA [め]
    / VERB5RA [れ]
    / VERB5WA [え]
    / VERB5GA [げ]
    / VERB5BA [べ]
    / VERB5KASOKU [け]
    #KateMeirei
}

Common =
    / {VERB1 #MizenRenyo}
    / {KAHEN? [来] #MizenRenyo}
    / {KAHEN? [こ] #Mizen}  // 命令形の活用語尾「こい」との競合避け
    / {SAHEN_SURU [し] #MizenRenyo}
    / {SAHEN_ZURU [じ] #MizenRenyo}
    / {(KANJI/KATA)? [し] #MizenRenyo}
    / {SAHEN_SURU [せ] #Mizen}  // 命令形の活用語尾「せよ」との競合避け
    / {SAHEN_ZURU [ぜ] #Mizen}  // 命令形の活用語尾「ぜよ」との競合避け
    / {(KANJI/KATA)? [せ] #Mizen}  // 命令形の活用語尾「せよ」との競合避け


/* 特殊な動詞 */
/* 「行く」はカ行五段活用だが、イ音便ではなく促音便 */
VERB5KASOKU = '行'
VERB5KA = @choice('dic/verbs/VERB5KA.txt')
VERB5SA = @choice('dic/verbs/VERB5SA.txt')
VERB5TA = @choice('dic/verbs/VERB5TA.txt')
VERB5NA = @choice('dic/verbs/VERB5NA.txt')
VERB5MA = @choice('dic/verbs/VERB5MA.txt')
VERB5RA = @choice('dic/verbs/VERB5RA.txt')
VERB5WA = @choice('dic/verbs/VERB5WA.txt')
VERB5GA = @choice('dic/verbs/VERB5GA.txt')
VERB5BA = @choice('dic/verbs/VERB5BA.txt')
VERB1 = @choice('dic/verbs/VERB1.txt')
KAHEN = @choice('dic/verbs/KAHEN.txt')
SAHEN_SURU = @choice('dic/verbs/SAHEN_SURU.txt')
SAHEN_ZURU = @choice('dic/verbs/SAHEN_ZURU.txt')
