/* 先読みのない学校文法 */

/*
トップの規則
句点で終わる一文に対応している
文をChunkの繰り返しとみる
*/
Sentence = {
    // (TEN/Chunk)* [。．.\n]? {.* #Remain}  // デバッグモード
    (TEN/Chunk)* EOS
    #Sentence
}


/*
形容詞句や動詞句などのひとかたまりごとにマッチする規則
先読みのない"gakkou.tpeg"では、ここの順番が大きく影響する
*/

Chunk =
    / AdnominalPhrase  // 連体詞
    / AdverbPhrase  // 副詞
    / VerbPhrase  // 動詞句
    / NounPhrase  // 名詞句
    / AdjectivePhrase  // 形容詞句
    / AdjectiveVerbPhrase  // 形容動詞句
    / ConjunctionPhrase  // 接続詞
    / InterjectionPhrase  // 感動詞


EOF = !.
EOS = [。．.\n]? EOF
TEN = [・、，,:：\n]
Eol = { (TEN/EOS) #Eol }
KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]+
ALPHA = [a-zA-Zａ-ｚＡ-Ｚ]+
NUM = [\-ー]? [0-9０-９] [\.\,．，]? [0-9０-９]*
    / [一二三四五六七八九十百千万億兆]+
SYMBOL =
    / [^＾~〜\-ー_＿!！?？#＃$＄&＆=＝@＠*＊+＋/・]
    / [\[\]\(\)\<\>\{\}\'\"\`「」（）＜＞『』［］【】”“｀”“’‘｛｝〔〕]


/*
動詞句
スモールケースから順に
1. 動詞の活用した形のみの場合：話す、話し
2. 動詞の活用した形+助動詞の場合：話した、話される
3. 動詞の活用した形+動詞句の場合：話し始める
4. 動詞の活用した形+助動詞+動詞句の場合：話され始める
*/
VerbPhrase = {
    Verb AuxVerbPhrase? PostpPhrase?
    #VerbPhrase
}


/*
形容詞
組み方は動詞と一緒
*/
AdjectivePhrase = {
    Adjective AuxVerbPhrase? PostpPhrase?
    #AdjectivePhrase
}


/*
形容動詞
組み方は動詞と一緒
*/
AdjectiveVerbPhrase = {
    AdjectiveVerb AuxVerbPhrase? PostpPhrase?
    #AdjectiveVerbPhrase
}


/*
名詞句
名詞にも助動詞が付属語としてつきうる
*/
NounPhrase = {
    Noun AuxVerbPhrase? PostpPhrase?
    #NounPhrase
}


/*
接続詞句
接続詞に助詞が続くことはないものとしている
*/
ConjunctionPhrase = {
    Conjunction
    #ConjunctionPhrase
}

/*
感動詞句
感動詞に助詞が続くことはないものとしている
*/
InterjectionPhrase = {
    Interjection
    #InterjectionPhrase
}


/*
連体詞句
「この・その・あの・どの」には助動詞「ようだ」がつきうる
連体詞には助詞は続かない
*/
AdnominalPhrase = {
    Adnominal AuxVerbPhrase? PostpPhrase?
    #AdnominalPhrase
}


/*
副詞句
*/
AdverbPhrase = {
    Adverb AuxVerbPhrase? PostpPhrase?
    #AdverbPhrase
}


/*
助詞句
助詞の「の」だけに続く助動詞があるため別扱いしている
助詞に続く助動詞「な」はその後が限定的なので別扱いしている
*/
PostpPhrase = {
    Postp AuxVerbPhrase? PostpPhrase?
    #PostpPhrase
}

AuxVerbPhrase = {
    AuxVerb+
    #AuxVerbPhrase
}

/*
動詞の語幹と活用語尾
活用順にしており、活用語尾が同じものまとめている
*/
Verb = {
      VERB5KA [かきくけこい]
    / VERB5SA [さしすせそ]
    / VERB5TA [たちつてとっ]
    / VERB5NA [なにぬねのん]
    / VERB5MA [まみむめもん]
    / VERB5RA [らりるれろっ]
    / VERB5WA [わいうえおっ]
    / VERB5GA [がぎぐげごい]
    / VERB5BA [ばびぶべぼん]
    / VERB5KASOKU [かきくけこっ]
    / SAHEN_SURU 'する'/'すれ'/'しろ'/'せよ'/[さしせ]
    / SAHEN_ZURU 'ずる'/'ずれ'/'じろ'/'ぜよ'/[ざじぜ]
    / (KANJI/KATA)* 'する'/'すれ'/'しろ'/'せよ'/[さしせ]
    / KAHEN? ( ([来] [るれい]?) / ('くる'/'くれ'/'こい'/[こき]) )
    / ('ください'/'下さい')  // 敬語動詞「下さる」の命令形は特殊
    / VERB1 [るれろよ]?
    #Verb
}


/*
形容詞の語幹と活用語尾
実験的に助動詞や助詞、動詞を先読みで入れている
語幹は辞書の"Adjective.dic"と"Noun.nai.dic"から生成
*/
Adjective =
    / {ADJ 'かろ' #ADJMizen}
    / {ADJ 'かっ' #ADJRenyo}
    / {ADJ 'けれ' #ADJKatei}
    / {ADJ 'く' #ADJRenyo}
    / {ADJ 'い' #ADJSyushi}
    / {ADJ 'い' #ADJRentai}
    / {ADJ #ADJGokan}


/*
形容動詞の語幹と活用語尾
形容詞とやっていることは同じ
語幹は辞書の"Noun.adjv.dic"から生成
*/
AdjectiveVerb =
    / {ADJV 'だろ' #ADJVMizen}
    / {ADJV 'だっ' #ADJVRenyo}
    / {ADJV 'なら' #ADJVKatei}
    / {ADJV 'で' #ADJVRenyo}
    / {ADJV 'に' #ADJVRenyo}
    / {ADJV 'だ' #ADJVSyushi}
    / {ADJV 'な' #ADJVRentai}
    / {ADJV #ADJVGokan}

/*
補助動詞の「ある」
補助形容詞の「ない」
*/
SUBARU = 'あ' [らりるれろっ]
SUBNAI = 'な' ('かろ' / 'かっ' / 'く' / 'い' / 'けれ')


/*
名詞
漢字カタカナアルファベット数字
混合した場合も可（例：アップル社）
括弧で括ったものも名詞
*/
Noun =
    / { BLOCK #Block }
    / { NOUN #Noun }
    // / { KATA #KATA }
    // / { ALPHA #ALPHA }
    // / { NUM [つこ個]? [め目]? #Num }
    // / { ('ひと'/'ふた'/'みっ'/'よっ'/'いつ'/'むっ'/'なな'/'やっ'/'ここの') 'つ' [め目]? #Count }
    // / { SYMBOL #SYMBOL }


BLOCK =
    / '\[' (!'\]' .)* '\]'
    / '\{' (!'\}' .)* '\}'
    / '\(' (!'\)' .)* '\)'
    / '\"' (!'\"' .)* '\"'
    / '\'' (!'\'' .)* '\''
    / '\<' (!'\>' .)* '\>'
    / '\（' (!'\）' .)* '\）'
    / '\「' (!'\」' .)* '\」'
    / '\『' (!'\』' .)* '\』'
    / '\“' (!'\”' .)* '\”'
    / '\‘' (!'\’' .)* '\’'
    / '\【' (!'\】' .)* '\】'
    / '\［' (!'\］' .)* '\］'
    / '\｛' (!'\｝' .)* '\｝'
    / '\〔' (!'\〕' .)* '\〕'


/* 助詞、手入力 */
Postp = { POSTP #Postp }

/* 連体詞、辞書から生成 */
Adnominal = { ADNM #Adnominal }

/* 副詞、辞書から生成 */
Adverb = { ADV #Adverb }

/* 接続詞、辞書から生成 */
Conjunction = { CONJ #Conjunction }

/* 感動詞、辞書から生成 */
Interjection = { INTJ #Interjection }

AuxVerb =
    / {'そうだろ' #Mizen}
    / {'そうだっ' #Renyo}
    / {'そうなら' #Katei}
    / {'ようだろ' #Mizen}
    / {'ようだっ' #Renyo}
    / {'ようなら' #Katei}
    / {'らしかっ' #Renyo}
    / {'らしけれ' #Katei}
    / {'させる' #SyushiRentai}
    / {'させれ' #Katei}
    / {'させろ' #Meirei}
    / {'させよ' #Meirei}
    / {'られる' #SyushiRentai}
    / {'られれ' #Katei}
    / {'られろ' #Meirei}
    / {'られよ' #Meirei}
    / {'なかろ' #Mizen}
    / {'なかっ' #Renyo}
    / {'なけれ' #Katei}
    / {'たかろ' #Mizen}
    / {'たかっ' #Renyo}
    / {'たけれ' #Katei}
    / {'たがら' #Mizen}
    / {'たがり' #Renyo}
    / {'たがっ' #Renyo}
    / {'たがる' #SyushiRentai}
    / {'たがれ' #Katei}
    / {'そうで' #Renyo}
    / {'そうに' #Renyo}
    / {'そうだ' #Syushi}
    / {'そうな' #Rentai}
    / {'ようで' #Renyo}
    / {'ように' #Renyo}
    / {'ようだ' #Syushi}
    / {'ような' #Rentai}
    / {'らしく' #Renyo}
    / {'らしい' #SyushiRentai}
    / {'ましょ' #Mizen}
    / {'でしょ' #Mizen}
    / {'ますれ' #Katei}
    / {'せる' #SyushiRentai}
    / {'せれ' #Katei}
    / {'せろ' #Meirei}
    / {'せよ' #Meirei}
    / {'させ' #MizenRenyo}
    / {'れる' #SyushiRentai}
    / {'れれ' #Katei}
    / {'れろ' #Meirei}
    / {'れよ' #Meirei}
    / {'なく' #Renyo}
    / {'ない' #SyushiRentai}
    / {'よう' #SyushiRentai}
    / {'まい' #SyushiRentai}
    / {'られ' #MizenRenyo}
    / {'たく' #Renyo}
    / {'たい' #SyushiRentai}
    / {'たろ' #Mizen}
    / {'たら' #Katei}
    / {'だろ' #Mizen}
    / {'だら' #Katei}
    / {'ませ' #MizenMeirei}
    / {'まし' #RenyoMeirei}
    / {'ます' #SyushiRentai}
    / {'だっ' #Renyo}
    / {'なら' #Katei}
    / {'でし' #Renyo}
    / {'です' #SyushiRentai}
    / {'せ' #MizenRenyo}
    / {'れ' #MizenRenyo}
    / {'ず' #Renyo}
    / {'ぬ' #SyushiRentai}
    / {'ね' #Katei}
    / {'ん' #SyushiRentai}
    / {'う' #SyushiRentai}
    / {'た' #SyushiRentai}
    / {'だ' #SyushiRentai}
    / {'で' #Renyo}
    / {'な' #Rentai}




NOUN = @choice('dic/TestNoun.txt')
VERB5KA = @choice('dic/Verb/VERB5KA.txt')
VERB5SA = @choice('dic/Verb/VERB5SA.txt')
VERB5TA = @choice('dic/Verb/VERB5TA.txt')
VERB5NA = @choice('dic/Verb/VERB5NA.txt')
VERB5MA = @choice('dic/Verb/VERB5MA.txt')
VERB5RA = @choice('dic/Verb/VERB5RA.txt')
VERB5WA = @choice('dic/Verb/VERB5WA.txt')
VERB5GA = @choice('dic/Verb/VERB5GA.txt')
VERB5BA = @choice('dic/Verb/VERB5BA.txt')
VERB1 = @choice('dic/Verb/VERB1.txt')
KAHEN = @choice('dic/Verb/KAHEN.txt')
SAHEN_SURU = @choice('dic/Verb/SAHEN_SURU.txt')
SAHEN_ZURU = @choice('dic/Verb/SAHEN_ZURU.txt')
VERB5KASOKU = '行'
ADJ = @choice('dic/ADJ.txt')
ADJV = @choice('dic/ADJV.txt')
POSTP = @choice('dic/POSTP.txt')
ADNM = @choice('dic/ADNM.txt')
ADV = @choice('dic/ADV.txt')
CONJ = @choice('dic/CONJ.txt')
INTJ = @choice('dic/INTJ.txt')
